# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           github 1.0

PortGroup           save_configure_cmd 1.0
if {${os.platform} eq "darwin"} {
    if {${os.major} < 20} {
        PortGroup   code_sign 1.0
    }
}

github.setup        aclap-dev vdhcoapp f8cc614622eb70e78c575dcbdeb3e0b4b947497f
distname            ${name}-git
name                videodownloadhelper-coapp
version             2.0.20.4
fetch.type          git

description         companion application for the Video DownloadHelper browser add-on 

long_description    Video DownloadHelper CoApp is a multi-platform application providing \
                    the Video DownloadHelper browser add-on with a set of extra features \
                    not available directly from the add-on

categories          www net
platforms           darwin linux
license             GPL-2.0

installs_libs 		no
use_configure       no

maintainers         nomaintainer

variant upstream_nodejs_pkg description {for recent OS versions (>= 10.13 for Mac): let the build download the upstream patched node18 instead of having to do the expensive local build} {}

configure.save_build_cmd "install"

depends_build-append \
                    port:esbuild \
                    port:nodejs18 \
                    port:npm9 \
                    port:yq \
                    port:coreutils \
                    port:filepicker

depends_lib-append  port:ffmpeg6

patch.pre_args      -Np1

if {${os.platform} ne "darwin"} {
    # let's just use our own patched nodejs18 instead of hoping the upstream one works!
    if {[variant_isset upstream_nodejs_pkg]} {
        patchfiles-append \
                    patch-build-@PREFIX@.diff
    } else {
        depends_patch-append \
                    port:nodejs18-pkg
        depends_build-append \
                    port:nodejs18-pkg
        patchfiles-append \
                    patch-build-@PREFIX@+@PKG_NODE_PATH@.diff
    }
    # we're not making an app-bundle here but installing directly into $prefix 
    depends_run-append \
                    port:filepicker
} elseif {${os.major} < 17 && ![variant_isset upstream_nodejs_pkg]} {
    # current minimum supported Darwin version by @yao-pkg/pkg is 17 (10.13)
    # so there we need to use a self-built patched node18 to create a standalone executable.
    depends_patch-append \
                    port:nodejs18-pkg
    depends_build-append \
                    port:nodejs18-pkg
    patchfiles-append \
                    patch-build-@PREFIX@+@PKG_NODE_PATH@.diff
} else {
    if {${os.major} < 17} {
        ui_warn "The build is bound to fail OR the resulting application will be crippled!"
    }
    patchfiles-append \
                    patch-build-@PREFIX@.diff
}

patchfiles-append   patch-@VERSION@.diff

post-patch {
    reinplace "s|@PREFIX@|${prefix}|g" ${worksrcpath}/build.sh
    reinplace "s|@BUILD_ARCH@|${build_arch}|g" ${worksrcpath}/build.sh
    # the browser add-on only supports M.m.p version strings
    set supported_version [join [lrange [split ${version} "."] 0 2] "."]
    reinplace "s|@VERSION@|${supported_version}|g" ${worksrcpath}/config.toml
    if {![variant_isset upstream_nodejs_pkg]} {
        if {${os.platform} eq "darwin"} {
            set njsp [glob ${prefix}/libexec/nodejs-pkg/built-v18*-macos-${build_arch}]
        } else {
            set njsp [glob ${prefix}/libexec/nodejs-pkg/built-v18*-${os.platform}-${build_arch}]
        }
        ui_info "--->  Using patched node18 ${njsp}"
        reinplace -q "s|@PKG_NODE_PATH@|${njsp}|g" ${worksrcpath}/build.sh
    }
    # install the older icon that works on older Mac OS versions too
    xinstall -m 644 ${filespath}/icon.icns ${worksrcpath}/assets/mac/
}

build.pre_args
build.post_args
build.cmd           ./build.sh

build.args          --skip-notary
if {${os.platform} eq "darwin" && ${os.major} < 20} {
    build.args-append \
                    --skip-signing
}

destroot {
    if {${os.platform} eq "darwin"} {
        file copy ${worksrcpath}/dist/mac/${build_arch}/dotApp/net.downloadhelper.coapp.app \
            ${destroot}${applications_dir}/
        if {${os.platform} eq "darwin" && ${os.major} < 20} {
            codesign ${destroot}${applications_dir}/net.downloadhelper.coapp.app
        }
    } else {
        xinstall -m 755 ${worksrcpath}/dist/${os.platform}/${build_arch}/vdhcoapp \
            ${destroot}${prefix}/bin/
        xinstall -m 755 ${worksrcpath}/dist/${os.platform}/${build_arch}/deb/opt/vdhcoapp/xdg-open \
            ${destroot}${prefix}/bin/
    }
}

if {${os.platform} eq "darwin"} {
    notes-append    "Remember to launch ${applications_dir}/net.downloadhelper.coapp.app once to register the helper application!"
} else {
    notes-append    "Remember to run `${prefix}/bin/vdhcoapp install` once to register the helper application!"
}
