# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem                      1.0
PortGroup                       clang_dependency 1.0

# Increase the revision of p5-www-curl whenever the version of curl gets updated.

name                            curl
version                         8.12.1
checksums                       rmd160  c29a185531db8a3983ff146e1b682ee408b0b257 \
                                sha256  0341f1ed97a26c811abaebd37d62b833956792b7607ea3f15d001613c76de202
#                                 size    2768160

categories                      net www
platforms                       darwin freebsd
maintainers                     {ryandesign @ryandesign}
license                         Curl

description                     Tool for transferring files with URL syntax

long_description                curl is a client to get documents/files from servers, \
                                using any of the supported protocols. The command is \
                                designed to work without user interaction or any kind \
                                of interactivity.

homepage                        https://curl.se
master_sites                    ${homepage}/download/:curl \
                                https://curl.askapache.com/:curl

use_xz                          yes
set curl_distfile               ${distfiles}
distfiles                       ${curl_distfile}:curl
checksums-prepend               ${curl_distfile}

subport MacPorts-libcurl {
    description                 libcurl built for use by MacPorts itself
    long_description            {*}${description}. This port is a dependency of \
                                port:MacPorts-pextlib(-devel) and doesn't make sense without it.
}
if {${subport} ne "MacPorts-libcurl" && ${subport} ne "curl-ca-bundle"} {
    PortGroup                   devport 1.0
    ## NB NB
    ## keep the variant list below in sync with the variants defined for the mainport!
    devport_variants            ares brotli darwinssl gnutls gsasl gss http2 http3 idn \
                                libre mbedtls openldap psl rtmp sftp_scp ssl standard \
                                use_lld wolfssl zstd
    create_devport              port:${name}
}

if {${name} eq ${subport} || ${subport} eq "MacPorts-libcurl" || ${subport} eq "${name}-dev"} {
    PortGroup                   muniversal 1.0
    PortGroup                   LTO 1.0
    PortGroup                   save_configure_cmd 1.0


    depends_build               path:bin/pkg-config:pkgconfig

    depends_lib                 port:zlib

    patchfiles-append           managen.patch

    configure.args              --disable-silent-rules \
                                --enable-ipv6 \
                                --without-brotli \
                                --without-gnutls \
                                --without-gssapi \
                                --without-libgsasl \
                                --without-libidn2 \
                                --without-libpsl \
                                --without-librtmp \
                                --without-libssh2 \
                                --without-mbedtls \
                                --without-nghttp2 \
                                --without-nghttp3 \
                                --without-ngtcp2 \
                                --without-openssl \
                                --without-ssl \
                                --without-secure-transport \
                                --without-wolfssl \
                                --without-zstd \
                                --disable-ares \
                                --disable-ldap \
                                --disable-ldaps \
                                --with-zlib=${prefix} \
                                ac_cv_prog_AWK=/usr/bin/awk

    platform darwin {
        configure.cflags-append -mmacosx-version-min=${macosx_deployment_target}
    }

    configure.checks.implicit_function_declaration.whitelist-append strchr

    if {${os.platform} eq "darwin" && ${os.major} < 10} {
        depends_build-append    port:cctools
        configure.env-append    NM=${prefix}/bin/nm
        configure.args-append   lt_cv_path_NM=${prefix}/bin/nm
    }

    post-configure {
        if {[variant_exists universal] && [variant_isset universal]} {
            set dirs {}
            foreach arch ${universal_archs_to_use} {
                lappend dirs ${worksrcpath}-${arch}
            }
        } else {
            set dirs ${worksrcpath}
        }
        foreach dir ${dirs} {
            reinplace -E {s|-arch [a-z0-9_]+||g} ${dir}/curl-config
            # These flags only ends up in curl-config in some cases, such as
            # when "cross compiling" a universal binary.
            # See https://trac.macports.org/ticket/24001
            reinplace -E -q {s/ '(host_alias|--host)=[^']+'//g} ${dir}/curl-config
        }
    }

    test.run                    yes
    test.target                 test-full
    test.args                   -j${build.jobs} TFLAGS=-j${build.jobs}

    global merger_dont_diff
    set merger_dont_diff "${prefix}/include/curl/curlbuild.h"

    configure.save_configure_cmd "install log"

    #############################################################################
    ### NB no code that should NOT apply to port:MacPorts-libcurl above this line
    ###
    if {${subport} eq "MacPorts-libcurl"} {
        PortGroup               conflicts_build 1.0

        depends_build-delete    port:pkgconfig
        depends_build-append    port:pkgconfig 
        if {${os.platform} eq "darwin" && ${os.major} >= 10} {
            # darwin < 10 already depends on cctools
            depends_build-append \
                                port:cctools
        } elseif {${os.platform} eq "linux"} {
            depends_build-append \
                                bin:patchelf:patchelf
        }

        depends_lib-append      port:libressl-co \
                                port:zstd \
                                path:share/curl/curl-ca-bundle.crt:curl-ca-bundle

        configure.args-delete   --without-ssl
        configure.args-replace  --without-openssl --with-openssl
        configure.args-replace  --without-zstd --with-zstd
        configure.args-append   --with-ca-bundle=${prefix}/share/curl/curl-ca-bundle.crt
        # The host curl on 10.9.5 supports GSS so we may want to do that too.
        configure.args-replace  --without-gssapi --with-gssapi
        # however I don't see why we could not depend on our own gss??
        depends_lib-append      port:gss

        configure.pkg_config_path-prepend \
                                ${prefix}/libexec/libressl/lib/pkgconfig
        if {${os.platform} eq "darwin"} {
            conflicts_build     openssl
        } else {
            conflicts_build     openssl-dev
        }

        set thePrefix           ${prefix}/libexec/macports/lib/pextlib1.0
        configure.pre_args      --prefix=${thePrefix}

        # we only want libcurl, so tell `make` to chdir into the lib subdir
        pre-build {
            build.pre_args-prepend \
                                -C ${worksrcpath}/lib
        }

        destroot {
            xinstall -m 755 -d ${destroot}${thePrefix}
            if {${os.platform} eq "darwin"} {
                xinstall -m 755 ${worksrcpath}/lib/.libs/libcurl.4.dylib \
                                ${destroot}${thePrefix}/libcurl-mp.dylib
                system "install_name_tool -id ${thePrefix}/libcurl-mp.dylib ${destroot}${thePrefix}/libcurl-mp.dylib"
            } else {
                xinstall -m 755 ${worksrcpath}/lib/.libs/libcurl.so.4.8.0 \
                                ${destroot}${thePrefix}/libcurl-mp.so
                system "patchelf --set-soname libcurl-mp.so ${destroot}${thePrefix}/libcurl-mp.so"
            }
            xinstall -m 755 -d ${destroot}${prefix}/share/doc/${subport}
            xinstall -m 755 ${worksrcpath}/curl-config ${destroot}${prefix}/share/doc/${subport}
        }

        pre-test {
            ui_msg "---->  Completing the build first"
            system -W ${build.dir} "${build.cmd} ${build.pre_args}"
            ui_msg "---->  Building and executing tests"
        }

        if {![file exists ${prefix}/libexec/macports/lib/pextlib1.0/pkgIndex-mp.tcl]} {
            notes-append "This port is only useful if port:MacPorts-pextlib(-devel) is also installed!"
        }

        return
    }
    ###
    ### NB no code that should ALSO apply to port:MacPorts-libcurl below this line
    ##############################################################################

    if {![is_mainport]} {
        # nothing else to be done
        return
    }

    post-destroot {
        set docdir ${prefix}/share/doc/${name}
        xinstall -d ${destroot}${docdir}/html/libcurl ${destroot}${docdir}/pdf/libcurl \
            ${destroot}${prefix}/share/aclocal
        xinstall -m 0644 -W ${worksrcpath} \
            CHANGES.md \
            COPYING \
            README \
            RELEASE-NOTES \
            ${destroot}${docdir}
        xinstall -m 0644 -W ${worksrcpath}/docs \
            BINDINGS.md \
            BUGS.md \
            CIPHERS.md \
            CODE_OF_CONDUCT.md \
            CONTRIBUTE.md \
            CURL-DISABLE.md \
            DEPRECATE.md \
            FAQ \
            FEATURES.md \
            GOVERNANCE.md \
            HELP-US.md \
            HISTORY.md \
            HTTP-COOKIES.md \
            INTERNALS.md \
            KNOWN_BUGS \
            MAIL-ETIQUETTE.md \
            ROADMAP.md \
            SECURITY-ADVISORY.md \
            SSL-PROBLEMS.md \
            SSLCERTS.md \
            THANKS \
            TODO \
            TheArtOfHttpScripting.md \
            URL-SYNTAX.md \
            VERSIONS.md \
            VULN-DISCLOSURE-POLICY.md \
            ${destroot}${docdir}
        if {[variant_isset http3]} {
            xinstall -m 0644 -W ${worksrcpath}/docs \
                HTTP3.md \
                ${destroot}${docdir}
        }
        xinstall -m 0644 ${worksrcpath}/docs/libcurl/libcurl.m4 \
            ${destroot}${prefix}/share/aclocal/

        register_devport_standard_content
        create_devport_content_archive
    }

    variant ares description {Support resolving names asynchronously} {
        depends_lib-append      port:c-ares
        configure.args-replace  --disable-ares --enable-ares
    }

    variant brotli description {Support brotli compression} {
        depends_lib-append      port:brotli
        configure.args-replace  --without-brotli --with-brotli
    }

    variant darwinssl conflicts gnutls mbedtls ssl wolfssl description {Allow secure connections using Apple OS native TLS} {
        configure.args-delete   --without-ssl
        configure.args-replace  --without-secure-transport --with-secure-transport
        configure.args-append   --without-ca-bundle
    }

    variant gnutls conflicts darwinssl mbedtls ssl wolfssl description {Allow secure connections using GNU TLS} {
        depends_lib-append      path:lib/pkgconfig/gnutls.pc:gnutls \
                                path:share/curl/curl-ca-bundle.crt:curl-ca-bundle
        configure.args-delete   --without-ssl
        configure.args-replace  --without-gnutls --with-gnutls
        configure.args-append   --with-ca-bundle=${prefix}/share/curl/curl-ca-bundle.crt
    }

    variant mbedtls conflicts darwinssl gnutls ssl wolfssl description {Allow secure connections using mbed TLS (formerly PolarSSL)} {
        depends_lib-append      port:mbedtls \
                                path:share/curl/curl-ca-bundle.crt:curl-ca-bundle
        configure.args-delete   --without-ssl
        configure.args-replace  --without-mbedtls --with-mbedtls
        configure.args-append   --with-ca-bundle=${prefix}/share/curl/curl-ca-bundle.crt
    }

    variant wolfssl conflicts darwinssl mbedtls gnutls ssl description {Allow secure connections using wolfSSL (formerly CyaSSL)} {
        depends_lib-append      port:wolfssl \
                                path:share/curl/curl-ca-bundle.crt:curl-ca-bundle
        configure.args-delete   --without-ssl
        configure.args-replace  --without-wolfssl --with-wolfssl
        configure.args-append   --with-ca-bundle=${prefix}/share/curl/curl-ca-bundle.crt
    }

    variant gsasl description {Support SCRAM-SHA-1 and SCRAM-SHA-256 with libgsasl} {
        depends_lib-append      port:libgsasl
        configure.args-replace  --without-libgsasl --with-libgsasl
    }

    variant gss description {Support the Generic Security Service API} {
        # This needs to use the system's Kerberos, not MacPorts' gss or kerberos5.
        ## WHY?
        #conflicts-append        gss kerberos5
        depends_lib-append      port:gss \
                                port:kerberos5
        configure.args-replace  --without-gssapi --with-gssapi
    }

    variant http2 description {Support HTTP/2} {
        depends_lib-append      port:nghttp2
        configure.args-replace  --without-nghttp2 --with-nghttp2=${prefix}
    }

    variant http3 requires gnutls description {Support HTTP/3 with nghttp3 and ngtcp2} {
        depends_lib-append      port:nghttp3 \
                                port:ngtcp2
        configure.args-replace  --without-nghttp3 --with-nghttp3=${prefix} \
                                --without-ngtcp2 --with-ngtcp2=${prefix}
    }

    variant idn description {Support internationalized domain names} {
        depends_lib-append      port:libidn2
        configure.args-replace  --without-libidn2 --with-libidn2=${prefix}
    }

    variant openldap description {Support performing Lightweight Directory Access Protocol queries with OpenLDAP} {
        depends_lib-append      path:lib/libldap.dylib:openldap
        configure.args-replace  --disable-ldap --enable-ldap
        configure.args-replace  --disable-ldaps --enable-ldaps
    }

    # Curl's psl support does not require its idn support but the libpsl port
    # depends on the libidn2 port so we might as well enable curl's idn support.
    variant psl requires idn description {Use the public suffix list to avoid privacy-leaking "supercookies"} {
        depends_lib-append      port:libpsl
        configure.args-replace  --without-libpsl --with-libpsl
    }

    variant rtmp description {Support RTMP media streams} {
        depends_lib-append      port:rtmpdump
        configure.args-replace  --without-librtmp --with-librtmp
    }

    variant sftp_scp description {Support SFTP/SCP connections via libssh2} {
        depends_lib-append      port:libssh2
        configure.args-replace  --without-libssh2 --with-libssh2
    }

    variant ssl conflicts darwinssl gnutls mbedtls wolfssl description {Allow secure connections using OpenSSL} {
        depends_lib-append      path:lib/libssl.dylib:openssl \
                                path:share/curl/curl-ca-bundle.crt:curl-ca-bundle
        configure.args-delete   --without-ssl
        configure.args-replace  --without-openssl --with-openssl
        configure.args-append   --with-ca-bundle=${prefix}/share/curl/curl-ca-bundle.crt
    }

    variant libre requires ssl description {Use the LibreSSL instead of OpenSSL} {
        PortGroup               conflicts_build 1.0
        depends_lib-replace     path:lib/libssl.dylib:openssl \
                                port:libressl-co
        configure.pkg_config_path-prepend \
                                ${prefix}/libexec/libressl/lib/pkgconfig
        depends_build-delete    port:pkgconfig
        depends_build-append    port:pkgconfig
        if {${os.platform} eq "darwin"} {
            conflicts_build     openssl
        } else {
            conflicts_build     openssl-dev
        }
    }

    variant zstd description {Support zstd compression} {
        depends_lib-append      port:zstd
        configure.args-replace  --without-zstd --with-zstd
    }

    set standard_variant_list [list brotli http2 idn psl zstd]
    variant standard conflicts ${standard_variant_list} description {Equivalent of +brotli+http2+idn+psl+zstd} {
        depends_lib-append      port:brotli
        depends_lib-append      port:nghttp2
        depends_lib-append      port:libidn2
        depends_lib-append      port:libpsl
        depends_lib-append      port:zstd
        configure.args-replace  --without-brotli --with-brotli
        configure.args-replace  --without-nghttp2 --with-nghttp2=${prefix}
        configure.args-replace  --without-libidn2 --with-libidn2=${prefix}
        configure.args-replace  --without-libpsl --with-libpsl
        configure.args-replace  --without-zstd --with-zstd
    }

    set needs_standard_variant yes
    foreach v ${standard_variant_list} {
        if {[variant_isset ${v}]} {
            set needs_standard_variant no
            break
        }
    }
    if {[tbool needs_standard_variant]} {
        default_variants-append +standard
    }

    if {![variant_isset darwinssl] && ![variant_isset gnutls] && ![variant_isset mbedtls] && ![variant_isset wolfssl]} {
        default_variants-append +ssl
    }

    if {[variant_isset darwinssl] || [variant_isset mbedtls]} {
        notes-append {
            The selected TLS library does not support TLS 1.3; you may want to\
            use one that does by choosing the +ssl, +gnutls, or +wolfssl variant.
        }
    }

    livecheck.type              regex
    livecheck.url               [join [lrange [split [lindex ${master_sites} 0] {:}] 0 end-1] {:}]
    livecheck.regex             ${name}-(\[0-9.\]+)[quotemeta ${extract.suffix}]
} else {
    livecheck.type              none
}

subport curl-ca-bundle {
    # Also increase the revision of privoxy-pki-bundle whenever curl-ca-bundle contents change.
#     revision                    0
    categories                  net
    license                     {MPL-2 LGPL-2.1+}
    supported_archs             noarch
    platforms                   any
    installs_libs               no
    conflicts                   certsync

    description                 CA certificate bundle for curl

    long_description            Installs a bundle of certification authority certificates \
                                (CA certs) which curl (when linked with OpenSSL) uses to \
                                verify the authenticity of secure web and FTP servers.

    depends_extract-append      bin:unzip:unzip

    if {${os.platform} eq "darwin" && ${os.major} <= 10} {
        # Day too big - 24854 > 24853
        # Cannot handle date (59, 59, 23, 18, 0, 2038) at /System/Library/Perl/5.10.0/darwin-thread-multi-2level/Time/Piece.pm line 315
        depends_build-append    port:perl5
    } else {
        depends_build-append    bin:perl:perl5
    }

    # The approximate time (in seconds since the epoch) when the port maintainer
    # updated the certdata.txt file in this port. (The output of "date +%s".)
    set certdata_updated        1748031890
    # The upstream commit in which certdata.txt was last updated.
    set certdata_commit         ac1b812c716e075be32f87b3ec78feec1cbd388b
    # The date (in YYYYMMDD format) that commit was pushed.
    set certdata_date           20250424
    set certdata_file           certdata.txt
    # Using tar.bz2 would be preferable because it's smaller but upstream has
    # disabled the creation of all but zip archives:
    # https://bugzilla.mozilla.org/show_bug.cgi?id=1596135
    set certdata_extract_suffix .zip
    set certdata_distfile       certdata-${certdata_date}-${certdata_commit}${certdata_extract_suffix}
    set certdata_path           security/nss/lib/ckfw/builtins/${certdata_file}

    # Only try to fetch from upstream shortly after the port is updated,
    # to reduce load on their server:
    # https://bugzilla.mozilla.org/show_bug.cgi?id=1596135
    if {[clock seconds] - ${certdata_updated} < 86400} {
        master_sites-append     https://hg.mozilla.org/mozilla-central/archive/${certdata_commit}${certdata_extract_suffix}/${certdata_path}?dummy=:certdata
    } else {
        master_sites-append     macports_distfiles::certdata
        # but still try their site in case something went wrong:
        master_sites-append     https://hg.mozilla.org/mozilla-central/archive/${certdata_commit}${certdata_extract_suffix}/${certdata_path}?dummy=:certdata
    }

    distfiles-append            ${certdata_distfile}:certdata

    checksums-append            ${certdata_distfile} \
                                rmd160  a8e6209aff4e814bded7549643217e85e272099c \
                                sha256  2db967de182cd27ccd0065c4af308789dfb88ed4e83bfceb3c4124cabd009ce8
#                                 size    293311

    extract.only                ${curl_distfile}
    extract.post_args-append    ${worksrcdir}/scripts/mk-ca-bundle.pl

    set worksrcpath             ${workpath}/${name}-${version}

    post-extract {
        if {[catch {system -W ${workpath} "unzip -q -a [shellescape ${distpath}/${certdata_distfile}]"} err]} {
            # see https://github.com/RJVB/macstrop/issues/113
            ui_error "unzip -q -a [shellescape ${distpath}/${certdata_distfile}] failed: ${err}"
            return -code error "Please install port:unzip and try again"
        }
        move ${workpath}/mozilla-central-${certdata_commit}/${certdata_path} ${worksrcpath}/${certdata_file}
        file mkdir ${worksrcpath}/lib
    }

    version                     ${certdata_date}
    use_configure               no

    build.cmd                   scripts/mk-ca-bundle.pl
    build.args                  -n lib/ca-bundle.crt
    build.target

    destroot {
        set ca_bundle_dir ${prefix}/share/curl
        set openssl_dir ${prefix}/etc/openssl
        xinstall -d ${destroot}${ca_bundle_dir} ${destroot}${openssl_dir}
        xinstall -m 644 ${worksrcpath}/lib/ca-bundle.crt ${destroot}${ca_bundle_dir}/curl-ca-bundle.crt
        ln -s ${ca_bundle_dir}/curl-ca-bundle.crt ${destroot}${openssl_dir}/cert.pem
    }

    livecheck.type              regexm
    livecheck.url               https://hg.mozilla.org/mozilla-central/log/default/${certdata_path}
#   manual check:
#   https://hg.mozilla.org/mozilla-central/log/tip/security/nss/lib/ckfw/builtins/certdata.txt
    livecheck.version           [regsub {(....)(..)(..)} ${certdata_date} {\1-\2-\3}]
    livecheck.regex             {[0-9a-f]+<br/>created <i>[^<]+</i>\n<br/>pushed <i>([0-9-]+)}
}
