# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:filetype=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           conflicts_build 1.0
PortGroup           active_variants 1.1
platform darwin {
    PortGroup       cxx11 1.1
}

name                audacious-plugins
subport             audacious-plugins-devel {}

# Please keep audacious, audacious-core and audacious-plugins synchronized.
version             3.8

# FIXME: probably more licenses involved here...
license             BSD GPL-2+
categories          multimedia
platforms           darwin
maintainers         ionic
homepage            http://www.audacious-media-player.org/
description         Adds I/O, audio decoding, audio transforming and UI plugins.
long_description    This ports bundles most of the functionality for audacious. \
                    ${description}

# Maintainer-only helper for testing changes quickly and easily.
#fetch.type          git
#git.url             https://github.com/Ionic/audacious-plugins
#git.branch          ${name}-${version}-buildfix

master_sites        http://distfiles.audacious-media-player.org

if {${subport} eq "${name}-devel"} {
    version         3.9.120
    fetch.type      git
    # we use my fork while my patches are being upstreamed.
    git.url         https://github.com/RJVB/${name}
    # master audacious-plugins-3.9-75-g1107598c8 / RJVB audacious-plugins-3.9-120-g1bbd88c22
    git.branch      1bbd88c22b1fb80c0fc3f3c657dd7db1550c2e08
    worksrcdir      ${name}-git
    distname        ${name}-git
    conflicts-append ${name} audacious-core
    depends_lib-append \
                    port:audacious-core-devel
} else {
    revision        1
    use_bzip2       yes
    checksums       rmd160  a9599198be430218fcfda4184222df7fcaf8398e \
                    sha256  8179b80fffc893d4a2533fc7b7c4800981bc9ab3c6d1742a4c832fd6176ca24a
    # really?
    # conflicts_build     ${name}
    conflicts-append ${name}-devel audacious-core-devel
    depends_lib-append \
                    port:audacious-core
}

# make sure there's no other major version installed
proc sanitise_build_env {first args} {
    global version
    set branch [join [lrange [split ${version} .] 0 1] .]
    set args [linsert $args[set list {}] 0 ${first}]
    foreach name ${args} {
        if {![catch {set installed [lindex [registry_active ${name}] 0]}]} {
            set _version [lindex $installed 1]
            set _branch [join [lrange [split ${_version} .] 0 1] .]
            if {[vercmp $branch $_branch] != 0} {
                ui_warn "${name} v${_version} cannot be active to build v${version}"
                registry_deactivate_composite ${name} "" [list ports_nodepcheck 1]
            } else {
                ui_debug "${name} v${_version}/${_branch} doesn't bite v${version}/${branch}"
            }
        } else {
            ui_debug "${name} not active or not installed"
        }
    }
}

pre-configure {
    sanitise_build_env \
                    audacious-core \
                    audacious-core-devel \
                    ${name} \
                    ${name}-devel
}
pre-build {
    sanitise_build_env \
                    audacious-core \
                    audacious-core-devel \
                    ${name} \
                    ${name}-devel
}


universal_variant   no

if {${subport} ne "${name}-devel"} {
    patchfiles      patch-update-buildsys-and-make-verbose.diff \
                    patch-drop-libc++.diff
} else {
#     patchfiles      patch-verbose-build.diff \
#                     patch-searchtool-numresults.diff
}
patchfiles-append   patch-fix-coreaudio.diff \
                    devel/patch-bitperfect-coreaudio.diff

depends_lib-append  port:gettext\
                    port:libxml2 \
                    path:lib/pkgconfig/glib-2.0.pc:glib2

options host_dependencies
default host_dependencies {}

proc depends_lib_darwin {args} {
    global os.platform
    if {${os.platform} eq "darwin"} {
        uplevel depends_lib-append ${args}
    } else {
        ui_debug "host depends_lib: ${args}"
        uplevel host_dependencies-append ${args}
    }
}
proc depends_build_darwin {args} {
    global os.platform
    if {${os.platform} eq "darwin"} {
        uplevel depends_build-append ${args}
    } else {
        ui_debug "host depends_build: ${args}"
        uplevel host_dependencies-append ${args}
    }
}

depends_lib_darwin  port:neon
platform darwin {
    depends_run     port:unzip
}

# Note: rpath is required on Mac OS X.
#       sdlout is the only working audio output plugin at the moment on OS X.
#       coreaudio is always enabled on OS X.
#       Try PulseAudio at your own risk.
#       --with-system-libxml2 enables or disables usage of libxml2 in /usr.
#       Play it safe and use our libxml2 port.
configure.args      --enable-nls \
                    --disable-gtk \
                    --disable-qt \
                    --with-system-libxml2=no \
                    --disable-hotkey \
                    --enable-songchange \
                    --enable-neon \
                    --enable-rpath \
                    --disable-console \
                    --disable-qtaudio \
                    --disable-pulse \
                    --enable-coreaudio \
                    --disable-sdlout \
                    --disable-mp3 \
                    --disable-gnomeshortcuts \
                    --disable-lirc \
                    --disable-aosd \
                    --disable-aosd-xcomp \
                    --disable-notify \
                    --disable-mpris2 \
                    --disable-vorbis \
                    --disable-flac \
                    --disable-wavpack \
                    --disable-aac \
                    --disable-sndfile \
                    --disable-modplug \
                    --with-ffmpeg=none \
                    --disable-jack \
                    --disable-sid \
                    --disable-oss4 \
                    --disable-alsa \
                    --disable-sndio \
                    --disable-amidiplug \
                    --disable-cdaudio \
                    --disable-scrobbler2 \
                    --disable-ampache \
                    --disable-mms \
                    --disable-cue \
                    --disable-filewriter \
                    --disable-filewriter_mp3 \
                    --disable-bs2b \
                    --disable-resample \
                    --disable-speedpitch \
                    --disable-soxr \
                    --disable-glspectrum \
                    --disable-qtglspectrum \
                    --disable-mac-media-keys

if {![variant_isset altfull]} {
    default_variants-append +full
}

if {![file exists ${worksrcpath}/config.log]} {
    use_autoreconf  yes
}
autoreconf.cmd      ./autogen.sh
autoreconf.args

depends_build       path:bin/pkg-config:pkgconfig \
                    path:bin/aclocal:automake \
                    path:bin/autom4te:autoconf

post-destroot {
    xinstall -d -m 0755 ${destroot}${prefix}/share/doc/${name}
    xinstall -m 0644 ${worksrcpath}/COPYING ${destroot}${prefix}/share/doc/${name}
}

variant console description {Add console game music decoder} {
    depends_lib-append      port:zlib
    configure.args-replace  --disable-console \
                            --enable-console
}

variant pulseaudio description {Add support for PulseAudio} {
    depends_lib_darwin      port:pulseaudio
    configure.args-replace  --disable-pulse \
                            --enable-pulse
}
if {${os.platform} ne "darwin"} {
    default_variants-append +pulseaudio
    conflicts_build-append  gettext-dev
    configure.args-replace  --disable-alsa \
                            --enable-alsa
}

variant mp3 description {Add support for reading MP3 files} {
    depends_lib_darwin      port:mpg123
    configure.args-replace  --disable-mp3 \
                            --enable-mp3
}

variant dbus description {Adds support for GNOME shortcuts and remote control via DBUS} {
    depends_build_darwin    path:bin/gdbus-codegen:glib2
    depends_lib_darwin      path:lib/pkgconfig/dbus-1.pc:dbus \
                            path:lib/pkgconfig/dbus-glib-1.pc:dbus-glib
    configure.args-replace  --disable-gnomeshortcuts \
                            --enable-gnomeshortcuts
    configure.args-replace  --disable-mpris2 \
                            --enable-mpris2
}

variant lirc description {Adds support for infrared devices via LIRC} {
    depends_lib-append      port:lirc
    configure.args-replace  --disable-lirc \
                            --enable-lirc
}

variant osd description {Adds OSD support via pangocairo} {
    depends_lib-append      port:xrender
    configure.args-replace  --disable-aosd \
                            --enable-aosd
}

variant osd_composite requires osd description {Enables X Composite support for OSD} {
    depends_lib-append      port:xorg-libXcomposite
    configure.args-replace  --disable-aosd-xcomp \
                            --enable-aosd-xcomp
}

variant notifications description {Adds support for notifications via libnotify} {
    depends_lib_darwin      port:libnotify
    configure.args-replace  --disable-notify \
                            --enable-notify
}

variant filewriter description {Add support for the filewriter output plugin} {
    configure.args-replace  --disable-filewriter \
                            --enable-filewriter
}

variant vorbis requires filewriter description {Add support for the OggVorbis audio codec} {
    depends_lib-append      port:libvorbis
    depends_lib-append      port:libogg
    configure.args-replace  --disable-vorbis \
                            --enable-vorbis

    configure.args-replace  --disable-filewriter \
                            --enable-filewriter
}

variant flac requires filewriter description {Add support for FLAC: Free Lossless Audio Codec} {
    depends_lib-append      port:flac
    configure.args-replace  --disable-flac \
                            --enable-flac

    configure.args-replace  --disable-filewriter \
                            --enable-filewriter
}

variant wavpack description {Add support for wavpack audio compression tools} {
    depends_lib_darwin      port:wavpack
    configure.args-replace  --disable-wavpack \
                            --enable-wavpack
}

variant aac description {Add support for MPEG-4 AAC Audio} {
    depends_lib_darwin      port:faad2
    configure.args-replace  --disable-aac \
                            --enable-aac
}

variant sndfile description {Add support for libsndfile} {
    depends_lib-append      port:libsndfile
    configure.args-replace  --disable-sndfile \
                            --enable-sndfile
}

variant modplug description {Add support for MOD audio codec} {
    depends_lib_darwin      port:libmodplug
    configure.args-replace  --disable-modplug \
                            --enable-modplug
}

variant ffmpeg conflicts sdl2 description {Add support for decoding audio streams via ffmpeg} {
    depends_lib-append      path:lib/libavcodec.dylib:ffmpeg
    configure.args-replace  --with-ffmpeg=none \
                            --with-ffmpeg=ffmpeg
}

variant jack description {Add support for the JACK Audio Connection Kit} {
    depends_lib-append      port:jack
    configure.args-replace  --disable-jack \
                            --enable-jack
}

# libsidplayfp not ported yet.
#variant sid description {Build with SID (Commodore 64 Audio) support} {
#    depends_lib-append      port:libsidplayfp
#    configure.args-replace  --disable-sid \
#                            --enable-sid
#}

variant midi description {Add MIDI playback support via fluidsynth} {
    depends_lib-append      port:fluidsynth
    configure.args-replace  --disable-amidiplug \
                            --enable-amidiplug
}

variant cdaudio description {Add support for CDAudio} {
    depends_lib_darwin      port:libcdio \
                            port:libcdio-paranoia \
                            port:libcddb
    configure.args-replace  --disable-cdaudio \
                            --enable-cdaudio
}

variant lastfm description {Add support for last.fm} {
    depends_lib-append      port:curl
    configure.args-replace  --disable-scrobbler2 \
                            --enable-scrobbler2
}

# ampache_browser not ported yet.
#variant ampache requires qt5 description {Add support for browsing music on an Ampache server} {
#   depends_lib-append      port:ampache_browser
#   configure.args-replace  --disable-ampache \
#                           --enable-ampache
#}

variant mms description {Add support for Microsoft Media Server (MMS) streams} {
    depends_lib_darwin      port:libmms
    configure.args-replace  --disable-mms \
                            --enable-mms
}

variant cue description {Add support for CUE sheets} {
    depends_lib_darwin      port:libcue
    configure.args-replace  --disable-cue \
                            --enable-cue
}

variant lame requires filewriter description {Add support for writing MP3 files} {
    depends_lib_darwin      port:lame
    configure.args-replace  --disable-filewriter_mp3 \
                            --enable-filewriter_mp3
}

variant transform description {Add support for audio transformation, most notably resampling, pitching and speed control} {
    depends_lib-append      port:libsamplerate
    depends_lib_darwin      port:soxr
    configure.args-replace  --disable-resample \
                            --enable-resample
    configure.args-replace  --disable-speedpitch \
                            --enable-speedpitch
    configure.args-replace  --disable-soxr \
                            --enable-soxr
}

variant opengl description {Add support for spectrum visualization via OpenGL} {
    if {[variant_isset gtk2] || [variant_isset gtk3]} {
        depends_lib_darwin      path:lib/libGL.dylib:mesa
        configure.args-replace  --disable-glspectrum \
                                --enable-glspectrum
    } elseif {[variant_isset qt5]} {
        configure.args-replace  --disable-qtglspectrum \
                                --enable-qtglspectrum
    }
}

variant sdl1 conflicts sdl2 description {Add SDL audio output via libsdl1} {
    depends_lib-append      port:libsdl
    configure.args-replace  --disable-sdlout \
                            --enable-sdlout
    configure.args-append   --with-libsdl=1
}

# ffmpeg is depending upon and using libsdl1. Loading both the ffmpeg and sdlout
# plugins (when compiled with libsdl2) will hence lead to undefined behavior, as
# both libsdl1 and libsdl2 expose functions with the same name.
variant sdl2 conflicts sdl1 ffmpeg description {Add SDL audio output via libsdl2} {
    depends_lib-append      port:libsdl2
    configure.args-replace  --disable-sdlout \
                            --enable-sdlout
    configure.args-append   --with-libsdl=2
}

variant qt5 description {Add Qt5 support} {}
if {[variant_isset qt5]} {
    PortGroup   qt5 1.0

    require_active_variants audacious-core qt5

    qt5.depends_component   qtmultimedia

    if {${subport} eq ${name}} {
        patchfiles-append   patch-qt-iconthemes.diff \
                            patch-qt-native-iconsize.diff \
                            patch-qt-no-unified.diff \
                            patch-qt-menushortcuts.diff
    } else {
#         patchfiles-append   devel/patch-qt-iconthemes.diff \
#                             patch-qt-native-iconsize.diff \
#                             devel/patch-qt-no-unified.diff \
#                             devel/patch-qt-menushortcuts.diff \
#                             devel/patch-qt-systray-behaviour.diff \
#                             devel/patch-qt-statusbar-msgs.diff \
#                             devel/patch-qt-fontchoices.diff \
#                             devel/patch-qt-buttonicons.diff \
#                             devel/patch-qt-playlist-tweaks.diff \
#                             devel/patch-searchresults-layout.diff
    }

    if {[tbool qt5.using_kde]} {
        # activate the qt5-kde patch that lets QStandardPaths use XDG-compliant locations;
        # this increases the chance that the application behaves the same way it would
        # as when using the GTk interface.
        configure.cppflags-append \
                            -DQT_USE_EXTSTANDARDPATHS -DQT_EXTSTANDARDPATHS_ALT_DEFAULT=true
    }

    configure.args-replace  --disable-qt \
                            --enable-qt

    if {[variant_isset opengl]} {
        configure.args-replace  --disable-qtglspectrum \
                                --enable-qtglspectrum
    }

    configure.args-replace  --disable-qtaudio \
                            --enable-qtaudio
    platform darwin {
        configure.args-replace --disable-mac-media-keys \
                            --enable-mac-media-keys
    }

}

variant gtk2 conflicts gtk3 description {Add GTK2 support} {
    require_active_variants audacious-core gtk2
    depends_lib_darwin      path:lib/pkgconfig/gtk-2.0.pc:gtk2 \
                            path:lib/pkgconfig/gdk-x11-2.0.pc:gtk2

    configure.args-replace  --disable-gtk \
                            --enable-gtk
    configure.args-replace  --disable-hotkey \
                            --enable-hotkey
}

if {${subport} eq "${name}"} {
    variant gtk3 conflicts gtk2 description {Add GTK3 support} {
        require_active_variants audacious-core gtk3
        depends_lib-append  path:lib/pkgconfig/gtk-3.0.pc:gtk3 \
                            path:lib/pkgconfig/gdk-x11-3.0.pc:gtk3

        patchfiles-append   patch-gtk3.diff

        configure.args-replace \
                            --disable-gtk \
                            --enable-gtk
        configure.args-replace  \
                            --disable-hotkey \
                            --enable-hotkey
    }
}

variant full requires console mp3 dbus lirc wavpack aac sndfile modplug \
                      midi cdaudio mms cue lame flac transform opengl \
                      filewriter notifications \
             conflicts altfull \
             description {Build all plugins, except additional sound output plugins and potentially conflicting variants} {}

variant altfull requires ffmpeg mp3 dbus wavpack aac sndfile modplug \
                      midi cdaudio mms cue lame flac transform opengl \
                      filewriter notifications vorbis \
             conflicts full \
             description {Build a different selection of most plugins, except additional sound output plugins and potentially conflicting variants} {}

if {[variant_isset jack]} {
    notes-append {
                    You have selected the JACK audio output plugin. \
                    To use this plugin, jackd needs to be started manually. \
                    The plugin is known to have issues leading to crackling sound output. \
                    Please don't report bugs against this plugin.\
    }
}

if {![variant_isset sdl2] && ![variant_isset altfull]} {
    default_variants-append +sdl1 +ffmpeg
}

# Need either one of gtk2, gtk3 or Qt5 to have a GUI. Default to gtk2, which is preferred by upstream.
if {![variant_isset gtk2] && ![variant_isset gtk3] && ![variant_isset qt5]} {
    default_variants-append +gtk2
}

# Add other variants to the "full" set if gtk2 or gtk3 have been enabled.
if {[variant_isset full] && ([variant_isset gtk2] || [variant_isset gtk3])} {
    default_variants-append +osd +osd_composite +lastfm
}

# OSD only supported with gtk2 or gtk3.
if {![variant_isset gtk2] && ![variant_isset gtk3] && ([variant_isset osd] || [variant_isset osd_composite])} {
    ui_debug "The osd or osd_composite variants require either the gtk2 or gtk3 variants to be enabled."
    error "+osd or +osd_composite require +gtk2 or +gtk3."
}

# LastFM/scrobbler2 only supported with gtk2 or gtk3.
if {![variant_isset gtk2] && ![variant_isset gtk3] && [variant_isset lastfm]} {
    ui_debug "The lastfm variant requires either the gtk2 or gtk3 variants to be enabled."
    error "+lastfm requires +gtk2 or +gtk3."
}

# OpenGL only supported with gtk2, gtk3 or Qt5.
if {![variant_isset gtk2] && ![variant_isset gtk3] && ![variant_isset qt5] && [variant_isset opengl]} {
    ui_debug "The opengl variant requires either the gtk2, gtk3 or qt5 variants to be enabled."
    error "+opengl requires +gtk2, +gtk3 or +qt5."
}

long_description-append \nDependencies obtained from the host:\n${host_dependencies}

livecheck.type      regex
livecheck.url       ${master_sites}
livecheck.regex     "${name}-(\\d+(?:\\.\\d+)*)${extract.suffix}"
