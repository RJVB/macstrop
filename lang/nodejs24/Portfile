# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               legacysupport 1.1
set LTO_needs_pre_build yes
set LTO.LTO.maybe_ForceLD yes
PortGroup               LTO 1.0
PortGroup               python-required 1.0
PortGroup               save_configure_cmd 1.0
PortGroup               compress_workdir 1.0

name                    nodejs24

if {${os.platform} eq "darwin"} {
    if {${subport} ne "${name}-pkg"} {
        variant libstdcxx description {build against GNU's libstdc++; installs in a subprefix} {}
    }
    if {[variant_exists libstdcxx] && [variant_isset libstdcxx]} {
        if {[string match macports-clang* ${configure.compiler}]} {
            configure.cxx_stdlib \
                        macports-libstdc++
        } else {
            configure.cxx_stdlib \
                        libstdc++
        }
        set install_prefix \
                        ${prefix}/libexec/nodejs24
        configure.pre_args-replace \
                        --prefix=${prefix} \
                        --prefix=${install_prefix}
        # better once too often than too few!
        configure.ldflags-append \
                        -stdlib=${configure.cxx_stdlib}
    } else {
        configure.cxx_stdlib \
                        libc++
        set install_prefix \
                        ${prefix}
    }
} else {
    set install_prefix  ${prefix}
}
compiler.cxx_standard   2020
# Has to be gnu++20 !
configure.cxx-append    -std=gnu++20
configure.cxxflags-append \
                        -fpermissive

version                 24.11.1

categories              lang net
license                 {MIT BSD}
maintainers             {ciserlohn @ci42} openmaintainer

# Attempting to use node on Mac OS X 10.7 causes a kernel panic. libuv could
# be patched to fix this; see https://trac.macports.org/ticket/71475
platforms               {darwin != 11.*} linux

description             Evented I/O for V8 JavaScript

long_description        Node's goal is to provide an easy way to build scalable network programs in JavaScript. \
                        Node is similar in design to and influenced by systems like Ruby's Event \
                        Machine or Python's Twisted. Node takes the event model a bit further-it \
                        presents the event loop as a language construct instead of as a library.

subport "${name}-pkg" {
    description     Patched node22 binary for creating standalone executables
    long_description \
                    {*}${description}. This is ${name} with the pkg-fetch patch allowing to create \
                    standalone NodeJS executables with the `pkg` utility.\n\
                    See https://github.com/yao-pkg/pkg-fetch\n\
                    https://github.com/aclap-dev/vdhcoapp/issues/262#issuecomment-2790839946
}

if {${subport} eq "${name}" && ![variant_isset libstdcxx]} {
    conflicts           nodejs8 nodejs10 nodejs12 nodejs14 nodejs16 nodejs18 nodejs20 nodejs22
}

homepage                https://nodejs.org/
master_sites            ${homepage}dist/v${version}
use_xz                  yes

checksums               rmd160  da30bbdf68b2061f63327d1d5c11493441128740 \
                        sha256  ea4da35f1c9ca376ec6837e1e30cee30d491847fe152a3f0378dc1156d954bbd
#                         size    51747676

distname                node-v${version}

# set py_ver              3.13
# set py_ver_nodot        [string map {. {}} ${py_ver}]
# the configure script starts checking at Python3.9 but let's ask for 3.10 which is fast
find_new_enough_python3 10

depends_build-append    port:pkgconfig \
                        port:python${py_ver_nodot} \
                        port:gmake

depends_lib-append      path:lib/pkgconfig/icu-uc.pc:icu \
                        port:zlib

# The Node.js docs says it requires "Xcode >= 16.1 (Apple LLVM >= 17)" [1].
# [1] https://github.com/nodejs/node/blob/v24.x/BUILDING.md#supported-toolchains
## RJVB: I still get failures building with port:clang-17, probably due to an error
## in libc++ 17
compiler.blacklist-append {clang < 1700}

# use the system libuv instead of the bundled version, as it is fixed for older systems
if { ${os.platform} eq "darwin" && ${os.major} < 15 } {
    # for older macs it seems nodejs20+ requires the packaged libuv,
    # unlike earlier versions. the flag below ensures the libuv
    # packaged with nodejs20+ can be used
    if {[string match *clang* ${configure.compiler}]} {
        configure.cxx-append      -Wno-error=incompatible-function-pointer-types
    }

    if {${configure.cxx_stdlib} eq "libc++"} {
        PortGroup                 active_variants 1.1
        depends_lib-append        port:libcxx
        require_active_variants   libcxx macports_libcxx
        configure.cxx-append      -Wl,-L${prefix}/lib/libcxx
        # leads to compiler errors because of missing standard typedefs like for size_t, uint_64t etc
        #legacysupport.use_mp_libcxx yes
    }

    #we guard against using the pthread qos API for Yosemite (10.10) and lower,
    #since it is not available.
    if { ${os.major} < 14 } {
        # SIC, BRosemite !!
        configure.cppflags-append -DNOT_ON_BROSEMITE_OR_LATER=1
    }
}

# suppress a warning-as-error that shows up with newer clang compilers
if {[string match *clang* ${configure.compiler}]} {
    configure.cxx-append      -Wno-error=enum-constexpr-conversion

    #the ventura buildbot will fail because it doesn't recognise this flag
    #so we disable unknown warnings as errors for older compilers
    configure.cxx-append      -Wno-error=unknown-warning-option
    if {${os.major} < 17} {
        configure.cxx-append  -faligned-allocation
    }
}

proc rec_glob {basedir pattern} {
    set files [glob -directory $basedir -nocomplain -type f $pattern]
    foreach dir [glob -directory $basedir -nocomplain -type d *] {
        lappend files {*}[rec_glob $dir $pattern]
    }
    return $files
}

configure.python        ${prefix}/bin/python${py_ver}
configure.env-append    "PYTHON_EXE=${configure.python}"

patchfiles              patch-common.gypi.diff \
                        patch-add-pthread-qos-guard-for-older-macs.patch \
                        patch-control-python-version.diff
# no longer necessary on Mac?
#                         patch-maybe-no-os_availability.diff

platform linux {
    if {![file exists /usr/include/sys/random.h]} {
        patchfiles-append \
                        patch-no-sys_random_h.diff
    }
}
patchfiles-append        patch-no-clang-only-werror-opts.diff

if {${subport} eq "${name}-pkg"} {
    # from https://github.com/yao-pkg/pkg-fetch/tree/7a9180c40ea16ee735d2885c0958ab872d270ff7/patches
    # aka https://raw.githubusercontent.com/yao-pkg/pkg-fetch/7a9180c40ea16ee735d2885c0958ab872d270ff7/patches/
    # EXCEPT we leave -flto to auto instead of 4
    patchfiles-append   node.v24.11.1.cpp.patch
}

post-patch {
    foreach f [list ${worksrcpath}/configure \
                   ${worksrcpath}/tools/gyp/gyp \
                   ${worksrcpath}/deps/v8/tools/objdump-v8 \
                   {*}[rec_glob ${worksrcpath} *.py]] {
        reinplace -q "s|/usr/bin/env python3|${configure.python}|" ${f}
        reinplace -q "s|/usr/bin/env python|${configure.python}|" ${f}
    }
    foreach gypfile [rec_glob ${worksrcpath} *.gyp*] {
        reinplace -q "s|'python3'|'${configure.python}'|" ${gypfile}
        reinplace -q "s|'python'|'${configure.python}'|" ${gypfile}
    }
    if { ${os.platform} eq "darwin" && ${os.major} <= 17 } {
        # The following text was inherited from older versions of
        # nodejs but it's being kept here for context:
        #
        # Officially nodejs requires Xcode 11 to compile, however, the
        # only item that Xcode 11 has that Xcode 10.1 doesn't is
        # os/signpost.h, so disable system instrumentation which uses
        # it. This provides support for macOS 10.13 which can only use
        # Xcode 10.1.
        reinplace -q "s|'v8_enable_system_instrumentation': 1|'v8_enable_system_instrumentation': 0|" ${worksrcpath}/tools/v8_gypfiles/features.gypi
    }
}

pre-configure {
    # Copy zlib headers here because we do not want to prepend the entire
    # ${prefix}/include to the include path (the build will then attempt to use
    # ICU headers)
    ## RJVB: NB: isn't that what we want because of --with-intl=system-icu ?!
    if {![file exists ${workpath}/zlib-inc]} {
        file mkdir ${workpath}/zlib-inc
        file copy ${prefix}/include/zconf.h ${prefix}/include/zlib.h \
            ${workpath}/zlib-inc/
    }
}

configure.args-append   --verbose
configure.args-append   --without-npm
configure.args-append   --with-intl=system-icu
configure.args-append   --shared-zlib
configure.args-append   --shared-zlib-includes=${workpath}/zlib-inc
configure.args-append   --shared-zlib-libpath=${prefix}/lib
# if {${os.platform} ne "darwin" || ([variant_exists libstdcxx] && [variant_isset libstdcxx])} {
    depends_lib-append  port:nghttp2 \
                        port:brotli \
                        port:sqlite3 \
                        port:zstd
    configure.args-append \
                        --shared-nghttp2 \
                        --shared-brotli \
                        --shared-sqlite \
                        --shared-zstd
# }

# V8 only supports ARM and IA-32 processors
supported_archs         i386 x86_64 arm64

universal_variant       no

# https://github.com/nodejs/node/blob/master/doc/changelogs/CHANGELOG_V17.md#openssl-30
variant no_openssl3_with_quic description {use MacPort's OpenSSL rather than Node.js' provided OpenSSL 3.0 extended with QUIC} {
    depends_lib-append      port:openssl3
    configure.args-append   --shared-openssl
    configure.args-append   --shared-openssl-includes=${prefix}/libexec/openssl3/include/openssl
    configure.args-append   --shared-openssl-libpath=${prefix}/libexec/openssl3/lib
    platform linux {
        configure.ldflags-append \
                            -Wl,-rpath,${prefix}/libexec/openssl3/lib
    }
}

# "V8 doesn't like cache. - before 17.0.2 or so"
#configure.ccache        no

configure.save_configure_cmd "install log"

test.run                yes
test.cmd                ${build.cmd} -j${build.jobs}

switch $build_arch {
    i386 {
        configure.args-append   --dest-cpu=ia32
    }
    x86_64 {
        configure.args-append   --dest-cpu=x64
    }
   arm64 {
        configure.args-append   --dest-cpu=arm64
    }
}

build.cmd           ${prefix}/bin/gmake
# we need the legacy-support CPP options NOW!
configure.cppflags-prepend \
                    {*}[legacysupport::get_cpp_flags]
# # see the explanation for symlinking zlib-inc, above:
# configure.cppflags-delete \
#                     -isystem${prefix}/include
build.args-append   CC=${configure.cc} \
                    "CXX=\"${configure.cxx}\"" \
                    "CXX.host=\"${configure.cxx}\"" \
                    CPP=${configure.cpp} \
                    CPPFLAGS="${configure.cppflags}" \
                    CFLAGS="${configure.cflags} ${configure.cppflags}" \
                    CXXFLAGS="${configure.cxxflags} ${configure.cppflags}" \
                    LDFLAGS="${configure.ldflags} [legacysupport::get_library_link_flags]" \
                    PYTHON=${configure.python} \
                    V=1
build.post_args-append -wk

configure.save_build_cmd "install"

destroot {
    set bindir ${destroot}${install_prefix}/bin
    set libdir ${destroot}${install_prefix}/lib
    set libndir ${libdir}/node
    set libddir ${libdir}/dtrace
    set incdir ${destroot}${install_prefix}/include/node
    set docdir ${destroot}${install_prefix}/share/doc/${name}

    xinstall -d ${bindir}
    xinstall -d ${libdir}
    xinstall -d ${libndir}
    xinstall -d ${libddir}
    xinstall -d ${incdir}
    xinstall -d ${incdir}/uv
    xinstall -d ${docdir}

    if {${subport} eq "${name}"} {
        # install binaries
        xinstall -m 755 -W ${worksrcpath} \
            out/Release/node \
            ${bindir}

        # install headers
        xinstall -m 644 {*}[glob ${worksrcpath}/src/*.h]                ${incdir}
        xinstall -m 644 {*}[glob ${worksrcpath}/deps/v8/include/*.h]    ${incdir}
        xinstall -m 644 {*}[glob ${worksrcpath}/deps/uv/include/*.h]    ${incdir}
        xinstall -m 644 {*}[glob ${worksrcpath}/deps/uv/include/uv/*.h] ${incdir}/uv
        xinstall -m 644 {*}[glob ${worksrcpath}/deps/cares/include/*.h] ${incdir}

        # install manpage
        xinstall -m 644 -W ${worksrcpath} \
            doc/node.1 \
            ${destroot}${install_prefix}/share/man/man1

        # install docs
        xinstall -m 644 -W ${worksrcpath} \
            CHANGELOG.md \
            CONTRIBUTING.md \
            GOVERNANCE.md \
            LICENSE \
            README.md \
            SECURITY.md \
            ${docdir}
    } else {
        xinstall -d -m 755 ${destroot}${prefix}/libexec/nodejs-pkg
        if {${os.platform} eq "darwin"} {
            set upstream_name macos
        } else {
            set upstream_name ${os.platform}
        }
        xinstall -m 755 -W ${worksrcpath} \
            out/Release/node \
            ${destroot}${prefix}/libexec/nodejs-pkg/built-v${version}-${upstream_name}-${build_arch}
    }
}

livecheck.url       ${homepage}dist/
livecheck.type      regex
livecheck.regex     {v(24\.\d+\.\d+)}
