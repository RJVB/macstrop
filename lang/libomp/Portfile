# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
PortGroup               cmake 1.1
if {${os.platform} eq "darwin" && ${os.major} <= 17} {
    PortGroup           muniversal 1.0
}
PortGroup               clang_dependency 1.0
PortGroup               compiler_blacklist_versions 1.0
if {[variant_isset universal]} {
    set LTO.LTO.maybe_ForceLD yes
}
set LTO.allow_ThinLTO   no
PortGroup               LTO 1.0
PortGroup               compress_workdir 1.0
PortGroup               python-required 1.0

name                    libomp
maintainers             {eborisch @eborisch} openmaintainer
homepage                https://openmp.llvm.org
description             The OpenMP subproject of LLVM
long_description        {*}${description} is intended to contain all of the\
                        components required to build an executing OpenMP\
                        program that are outside the compiler itself. \
                        This port works with MacPorts' clang-3.7+ (optionally \
                        with +openmp variant for clang-3.7.)

categories              lang
platforms               darwin linux
supported_archs         arm64 i386 x86_64
license                 {MIT NCSA}

# Moved to epoch 1 for svn # -> version # change.
epoch                   1

if {${os.platform} ne "darwin" || ${configure.cxx_stdlib} ne "libstdc++"} {

    version             20.1.1
    platform darwin {
        revision        1
    }

    livecheck.regex {"llvmorg-([0-9.]+)".*}

    distname        openmp-${version}.src
    distfiles       ${distname}.tar.xz cmake-${version}.src.tar.xz

    checksums \
        openmp-20.1.1.src.tar.xz \
        rmd160  67ccc02cad70b9f4714e0a7d200ab2ec20eaeca5 \
        sha256  a5d2bba7bc4e7d159ce3fe8d09bdadeb59ff00af967b0a2bf5993fc1528f569e \
        cmake-20.1.1.src.tar.xz \
        rmd160  49696af4a2acec9d52919ca16a9dbeaac2150d1d \
        sha256  b5988c9e3df3a61249fa82db54061f733756e74f73dfb299ff6314873a732d61
#         size    8648 

    if {${os.major} <= 12} {
        # kmp_alloc.c includes <atomic> but libc++ is not the default on
        # these systems. https://trac.macports.org/ticket/52554
        configure.cflags-append -stdlib=libc++
    }

    if {${os.platform} eq "darwin" && ${os.major} < 13} {
        # Last python version to not require C11 (and thus a clang
        # dependency on some OS versions). Used by libpsl (which is a
        # dependency of cmake) for the same reason.
        set python_branch   10
    } else {
        set python_branch   13
    }
    set python              [find_new_enough_python3 7 ${python_branch}]
    configure.args-append   -DPython3_EXECUTABLE=${python} \
                            -DPython_EXECUTABLE=${python}
    depends_build-append    port:python${py_ver_nodot}
    depends_skip_archcheck-append python${py_ver_nodot}

    # Build requires std::atomic
    configure.cxxflags-append -std=c++11
    compiler.cxx_standard     2011

    master_sites        https://github.com/llvm/llvm-project/releases/download/llvmorg-${version} \
                        https://releases.llvm.org/${version}
    use_xz              yes
    dist_subdir         openmp-release
    worksrcdir          ${distname}
    set rtpath          "runtime/"
    patchfiles-append   patch-libomp-use-gettid-on-Leopard.diff \
                        patch-test-ignore-unexpected-dependencies.diff

    livecheck.url       https://api.github.com/repos/llvm/llvm-project/tags
} else {
    PortGroup       github 1.0
    # Last version working on libstdc++
    github.setup    llvm-mirror openmp 381 svn-tags/RELEASE_
    # Change github.tarball_from to 'releases' or 'archive' next update
    github.tarball_from tarball
    name            libomp

    worksrcdir      openmp-${version}/final/runtime
    version         3.8.1
    checksums       rmd160  a41054068a127ef84610afef8090109078cb6c46 \
                    sha256  4c46b5946fe9b2a701661746d11c7c85c51a7f18673194a7ebd2a43470948a34 \
                    size    5587986
    set rtpath      "./"

    if {![file exists ${prefix}/bin/cmake]} {
        # use cmake-bootstrap to minimize dependencies.
        depends_build-replace  \
                    path:bin/cmake:cmake port:cmake-bootstrap
        configure.cmd \
                    ${prefix}/libexec/cmake-bootstrap/bin/cmake
    }

    depends_build-append    port:perl5
    livecheck.type      none
}

# According to documentation builds with clang >= 3.3
compiler.blacklist-append {clang < 500} *gcc*

# https://trac.macports.org/ticket/68490#
# Use clang-11-bootstrap on OSX10.11 and older
platform darwin {
    if {${os.major} <= 15 && ![string match macports-* ${configure.compiler}]} {
        configure.compiler.add_deps no
        depends_build-append port:clang-11-bootstrap
        depends_skip_archcheck-append clang-11-bootstrap
        pre-configure {
            configure.cc  ${prefix}/libexec/clang-11-bootstrap/bin/clang
            configure.cxx ${prefix}/libexec/clang-11-bootstrap/bin/clang++
        }
    }

    if {${os.major} <= 17} {
        default_variants +universal
    }
}

# Do actual install into ${prefix}/(install|lib)/libomp
# A little unorthodox, but to have clang automatically find the includes and
# lib, with -fopenmp, it is nice to have them their own directories. Not
# exactly a config cmake supports directly; install into dummy location and
# move post-install
configure.pre_args      -DCMAKE_INSTALL_PREFIX=${prefix}/tmp

configure.args-delete   -DCMAKE_INSTALL_RPATH=${prefix}/lib \
                        -DCMAKE_INSTALL_NAME_DIR=${prefix}/lib

# With this, cmake sets the correct library name in the dylibs for the
# final destination we move them to
configure.args-append   -DCMAKE_INSTALL_RPATH=${prefix}/lib/libomp \
                        -DCMAKE_INSTALL_NAME_DIR=${prefix}/lib/libomp

if {${os.platform} eq "darwin" && ${os.major} <= 10} {
    set hnames          {omp.h}
    configure.args-append \
                        -DLIBOMP_OMPT_SUPPORT=FALSE
} else {
    set hnames          {*.h}
}

if {[variant_isset hwloc]} {
    depends_lib-append  port:hwloc
    configure.args-append \
                        -DLIBOMP_USE_HWLOC=ON \
                        -DLIBOMP_HWLOC_INSTALL_DIR=${prefix}
} else {
    configure.args-append \
                        -DLIBOMP_USE_HWLOC=OFF
}

if {[string match *clang* ${configure.cxx}]} {
    set llvmlit [string map {"clang++" "llvm-lit"} ${configure.cxx}]
    if {[file exists ${llvmlit}]} {
        configure.args-append \
                    -DOPENMP_LLVM_LIT_EXECUTABLE=${llvmlit}
    }
}

variant top_level description \
    "Install (links to) omp.h and libs into ${prefix}/(include|lib)" {}

variant hwloc description {Build hwloc support for affinity} {}

post-extract {
    # Patch tool for build with CMAKE_INSTALL_PREFIX
    if {[vercmp ${version} 15.0.0] >= 0} {
        system -W ${workpath} "mv cmake*.src/Modules/* openmp*/cmake/"
    }
}

post-destroot {
    set instdest ${destroot}${prefix}
    xinstall -d ${instdest}/share/doc/libomp

    xinstall -d ${instdest}/include/libomp
    foreach h [glob -directory ${instdest}/tmp/include ${hnames}] {
        move ${h} ${instdest}/include/libomp/
    }

    xinstall -d ${instdest}/lib/libomp
    if {${os.platform} eq "darwin"} {
        set shlext "dylib"
    } else {
        set shlext "so"
    }
    foreach p [glob -directory ${instdest}/tmp/lib/ *.${shlext}* *.a] {
        move ${p} ${instdest}/lib/libomp/
    }
    move ${instdest}/tmp/lib/cmake ${instdest}/lib/

    if {[variant_isset top_level]} {
        system -W ${instdest}/include \
          "ln -sf libomp/* ."
        system -W ${instdest}/lib/ \
          "ln -sf libomp/* ."
    }

    if {[file exists ${destroot}${prefix}/tmp/share/gdb]} {
        file rename ${destroot}${prefix}/tmp/share/gdb ${destroot}${prefix}/share/gdb
    }

    set fpath ${worksrcpath}/${rtpath}
    file copy ${fpath}/README.txt ${fpath}/../LICENSE.TXT \
        ${instdest}/share/doc/libomp/

}

notes "
 To use this OpenMP library:
  * For clang-3.8+, or clang-3.7 with +openmp variant:
     add \"-fopenmp\" during compilation / linking.
  * For clang-3.7 without +openmp variant, use:
     \"-I${prefix}/include/libomp -L${prefix}/lib/libomp -fopenmp\"
"

test.run                yes
test.target             libomp-micro-tests

post-test {
    if {${os.platform} eq "darwin"} {
        set env(DYLD_LIBRARY_PATH)  "${build.dir}/runtime/src"
    } else {
        set env(LD_LIBRARY_PATH)    "${build.dir}/runtime/src"
    }
    system -W ${build.dir} "runtime/src/test-touch-rt/test-touch"
}
