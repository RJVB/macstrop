# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0

# cmake requires C++11 and thus libc++. Avoid circular dependencies and allow
# building using the cmake build system only when the system has already has libc++
# installed, either natively or because the libc++ conversion has been bootstrapped.
# Also require cmake to be present.
# check the presence of cmake by checking the registry for the activated port as
# ${prefix} will be set to "${prefix}" during portindex'ing ...
if {[catch {set cmake_available [registry_active cmake]}]} {
    ui_debug "port:cmake is not installed or not activated"
    set use_cmake       0
} else {
    set use_cmake       [expr [file exists /usr/lib/libc++.dylib] && [lindex [lindex ${cmake_available} 0] 2]]
    ui_debug "use_cmake: [tbool use_cmake]"
}

# version                 13.0.1 ; revision 3
# set llvm_version        13

# NB : see the NB comment below about running $filespath/find_reexported_from_legacy-support.sh !
version                 17.0.6 ; revision 4
set llvm_version        17

# variant exp19 description {installs the still experimental libc++ v19.0.x version} {}
if {[variant_exists exp19] && [variant_isset exp19]} {
    version             19.0.X
    set llvm_version    19

}

if {${os.platform} ne "darwin" || ${os.major} < 11} {
    ui_debug "Platform ${os.platform}, version ${os.major}.${os.minor}"
    # The official/mainstream port:libcxx has received some useful
    # tweaks recently, for 10.6 and earlier.
    name                libcxx_from_macstrop
    set almost_done     yes
} elseif {![tbool use_cmake]} {
    ui_debug "/usr/lib/libc++.dylib and/or ${prefix}/bin/cmake are missing"
    # force a bootstrap using the mainstream port:libcxx
    ## NB ##
    # We could also embed the bootstrap version, maintaining the 2 different build methods
    # like we long did, except clamping the bootstart libc++ version to 5.0.1
    name                libcxx_from_macstrop
    long_description    The LLVM C++ standard/runtime library.\n\
                        This port can only been installed if you already have a libc++ in /usr/lib \
                        and have port:cmake installed.
    set almost_done     yes
} else {
    name                libcxx
}

if {[tbool almost_done]} {
    # we're in uncharted waters; bail out
    platform darwin {
        # we probably want to provide a libcxx-dev subport so ports can have a build
        # dependency on it. 
        subport libcxx-dev {}
        if {${subport} eq "libcxx-dev"} {
            PortGroup   stub 1.0
            version     0
            description stub for the newer port:libcxx's dev-port
            long_description \
                        This is a stub for the the dev-port of a port:libcxx \
                        that you cannot install (or not yet).
        } else {
            replaced_by libcxx
            PortGroup   obsolete 1.0
        }
    }
    configure.compiler.add_deps no
    return
}

set LTO.allow_ThinLTO no
PortGroup               LTO 1.0
PortGroup               cmake 1.1
# Do not propagate c/c++ standards as set by base
cmake.set_c_standard    no
cmake.set_cxx_standard  no

variant replacemnt_libcxx description {EXPERTS ONLY: Build a replacement libcxxabi and libcxx even if it is already part of the base OS.} {}
variant macports_libcxx description "EXPERTS ONLY: Install the libcxx library so that MacPorts build will link against it" {}
variant pstl description "Experimental: Build libc++ with ParallelSTL support (TBB backend)" {}

# if {${os.major} >= 11 && ![variant_isset replacemnt_libcxx] && ![variant_isset macports_libcxx]} {
#     # nothing to be done in this case, we're a stub port
#     set is_stub         yes
# }

epoch                   1
# FUTURE WARNING:
# Starting with LLVM 8.0.0, users who wish to link together translation units built
# with different versions of libc++'s headers into the same final linked image MUST
# define the _LIBCPP_HIDE_FROM_ABI_PER_TU macro to 1 when building those translation
# units. Not defining _LIBCPP_HIDE_FROM_ABI_PER_TU to 1 and linking translation units
# built with different versions of libc++'s headers together may lead to ODR violations
# and ABI issues. On the flipside, code size improvements should be expected for
# everyone not defining the macro.
## NB: the _LIBCPP_HIDE_FROM_ABI_PER_TU macros were retired somewhere around libc++ 13
## but the ABIhiding patch also sets _LIBCPP_DISABLE_AVAILABILITY (which overrides whatever
## the VENDOR_AVAILABILITY_ANNOTATIONS macro is set to). It still implies that the
## resulting libc++ will be "full" and not limited as the system libc++ is.
## NB: we should rename the patchfile...
# Hence see patch-config-availability+ABIhiding.diff

if {${llvm_version} >= 17} {
    PortGroup           legacysupport 1.1
    # link legacysupport statically for compilers
    legacysupport.use_static yes
    # Sysconf wrapping causes issues so disable
    legacysupport.disable_function_wrap yes

    set minClang        14
    set clangVersions   {14 15 16 17 18 19 20}

    # some kind of version that should be larger than the current_version Apple gave to this libc++ version
    # could also simply be a version that is based on Apple's libc++ version on the OS version we are building for.
    set version4macports 1700.99.6

    checksums           rmd160  10f44d5a8e3d0d5fc1a1961a8adaac945733ec07 \
                        sha256  58a8818c60e6627064f312dbf46c02d9949956558340938b71cf731ad8bc0813
#                         size    127838860
} else {
    set minClang        8
    set clangVersions   {8.0 9.0 10 11 12 13 14 15 16 17 18 19 20}

    # some kind of version that should be larger than the current_version Apple gave to this libc++ version
    # could also simply be a version that is based on Apple's libc++ version on the OS version we are building for.
    set version4macports 1300.99.1

    checksums           rmd160  ae542658ad0e97b4bf088b1cfba66fa10b9b52d8 \
                        sha256  326335a830f2e32d06d0a36393b5455d17dc73e0bd1211065227ee014f92cbf8
#                         size    97584928
}

categories              lang
platforms               darwin
license                 MIT NCSA
maintainers             gmail.com:rjvbertin {jeremyhu @jeremyhu}
description             libc++ is the LLVM Project's implementation of the C++ standard library with support for C++11 and newer.
long_description        ${description} \
                        Because objects cannot be passed between different versions of the C++ runtime, this port must \
                        replace the host version in order to be used. On Lion and later, \
                        users must build the port with +replacemnt_libcxx and install the root manually if they wish \
                        to replace the existing host implementation. Either way, the libraries are installed into \
                        $prefix/lib/libcxx, in line with port:macports-libcxx . They can also use the +macports_libcxx variant \
                        which installs the library under $prefix/lib where it will be picked up by all \
                        subsequent MacPorts builds if port:libcxx-dev is also installed. This variant also installs the optlibs \
                        wrapper script with which applications can be started such that they use the libc++ from this port.

homepage                http://libcxx.llvm.org/

master_sites            https://github.com/llvm/llvm-project/releases/download/llvmorg-${version}
dist_subdir             llvm

livecheck.url           http://www.llvm.org/
livecheck.regex         download.html\#(\\d+\\.\\d+(\\.\\d+)+)

use_xz                  yes

distname                llvm-project-${version}.src

set suffix              mp-${llvm_version}

compiler.blacklist-append clang
# pick the newest available (= installed) port:clang-XY compiler:
foreach clv ${clangVersions} {
    if {[file exists ${prefix}/bin/clang-mp-${clv}]} {
        compiler.whitelist-prepend macports-clang-${clv}
    }
    compiler.fallback-prepend macports-clang-${clv}
}

if {[string match *clang++-mp-* ${configure.cxx}]} {
    compiler.blacklist-append {clang < 1204} {macports-clang-[5-9].0} {macports-clang-1[0-3]}
    compiler.fallback       clang macports-clang-15 macports-clang-14 macports-clang-13 macports-clang-12 \
                            macports-clang-11 macports-clang-10 macports-clang-9.0 macports-clang-8.0
    set clangversion [lindex [split ${configure.cc} -] 2]
    if {${clangversion} ne ""} {
        depends_build-append port:clang-${clangversion}
        if {${clangversion} < ${minClang}} {
            pre-configure {
                ui_error "This port requires at least clang 14 to be built!"
                return -code error "Please install or specify a sufficiently recent compiler"
            }
        }
    }
} elseif {![string match macports-gcc* ${configure.compiler}]} {
    pre-configure {
        ui_error "This port needs to be built with a gcc or clang compiler from MacPorts (clang >= 8.0)"
        return -code error "install a MacPorts clang compiler or invoke the install with `configure.compiler=macports-clang-XY`"
    }
}

# variant builtwith description {Label the install with the compiler used} {}
# if {[variant_isset builtwith]} {
# 	set usedCompiler [string map {"-" "_"} [file tail ${configure.cc}]]
# 	variant ${usedCompiler} requires builtwith description "placeholder variant to record the compiler used" {
# 		pre-configure {
# 			ui_warn "+builtwith+${usedCompiler} are just placeholder variants used only to label the install with the compiler used"
# 		}
# 	}
# 	default_variants-append +${usedCompiler}
# }

if {${name} eq "libcxx"} {
    PortGroup           devport 1.0
    create_devport      port:${name}
}

if {![is_mainport]} {
    # nothing else to be done
    return
}
PortGroup           compress_workdir 1.0

installs_libs       yes

worksrcdir          ${distname}

# these are used during the build but not linked to by the resulting libraries
depends_build-append \
                    port:libedit \
                    port:libffi \
                    port:ncurses \
                    port:libxml2 \
                    port:zlib \
                    port:git \
                    port:cctools

depends_skip_archcheck-append git cctools

patch.pre_args -Np1
switch -- ${llvm_version} {
    13 {
        patchfiles-append \
            ${llvm_version}/0010-compiler-rt-cmake-config-ix.cmake-was-Leopard-No-ASA.patch \
            ${llvm_version}/0011-Fix-missing-long-long-math-prototypes-when-using-the.patch \
            ${llvm_version}/0012-compiler-rt-add-some-defs-missing-in-older-SDKs.patch \
            ${llvm_version}/0016-10.10-and-less-compiler-rt-find-macosx-sdk.patch \
            ${llvm_version}/0017-10.6-and-less-libcxxabi-work-around-no-reexported_symbo.patch \
            ${llvm_version}/0017-10.6-and-less-libcxx-work-around-no-reexported_symbo.patch \
            ${llvm_version}/0021-10.6-and-less-libcxx-disable-Availability-tests.patch \
            ${llvm_version}/0024-10.5-and-less-compiler-rt-work-around-no-libdispatch.patch
        # 240318 : NB: 1 build_type fix moved to patch-benchmarks-cmake-fixes.diff
        patchfiles-append \
            ${llvm_version}/patch-accept-build_types.diff \
            ${llvm_version}/patch-config-availability+ABIhiding.diff \
            ${llvm_version}/patch-experimental-no-lto.diff
        # make benchmarks buildable (incl. not requiring over 2G of RAM to build)
        patchfiles-append \
            ${llvm_version}/patch-no-universal-or-filesystem-benchmarks.diff \
            ${llvm_version}/patch-strings-benchmark.diff \
            ${llvm_version}/patch-cheaper-variant-benchmark.diff
        # clang-13/work/build/include/c++/v1/optional:224:5: error: destructor cannot be marked constexpr
        #     _LIBCPP_CONSTEXPR_AFTER_CXX17 ~__optional_destruct_base()
        patchfiles-append \
            ${llvm_version}/patch-no-const_expr_dtor.diff
        # set our compatibility version and install location:
        patchfiles-append \
            ${llvm_version}/patch-install_location_version_and_name.diff
        # let's see what we gain from supporting ParellelSTL
        # see https://reviews.llvm.org/D129452
        patchfiles-append \
            ${llvm_version}/patch-use-pstl.diff \
            ${llvm_version}/patch-no_result_of.diff
        # make certain symbols like __gxx_personality_v0 can be found as expected by software
        # built against older libc++ versions.
        patchfiles-append \
            ${llvm_version}/patch-more-exported-symbols.diff
        # allow building with compilers that don't support -nostdlib++ (like GCC 12)
        # (NB: the build still fails ATM, when using g++ -stdlib=libc++)
        patchfiles-append \
            ${llvm_version}/patch-allow-builds-with-gcc.diff
        # Somehow the benchmarks used to build for me, and now they didn't until I came up with:
        patchfiles-append \
            ${llvm_version}/patch-benchmarks-cmake-fixes.diff
        # make the +benchmark_mpstdlibcxx variant possible:
        patchfiles-append \
            ${llvm_version}/patch-benchmark_mplibstdcxx.diff

        ## RJVB : bring back some header magic to allow using libc++ 17 with older clang compilers
        ## (tested down to clang 5) but also G++ targetting libc++
        patchfiles-append \
            ${llvm_version}/patch-support-GCC-and-older-clang.diff
    }
    17 {
        patchfiles-append \
            ${llvm_version}/0010-compiler-rt-cmake-config-ix.cmake-was-Leopard-No-ASA.patch \
            ${llvm_version}/0011-Fix-missing-long-long-math-prototypes-when-using-the.patch \
            ${llvm_version}/0012-compiler-rt-add-some-defs-missing-in-older-SDKs.patch
        if {${os.major} < 14} {
            patchfiles-append \
                ${llvm_version}/0016-10.10-and-less-compiler-rt-find-macosx-sdk.patch
        }
        if {${os.major} < 11} {
            patchfiles-append \
                ${llvm_version}/0017-10.6-and-less-libcxxabi-work-around-no-reexported_symbo.patch \
                ${llvm_version}/0017-10.6-and-less-libcxx-work-around-no-reexported_symbo.patch \
                ${llvm_version}/0021-10.6-and-less-libcxx-disable-Availability-tests.patch
        }
        if {${os.major} < 10} {
            patchfiles-append \
                ${llvm_version}/0024-10.5-and-less-compiler-rt-work-around-no-libdispatch.patch \
                ${llvm_version}/0027-libcxx-link-gcc_s.1-on-macOS-before-10.6.patch \
                ${llvm_version}/0028-libcxx-use-malloc-free-only-on-macOS-before-10.6.patch \
                ${llvm_version}/49d2071572d484a2b5dc356f59050bb173c8c77c-inverse.patch
        }
        if {${os.major} <= 16} {
            # https://github.com/llvm/llvm-project/issues/64226
            patchfiles-append \
                ${llvm_version}/0042-mbstate_t-not-defined.patch
        }
        patchfiles-append \
            ${llvm_version}/patch-accept-build_types.diff \
            ${llvm_version}/patch-config-availability+ABIhiding.diff \
            ${llvm_version}/patch-experimental-no-lto.diff
        # make benchmarks buildable (incl. not requiring over 2G of RAM to build)
        patchfiles-append \
            ${llvm_version}/patch-strings-benchmark.diff \
            ${llvm_version}/patch-cheaper-variant-benchmark.diff
        # clang-13/work/build/include/c++/v1/optional:224:5: error: destructor cannot be marked constexpr
        #     _LIBCPP_CONSTEXPR_AFTER_CXX17 ~__optional_destruct_base()
        patchfiles-append \
            ${llvm_version}/patch-no-const_expr_dtor.diff

        # set our compatibility version and install location:
        patchfiles-append \
            ${llvm_version}/patch-install_location_version_and_name.diff
        # make certain symbols like __gxx_personality_v0 can be found as expected by software
        # built against older libc++ versions.
        patchfiles-append \
            ${llvm_version}/patch-more-exported-symbols.diff
        # this one may have to be conditional to older systems only:
        patchfiles-append \
            ${llvm_version}/patch-no-reexported-symbols-from-objectfiles.diff

        # make the +benchmark_mpstdlibcxx variant possible:
        patchfiles-append \
            ${llvm_version}/patch-benchmark_mplibstdcxx.diff

        # alter the default availability header to indicate no verbose_abort is in libcxx
        # the altered section is used only when macports-libcxx is being invoked
        # see: https://trac.macports.org/ticket/68640
        patchfiles-append      ${llvm_version}/0120-clang-no-verbose-abort-in-library.diff

        # https://trac.macports.org/ticket/67686
        # revert commit that breaks rpath linkages in clang runtime
        patchfiles-append \
            ${llvm_version}/b98da4c71edda3df0a0555b1ab63ec52e92252b4-inverse.patch

        # backport a patch from 18.0.1
        patchfiles-append \
            ${llvm_version}/patch-make-pointer_traits-sfinae-friendly.diff
    }
}

switch -- ${llvm_version} {
    17 {
        if {${os.major} <= 18} {
            # on systems that might build i386, we need atomic builtins
            # https://trac.macports.org/ticket/58712
            configure.args-append    -DCOMPILER_RT_EXCLUDE_ATOMIC_BUILTIN=OFF
        }

        # libc++ link step fails on OSX10.10 and older to correctly use static legacy support lib.
        if {${os.platform} eq "darwin" && ${os.major} <= [option legacysupport.newest_darwin_requires_legacy] } {
            # 
            # NB: check if the patch below is still up-to-date AFTER upgrading libc++ !!
            # or after upgradging legacy-support and rebuilding libcxx !
            # use $filespath/find_reexported_from_legacy-support.sh ${destroot}${prefix}/lib/libcxx/libc++.1.dylib
            # 
            patchfiles-append ${llvm_version}/patch-make-legacy_support-symbols-private.diff
            post-patch {
                reinplace "s|LIBCXX_LIBRARIES \"\"|LIBCXX_LIBRARIES \"[legacysupport::get_library_link_flags]\"|" ${patch.dir}/libcxx/CMakeLists.txt
            }
        }

        post-patch {
            # we build the tests for the host architecture only
            reinplace "s|@TESTARCHS@|${build_arch}|g" ${worksrcpath}/libcxx/benchmarks/CMakeLists.txt
        }
    }
}

if {${os.platform} eq "darwin" && ${os.major} < 12} {
    # On OSX10.7 and older revert call to add_security_warnings as fails with
    #   add_security_warnings Function invoked with incorrect arguments
    post-patch {
        reinplace "s|add_security_warnings\(|\#add_security_warnings\(|" \
            ${patch.dir}/compiler-rt/lib/builtins/CMakeLists.txt \
            ${patch.dir}/compiler-rt/cmake/Modules/CompilerRTDarwinUtils.cmake
    }

    # On OSX10.7 and older revert call to add_security_warnings as fails with
    #   add_security_warnings Function invoked with incorrect arguments
    post-patch {
        reinplace "s|add_security_warnings\(|\#add_security_warnings\(|" \
            ${patch.dir}/compiler-rt/lib/builtins/CMakeLists.txt \
            ${patch.dir}/compiler-rt/cmake/Modules/CompilerRTDarwinUtils.cmake
    }
}

# make some space, but only after the patching so it's easier to share
# patchfiles with the main llvm ports.
post-patch {
    ui_info "Cleaning up unneeded directories"
    file delete -force ${worksrcpath}/clang
    file delete -force ${worksrcpath}/llvm/lib
    file delete -force ${worksrcpath}/llvm/test
    file delete -force ${worksrcpath}/polly
    file delete -force ${worksrcpath}/lldb
}

configure.args-append \
                    -DLLVM_ENABLE_RUNTIMES="libcxx\;libcxxabi" \
                    -DLIBCXX_INSTALL_LIBRARY=ON \
                    -DLIBCXX_ENABLE_ASSERTIONS=OFF \
                    -DLIBCXXABI_ENABLE_STATIC=ON \
                    -DLIBCXX_ENABLE_STATIC=ON \
                    -ULIBCXX_BENCHMARK_NATIVE_STDLIB \
                    -DLIBCXX_LINK_TESTS_WITH_SHARED_LIBCXXABI=ON
## NB: prevent "dyld: Symbol not found: __ZdaPv (expected in libc++)
## idem __ZdlPv
## See https://reviews.llvm.org/D68269
configure.args-append \
                    -DLIBCXX_ENABLE_NEW_DELETE_DEFINITIONS=ON

if {[variant_isset pstl]} {
    depends_lib-append \
                    port:onetbb
    configure.args-replace \
                    -DLLVM_ENABLE_RUNTIMES="libcxx\;libcxxabi" \
                    -DLLVM_ENABLE_RUNTIMES="pstl\;libcxx\;libcxxabi"
    configure.args-append \
                    -DPSTL_PARALLEL_BACKEND=tbb \
                    -DLIBCXX_ENABLE_PARALLEL_ALGORITHMS=ON \
                    -DPSTL_HIDE_FROM_ABI_PER_TU=ON
    if {${llvm_version} == 17} {
        notes-append "Using pstl features requires the -fexperimental-library flag, or -D_LIBCPP_ENABLE_EXPERIMENTAL in CXXFLAGS/CPPFLAGS!"
    }
}

if {${os.major} >= 18} {
    configure.args-append -DLLVM_TARGETS_TO_BUILD="X86\;PowerPC\;AMDGPU\;arm64"
} else {
    configure.args-append -DLLVM_TARGETS_TO_BUILD="X86\;PowerPC\;AMDGPU"
}
# Testing:
configure.args-append \
                    -DVERSION_ACCORDING_TO_MACPORTS=${version4macports}

## Check how to build the "native" benchmarks:
if {[string match *g++-mp-* ${configure.cxx}]} {
    if {[string match *clang++-mp-* ${configure.cxx}]} {
        # clang++-mp-XY will need `-stdlib=macports-stdc++`, g++-mp-XY just `-stdlib=libstdc++`
        # to achieve the same effect: build and link against our libstdc++ runtime.
        pre-configure {
            if {![catch \
                {exec echo "extern int foo;" | ${configure.cxx} -x c++ -fsyntax-only - -stdlib=macports-libstdc++}]} {
                configure.args-replace \
                    -ULIBCXX_BENCHMARK_NATIVE_STDLIB \
                    -DLIBCXX_BENCHMARK_NATIVE_STDLIB=libstdc++
                configure.args-append \
                    -DLIBCXX_BENCHMARK_NATIVE_STDLIB_FROM_MACPORTS=ON
                ui_warn "${configure.cxx} accepts -stdlib=macports-libstdcxx; building benchmarks against libstdc++"
            }
        }
        pre-test {
            if {[catch \
                {exec echo "extern int foo;" | ${configure.cxx} -x c++ -fsyntax-only - -stdlib=macports-libstdc++}]} {
                ui_warn "${configure.cxx} doesn't accept -stdlib=macports-libstdcxx; the native benchmark are against the platform libc++ and will probably fail"
            }
        }
    } else {
        configure.args-replace \
                    -ULIBCXX_BENCHMARK_NATIVE_STDLIB \
                    -DLIBCXX_BENCHMARK_NATIVE_STDLIB=libstdc++
    }
}

if {[info proc cmake.save_configure_cmd] ne ""} {
    cmake.save_configure_cmd "install log"
}
if {${build.jobs} ne 1} {
    build.post_args-append -wk
}

cmake.source_dir    ${worksrcpath}/runtimes

set libcxxabi_worksrcpath \
                    ${build.dir}
set libcxx_worksrcpath \
                    ${build.dir}

post-build {
    file delete -force ${libcxxabi_worksrcpath}/lib/libc++abi.dylib ${libcxxabi_worksrcpath}/lib/libc++abi.1.dylib
    file rename ${libcxxabi_worksrcpath}/lib/libc++abi.${version4macports}.dylib ${libcxxabi_worksrcpath}/lib/libc++abi.dylib
    file delete -force ${libcxx_worksrcpath}/lib/libc++.dylib ${libcxx_worksrcpath}/lib/libc++.1.dylib
    file rename ${libcxx_worksrcpath}/lib/libc++.${version4macports}.dylib ${libcxx_worksrcpath}/lib/libc++.1.dylib
}

variant universal {}
default_variants +universal

set roots_path ${prefix}/var/system_roots/
set root_name  ${name}-${version}-${revision}

# variant runtime description "EXPERTS ONLY: Install the libcxx library so that it can be used with DYLD_INSERT_LIBRARIES (obsolete: use +macports_libcxx)" {
#     ui_warn "The +runtime variant is obsolete, please use +macports_libcxx"
    default_variants-append +macports_libcxx
# }
if {[variant_isset macports_libcxx]} {
    notes-append "Install port:libcxx-dev and rebuild ports or start applications via ${prefix}/bin/optlibs to use libc++ v${version}, or\
         set DYLD_INSERT_LIBRARIES as illustrated in that script."
}

## always install to $prefix. This means we could just use a regular destroot phase followed by a
## post-desroot{} to make our changes.
# if {[variant_isset macports_libcxx]} {
    configure.args-append \
                    -DLIBCXX_INSTALL_PREFIX=${prefix}
# } else {
#     cmake.install_prefix \
#                     /usr
# }


supported_archs i386 x86_64 ppc arm64

# We want to use the host's libc++ everywhere because we want to ensure
# that only *ONE* copy of this library is in any process's address space.
# On Snow Leopard and earlier, this port provides this library in /usr for
# binary compatibility with Lion and later.
#
# On newer OS version, this port does not install the content to /usr and
# just leaves it to expert users who may want to replace their system
# runtime.

destroot {
    ## Make a formal reference install so it's easier to track what would get installed where
    ui_debug "-->  doing a 'formal-reference' install to ${destroot}-formal"
    system -W ${build.dir} "${build.cmd} ${build.pre_args} install/fast ${build.post_args} ${destroot.destdir}-formal"

    if {[variant_isset replacemnt_libcxx]} {
        xinstall -m 755 -d ${destroot}${roots_path}/${root_name}/usr/lib

        xinstall -m 755 ${libcxxabi_worksrcpath}/lib/libc++abi.dylib ${destroot}${roots_path}/${root_name}/usr/lib
        xinstall -m 755 ${libcxxabi_worksrcpath}/lib/libc++abi.a ${destroot}${roots_path}/${root_name}/usr/lib

        xinstall -m 755 ${libcxx_worksrcpath}/lib/libc++.1.dylib ${destroot}${roots_path}/${root_name}/usr/lib
        ln -s libc++.1.dylib ${destroot}${roots_path}/${root_name}/usr/lib/libc++.dylib
        xinstall -m 755 ${libcxx_worksrcpath}/lib/libc++.a ${destroot}${roots_path}/${root_name}/usr/lib

        if {[file exists ${destroot}-formal${prefix}/lib/libc++experimental.a]} {
            xinstall -m 755 ${destroot}-formal${prefix}/lib/libc++experimental.a \
                ${destroot}${roots_path}/${root_name}/usr/lib
        }

        system "install_name_tool -id /usr/lib/libc++abi.dylib ${destroot}${roots_path}/${root_name}/usr/lib/libc++abi.dylib"
        system "install_name_tool -id /usr/lib/libc++.1.dylib ${destroot}${roots_path}/${root_name}/usr/lib/libc++.1.dylib"
        system "install_name_tool -change ${prefix}/lib/libc++abi.1.dylib /usr/lib/libc++abi.dylib ${destroot}${roots_path}/${root_name}/usr/lib/libc++.1.dylib"
        system "install_name_tool -change @rpath/libc++abi.1.dylib /usr/lib/libc++abi.dylib ${destroot}${roots_path}/${root_name}/usr/lib/libc++.1.dylib"

        system -W ${destroot}${roots_path}/${root_name} "tar czf ../${root_name}.tgz ."
        delete ${destroot}${roots_path}/${root_name}
    }

    ## 20230729 : our default behaviour should already have been to install like port:macports_libcxx, i.e.
    ## install the headers and the libraries into $prefix/lib/libcxx!
    ## We keep the macports_libcxx VARIANT to install into $prefix/lib too, as before.

    # contrary to port:macports-libcxx we build the libraries from source so
    # we can just install the headers using the corresponding command rather
    # than copying them from the corresponding port:clang-${clangversion} install.
    ui_debug "-->  installing libc++ headers to destroot"
    system -W ${build.dir} "${build.cmd} ${build.pre_args} install-cxx-headers/fast ${build.post_args} ${destroot.destdir}"
    file rename ${destroot}${prefix}/include/c++ ${destroot}${prefix}/include/libcxx
    if {[variant_isset pstl]} {
        system -W ${build.dir} "${build.cmd} ${build.pre_args} install-pstl/fast ${build.post_args} ${destroot.destdir}"
        if {${llvm_version} == 13} {
            ## With libc++ v13 we move the pstl headers into the libc++ header dir but somewhere after that
            ## version upstream put a modified subsection of the pstl headers there, and kept a larger selection
            ## in the toplevel include dir. No meddling starting with that future version (<= 17)
            foreach i [glob ${destroot}${prefix}/include/*pstl*] {
                # move the ParellelSTL headers into the libc++ include dir so they're be found when
                # that directory is added to the compiler's header search path. Upstream appear to
                # be making this same merger.
                file rename ${i} ${destroot}${prefix}/include/libcxx/v1
            }
        }
    }
    xinstall -m 755 -d ${destroot}${prefix}/lib/libcxx

    # first install possibly temporary copies into a temp dir
    xinstall -m 755 -d ${destroot}${prefix}/libtemp
    xinstall -m 755 ${libcxxabi_worksrcpath}/lib/libc++abi.dylib ${destroot}${prefix}/libtemp/libc++abi.1.dylib
    xinstall -m 755 ${libcxx_worksrcpath}/lib/libc++.1.dylib ${destroot}${prefix}/libtemp/
    ln -s libc++abi.1.dylib  ${destroot}${prefix}/libtemp/libc++abi.dylib
    ln -s libc++.1.dylib  ${destroot}${prefix}/libtemp/libc++.dylib
    foreach l {libc++ libc++abi} {
        # make copies that are interchangeable with the ones from port:macports-libcxx (version aside)
        xinstall -m 755 ${destroot}${prefix}/libtemp/${l}.1.dylib ${destroot}${prefix}/lib/libcxx/
        system "install_name_tool -id ${prefix}/lib/libcxx/${l}.1.dylib ${destroot}${prefix}/lib/libcxx/${l}.1.dylib"
        system "install_name_tool -change \
            ${prefix}/lib/libc++abi.1.dylib ${prefix}/lib/libcxx/libc++abi.1.dylib \
            ${destroot}${prefix}/lib/libcxx/${l}.1.dylib"
        system "install_name_tool -change \
            @rpath/libc++abi.1.dylib ${prefix}/lib/libcxx/libc++abi.1.dylib \
            ${destroot}${prefix}/lib/libcxx/${l}.1.dylib"
        ln -s ${l}.1.dylib ${destroot}${prefix}/lib/libcxx/${l}.dylib
    }
    xinstall -m 755 \
            ${destroot}-formal${prefix}/lib/libc++.a \
            ${destroot}-formal${prefix}/lib/libc++abi.a \
            ${destroot}${prefix}/lib/libcxx/
    if {[file exists ${destroot}-formal${prefix}/lib/libc++experimental.a]} {
        xinstall -m 755 ${destroot}-formal${prefix}/lib/libc++experimental.a \
            ${destroot}${prefix}/lib/libcxx/
    }

    if {[variant_isset macports_libcxx]} {
        # move the files from libtemp
        foreach l [glob ${destroot}${prefix}/libtemp/*] {
            file rename ${l} ${destroot}${prefix}/lib/[file tail ${l}]
        }
        foreach l {libc++ libc++abi} {
            system "install_name_tool -id ${prefix}/lib/${l}.1.dylib ${destroot}${prefix}/lib/${l}.1.dylib"
            system "install_name_tool -change \
                ${prefix}/lib/libc++abi.1.dylib ${prefix}/lib/libc++abi.1.dylib \
                ${destroot}${prefix}/lib/${l}.1.dylib"
            system "install_name_tool -change \
                @rpath/libc++abi.1.dylib ${prefix}/lib/libc++abi.1.dylib \
                ${destroot}${prefix}/lib/${l}.1.dylib"
        }
        ln -s libcxx/libc++.a  ${destroot}${prefix}/lib/
        ln -s libcxx/libc++abi.a  ${destroot}${prefix}/lib/
        if {[file exists ${destroot}${prefix}/lib/libcxx/libc++experimental.a]} {
            ln -s libcxx/libc++experimental.a  ${destroot}${prefix}/lib/
        }
        xinstall -m 755 ${filespath}/optlibs.sh ${destroot}${prefix}/bin/optlibs
        reinplace "s|@PREFIX@|${prefix}|g" ${destroot}${prefix}/bin/optlibs
    } else {
        file delete -force ${destroot}${prefix}/libtemp
    }
}

post-destroot {
    register_devport_standard_content
    append_to_devport_standard_content ${prefix}/lib/libcxx/libc++.dylib
    append_to_devport_standard_content ${prefix}/lib/libcxx/libc++abi.dylib
    append_to_devport_standard_content ${prefix}/lib/libcxx/libc++.a
    append_to_devport_standard_content ${prefix}/lib/libcxx/libc++abi.a
    append_to_devport_standard_content ${prefix}/lib/libcxx/libc++experimental.a
    append_to_devport_standard_content ${prefix}/lib/cmake
    create_devport_content_archive

    if {${llvm_version} >= 17} {
        ui_warn "Should you run ${filespath}/find_reexported_from_legacy-support.sh ${destroot}${prefix}/lib/libcxx/libc++.1.dylib?"
    }
}

test.run        yes
test.dir        ${build.dir}
test.target     cxx-benchmarks
test.post_args-append -wk
pre-test {
    # this doesn't seem to have any effect?!"
    set SDKPATH [exec xcrun -sdk macosx${macosx_deployment_target} --show-sdk-path]
    test.env-append "SDKROOT=${SDKPATH}"
    test.env-append "SDKPATH=${SDKPATH}"
    # don't set LIBRARY_PATH (we don't want unexpected linking to $prefix/lib/libc++)!
    test.env-append "LIBRARY_PATH="
}
post-test {
    # on Mac we don't build/run the "native" benchmarks as it is very tricky to link only the system libc++
    # and there is a good chance that it will be too old anyway.
    set nofilter {deque.libcxx.out dynamic_cast.libcxx.out formatter_float.libcxx.out \
        formatter_int.libcxx.out function.libcxx.out map.libcxx.out ordered_set.libcxx.out \
        std_format_spec_string_unicode.libcxx.out stringstream.libcxx.out to_chars.libcxx.out \
        unordered_set_operations.libcxx.out util_smartptr.libcxx.out variant_visit_1.libcxx.out \
        variant_visit_2.libcxx.out variant_visit_3.libcxx.out vector_operations.libcxx.out}
    foreach b [glob ${build.dir}/libcxx/benchmarks/*.libcxx.out] {
        set bm [file tail ${b}]
        ui_info "\nBenchmark ${bm}:"
        if {[lsearch ${nofilter} ${bm}] >= 0} {
            system -W ${build.dir}/libcxx/benchmarks \
                "time ./${bm} --benchmark_repetitions=1"
        } else {
            system -W ${build.dir}/libcxx/benchmarks \
                "time ./${bm} --benchmark_repetitions=1 --benchmark_filter='\[25\]\[51\]\[62\]$'"
        }
    }
}

