PortSystem              1.0
PortGroup               compiler_blacklist_versions 1.0

replaced_by             libcxx
PortGroup               obsolete 1.0

name                    libcxxabi
version                 4.0.1 ; revision 1
epoch                   1
categories              lang
platforms               darwin
license                 MIT NCSA
maintainers             {jeremyhu @jeremyhu}
description             libc++abi is a new implementation of low level support for a C++ runtime.
long_description        ${description} \
                        Because objects cannot be passed between different versions of the C++ runtime, this port must \
                        replace the host version in order to be used.  On Snow Leopard and earlier, this is done \
                        automatically because there is no existing host version of this library.  On Lion and later, \
                        users must build the port with +replacemnt_libcxx and install the root manually if they wish \
                        to replace the existing host implementation. They can also use the +runtime variant which installs \
                        copies of the library that can be loaded via DYLD_INSERT_LIBRARIES\; \
                        applications started that way will then use a more recent libc++abi than the host provides.

homepage                http://libcxxabi.llvm.org/

master_sites            http://www.llvm.org/releases/${version}/
dist_subdir             llvm
 
use_xz                  yes
distname                ${name}-${version}.src

checksums               rmd160  b09efc5d3f62187874b3240930dd8ef2147f2ab4 \
                        sha256  8f08178989a06c66cd19e771ff9d8ca526dd4a23d1382d63e416c04ea9fa1b33

set roots_path ${prefix}/var/system_roots/
set root_name  ${name}-${version}-${revision}

patch.pre_args          -p1
patchfiles \
    0001-Update-buildit-script-to-read-EXTRA_LDFLAGS-from-the.patch \
    patch-respect-optflags.diff

variant universal {}
default_variants +universal
use_configure no

platform darwin {
    if {${os.major} < 9} {
        pre-fetch {
            ui_error "${name} is not supported on Tiger or earlier."
            error "unsupported platform"
        }
    }

    if {${os.major} < 11} {
        post-activate {
            system "cd / && tar xzf ${roots_path}/${root_name}.tgz"
        }
    }
}

variant replacemnt_libcxx description {EXPERTS ONLY: Build a replacement libcxxabi and libcxx even if it is already part of the base OS.} {}
variant runtime description "Install the libcxxabi library so that it can be used with DYLD_INSERT_LIBRARIES" {
    depends_build-append \
                        port:cctools
}

if {${os.major} < 11 || [variant_isset replacemnt_libcxx] || [variant_isset runtime]} {
    compiler.blacklist *gcc* {clang < 300} {clang > 401}

    # clang 3.5 and newer are blacklisted to prevent dependency cycles
    foreach ver {3.5 3.6 3.7 3.8 3.9 devel} {
        if {![file exists ${prefix}/bin/clang-mp-${ver}]} {
            compiler.blacklist-append macports-clang-${ver}
        }
    }

    supported_archs i386 x86_64 ppc

    if {[variant_isset universal] && [lsearch ${universal_archs} ppc] != -1} {
        # http://trac.macports.org/wiki/LibcxxOnOlderSystems#Leopardppc
        compiler.whitelist macports-clang-3.7 macports-clang-3.6
    }

    # delete cxa_noexception.cpp as we're not using the cmake build system
    # cmake would choose either cxa_exceptions or cxa_noexceptions
    # but the buildit script just builds every file in the src directory and you can't have both
    post-extract {
        system "rm ${worksrcpath}/src/cxa_noexception.cpp"
        # remove this one too (it's not included by cmake)
        system "rm ${worksrcpath}/src/cxa_thread_atexit.cpp"
    }

    if {${os.major} < 10} {
        depends_build-append \
            port:libunwind \
            port:libmacho

        pre-build {
            system "nm -g ${prefix}/lib/libmacho.a 2> /dev/null | grep ' \[DST\] ' | awk '{print \$3}' > ${worksrcpath}/unexported_symbols_macho"
            system "nm -g ${prefix}/lib/libunwind.a 2> /dev/null | grep ' \[DST\] ' | awk '{print \$3}' | grep -v '^__Unwind' > ${worksrcpath}/unexported_symbols_unwind"
            system "cat ${worksrcpath}/unexported_symbols_macho ${worksrcpath}/unexported_symbols_unwind > ${worksrcpath}/unexported_symbols"
        }

        build.env-append \
            EXTRA_LDFLAGS="${prefix}/lib/libmacho.a ${prefix}/lib/libunwind.a -unexported_symbols_list ${worksrcpath}/unexported_symbols"
    }

    build.dir ${worksrcpath}/lib
    build.cmd ./buildit
    build.env-append \
        CC="${configure.cc} ${configure.cppflags}" \
        CXX="${configure.cxx} ${configure.cppflags}" \
        RC_XBS=1 \
        RC_CFLAGS="[get_canonical_archflags]" \
        CXXFLAGS="${configure.optflags}" \
        RC_ProjectSourceVersion="${version}" \
        TRIPLE="-apple-darwin${os.major}"

    # We want to use the host's libc++abi everywhere because we want to ensure
    # that only *ONE* copy of this library is in any process's address space.
    # On Snow Leopard, this port provides this library in /usr for binary
    # compatibility with Lion and later.
    #
    # On newer OS version, this port does not install the content to /usr and
    # just leaves it to expert users who may want to replace their system
    # runtime.

    destroot {
        if {[variant_isset replacemnt_libcxx]} {
            xinstall -m 755 -d ${destroot}${roots_path}/${root_name}/usr/lib
            xinstall -m 755 ${worksrcpath}/lib/libc++abi.dylib ${destroot}${roots_path}/${root_name}/usr/lib

            xinstall -m 755 -d ${destroot}${roots_path}/${root_name}/usr/include
            xinstall -m 644 ${worksrcpath}/include/__cxxabi_config.h ${destroot}${roots_path}/${root_name}/usr/include
            xinstall -m 644 ${worksrcpath}/include/cxxabi.h ${destroot}${roots_path}/${root_name}/usr/include

            system "cd ${destroot}${roots_path}/${root_name} && tar czf ../${root_name}.tgz ."
            file delete -force ${destroot}${roots_path}/${root_name}
        }
        if {[variant_isset runtime]} {
            # install as a runtime library that will not be picked up by the linker/editor!
            xinstall -m 755 ${worksrcpath}/lib/libc++abi.dylib ${destroot}${prefix}/lib/libc++abi.1.dylib
            system "install_name_tool -id ${prefix}/lib/libc++abi.1.dylib ${destroot}${prefix}/lib/libc++abi.1.dylib"
        }
    }
} else {
    distfiles
    patchfiles
    fetch {}
    build {}
    destroot {
        xinstall -d ${destroot}${prefix}/share/doc/${name}
        system "echo ${name} is an empty port on this OS version because the functionality is already provided by the OS. > ${destroot}${prefix}/share/doc/${name}/README.txt"
    }
}

livecheck.type none
