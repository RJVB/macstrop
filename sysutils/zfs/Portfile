# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           LTO 1.0

name                zfs
categories          sysutils filesystems

description         OpenZFS on OS X
long_description    OpenZFS on OS X (O3X) brings OpenZFS features to Apple's OS X.
homepage            http://openzfsonosx.org/
license             CDDL

maintainers         gmail.com:rjvbertin openmaintainer

platforms           darwin
installs_libs       yes

universal_variant   no

subport ${name}-devel {
    description     ${description} This port follows git/master.
    long_description \
                    ${long_description} This port follows git/master.
    if {${os.major} >= 14} {
        variant look_ma_no_sip description \
            "This port won't work without disabling a basic security feature: see `port info ${subport} +look_ma_no_sip`" {}
        if {[variant_isset look_ma_no_sip]} {
            long_description-append \nNB NB NB\n\
                "This port installs unsigned kexts, and those will not load without disabling SIP. You do so at your own risk."
        } else {
            pre-fetch {
                ui_error "This port won't build by default on OS X 10.10 and higher; see `port variants ${subport}`"
                return -code error "See `port variants ${subport}`"
            }
        }
    }
}

if {${subport} eq "${name}-devel"} {
    PortGroup       github 1.0
    depends_fetch-append \
                    bin:wget:wget
    conflicts       ${name}
    epoch           1
#     # spl-1.9.3-4-g4d1859e
#     set spl_commit  4d1859e3060bc1991d10d9215a3d1845e7caeaf8
#     # zfs-1.9.4
#     set zfs_commit  fa24877f83051301c07d5e41a8982f962de4dc10
#     master_sites    https://github.com/openzfsonosx/spl/tarball/${spl_commit}/:distspl \
#                     https://github.com/openzfsonosx/zfs/tarball/${zfs_commit}:distzfs
#     distfiles       spl-${spl_commit}${extract.suffix}:distspl \
#                     zfs-${zfs_commit}${extract.suffix}:distzfs
#     checksums       spl-${spl_commit}${extract.suffix} \
#                     rmd160  706523ff3d2c2ec53152841095561b141c3bdad4 \
#                     sha256  2a828bbe66d053a20ac67d34e5bfdc456c3b9aeb2879e9aab8b94dd93fb16553 \
#                     zfs-${zfs_commit}${extract.suffix} \
#                     rmd160  780ba772870856b4600e77260d937f25f8ce1b3e \
#                     sha256  24f145ec8ee694638b952f529db67047b5395cddca727b00ba9e11635faac220
#     zfs-macOS-2.1.0-4-g5b5034b44
    set zfs_commit  5b5034b449e46849fde1f70169701aa36e675c2a
    github.setup    openzfsonosx openzfs ${zfs_commit}
    version         2.1.0.4
    fetch {
        # not sure which OS version will do the fetch correctly...
        ui_info "--->  Fetching ${distname}${extract.suffix}"
        system "wget --no-check-certificate \"${github.master_sites}/${distfiles}\" \
                    --progress=bar:force -O ${distpath}/${distname}${extract.suffix}"
    }
    checksums       rmd160  f6f4dbf3e81d2283d21024e3dba38c2ff3c88db6 \
                    sha256  be7f8c0603770cbe114825631d070c641543f6d357979f00e42c1c080ce7c0f4
#                     size    33857685

    set PPREFIX     devel

    set worksrcpath ${workpath}/openzfs-${zfs_commit}

    # ensure we can load from ${prefix}/Library/Extensions and that we always auto-load the kext
    # (as was the case before).
    patchfiles-append \
                    devel/patch-kextloading.diff
    # don't warn about invalid arguments in some thread policy change attemps as
    # they could be due to OS version specifics.
    patchfiles-append \
                    devel/patch-threadpol-warnings.diff

    depends_build-append \
                    port:coreutils
} else {
    conflicts       ${name}-devel
    version         1.8.2
    if {[vercmp ${version} 1.8.1] == 0} {
        set dmg_version 1.8.2
    } else {
        set dmg_version ${version}
    }
    set spl_commit  spl-${version}
    set zfs_commit  zfs-${version}

    # Code borrowed from port:osxfuse, except that we make do with the single unsigned kext
    # warning on OS X 10.9.
    # Use the published signed kext for OS 10.9 (Darwin 13) and later.
    # Don't even fetch and extract the dmg unnecessarily, since the commands don't
    # work on some earlier OS versions.
    if {${os.major} >= 13} {
        set use_signed_kext    yes
    } else {
        # not currently available for this version?
        set use_signed_kext    no
    }
    set extdir ""
    set PPREFIX ""

    master_sites    https://github.com/openzfsonosx/spl/archive/:distspl \
                    https://github.com/openzfsonosx/zfs/archive/:distzfs
    distfiles       spl-${version}${extract.suffix}:distspl \
                    zfs-${version}${extract.suffix}:distzfs
    checksums       spl-${version}${extract.suffix} \
                    rmd160  21eab30802af1d9732f88cebfb0cd31339e300f1 \
                    sha256  502371ba9c0ad624acf8a7ae68f86f5f9fae1126c9a1c3c48ce5c7cc2c8cba9c \
                    zfs-${version}${extract.suffix} \
                    rmd160  c9a8f6b96ec4b7af1acf78266d12a5826e212555 \
                    sha256  39216008f096f043a751cd2a36becf99c4fbb2d1ec844b58ee1f93d9bdd73a0f
    if {${use_signed_kext}} {
        master_sites-append \
                    https://openzfsonosx.org/w/images/f/fb/:officialDmg
        distfiles-append \
                    OpenZFS_on_OS_X_${dmg_version}.dmg:officialDmg
        checksums-append \
                    OpenZFS_on_OS_X_${dmg_version}.dmg \
                    rmd160  7c9fe204e30d83cdef2ee890da279413cf7b7692 \
                    sha256  d1f23704127e22ead62972ba668d3dfce0718b9f7ee1092dec014244eff73f61
        # extract phase will just extract the dmg; post-extract will expand
        # the tarballs
        use_dmg yes
        extract.only OpenZFS_on_OS_X_${dmg_version}.dmg

        post-extract {
            ui_msg "--->    Extracting kernel extensions from the official OpenZFS_on_OS_X_${dmg_version}.dmg install image"
            switch ${os.major} {
                # 10.9
                "13" {
                    system -W ${workpath}/${name}-${version} "pkgutil --expand 'OpenZFS on OS X ${version} Mavericks.pkg' ${workpath}/pkg"
                    system -W ${workpath}/pkg/zfs109.pkg "gzip -dc Payload | cpio -id"
                    file rename ${workpath}/pkg/zfs109.pkg ${workpath}/pkg/official.pkg
                }
                # 10.10
                "14" {
                    system -W ${workpath}/${name}-${version} "pkgutil --expand 'OpenZFS on OS X ${version} Yosemite.pkg' ${workpath}/pkg"
                    system -W ${workpath}/pkg/zfs1010.pkg "gzip -dc Payload | cpio -id"
                    file rename ${workpath}/pkg/zfs1010.pkg ${workpath}/pkg/official.pkg
                }
                # 10.11
                "15" {
                    system -W ${workpath}/${name}-${version} "pkgutil --expand 'OpenZFS on OS X ${version} El Capitan.pkg' ${workpath}/pkg"
                    system -W ${workpath}/pkg/zfs1011.pkg "gzip -dc Payload | cpio -id"
                    file rename ${workpath}/pkg/zfs1011.pkg ${workpath}/pkg/official.pkg
                }
                # 10.12
                "16" {
                    system -W ${workpath}/${name}-${version} "pkgutil --expand 'OpenZFS on OS X ${version} Sierra.pkg' ${workpath}/pkg"
                    system -W ${workpath}/pkg/zfs1012.pkg "gzip -dc Payload | cpio -id"
                    file rename ${workpath}/pkg/zfs1012.pkg ${workpath}/pkg/official.pkg
                }
                # 10.13
                "17" {
                    system -W ${workpath}/${name}-${version} "pkgutil --expand 'OpenZFS on OS X ${version} High Sierra.pkg' ${workpath}/pkg"
                    system -W ${workpath}/pkg/zfs1013.pkg "gzip -dc Payload | cpio -id"
                    file rename ${workpath}/pkg/zfs1013.pkg ${workpath}/pkg/official.pkg
                }
                # 10.14
                "18" {
                    system -W ${workpath}/${name}-${version} "pkgutil --expand 'OpenZFS on OS X ${version} Mojave.pkg' ${workpath}/pkg"
                    system -W ${workpath}/pkg/zfs1014.pkg "gzip -dc Payload | cpio -id"
                    file rename ${workpath}/pkg/zfs1014.pkg ${workpath}/pkg/official.pkg
                }
                default {
                    ui_error "Darwin ${os.major}.${os.minor} is an as-yet unsupport OS X version"
                    return -code error "Unsupported OS X version"
                }
            }

            # Extract the source tarballs
            foreach tarball {spl zfs} {
                system -W ${workpath} "tar -xf ${distpath}/${tarball}-${version}.tar.gz"
            }
            # cleanup
            file delete -force ${workpath}/${name}-${version}
        }
        post-destroot {
            # replace our home-built kexts with the signed ones from the official dmg
            file delete -force ${destroot}${prefix}/Library/Extensions/spl.kext
            file delete -force ${destroot}${prefix}/Library/Extensions/zfs.kext
            system "ditto --rsrc ${workpath}/pkg/official.pkg/Library/Extensions/spl.kext ${destroot}${prefix}/Library/Extensions/spl.kext"
            system "ditto --rsrc ${workpath}/pkg/official.pkg/Library/Extensions/zfs.kext ${destroot}${prefix}/Library/Extensions/zfs.kext"
            system -W ${destroot}${prefix}/Library/Extensions/ "chown -R root:wheel spl.kext zfs.kext"
        }
    }
    # ensure we can load from ${prefix}/Library/Extensions
    patchfiles-append   patch-kextloading.diff 
    patchfiles-append \
                    patch-dont-invoke-launchctl.diff \
                    patch-libblkid-test.diff
    configure.dir   ${workpath}/zfs-${zfs_commit}
}

post-extract {
    file delete -force zfs-${version}
    file delete -force [glob -nocomplain ${workpath}/openzfsonosx-openzfs-*]
}

use_configure       yes
patch.pre_args      -Np1

depends_build-append \
                    port:autoconf \
                    port:automake \
                    port:libtool

depends_lib-append  port:zlib

build.dir           ${configure.dir}
patch.dir           ${configure.dir}

# build.env-append            "MAKEFLAGS=LIBTOOL=${prefix}/bin/rlibtool"
# destroot.env-append         "MAKEFLAGS=LIBTOOL=${prefix}/bin/rlibtool"
# if {[info exists configure.ar]} {
#     build.env-append        SLIBTOOL_AR="${configure.ar}" \
#                             SLIBTOOL_RANLIB="${configure.ranlib}"
#     destroot.env-append     SLIBTOOL_AR="${configure.ar}" \
#                             SLIBTOOL_RANLIB="${configure.ranlib}"
# }

if {"${os.major}" <= 13 && ${configure.sdkroot} eq ""} {
    # The ZFS code contains conditional code that uses functions available only
    # in 10.10+. For detection to work correctly, 10.9 and earlier must build using
    # their dedicated SDKs.
    set configure.sdkroot \
                    "$developer_dir/Platforms/MacOSX.platform/Developer/SDKs/MacOSX${configure.sdk_version}.sdk"
}

configure.ldflags-append \
                    ${configure.optflags} -mmacosx-version-min=${configure.sdk_version}

configure.args-append \
                    --disable-dependency-tracking \
                    --with-kernel-modprefix=${prefix}/Library/Extensions
configure.cflags-append\
                    -mmacosx-version-min=${configure.sdk_version}
configure.cxxflags-append\
                    -mmacosx-version-min=${configure.sdk_version}

build.args-append   V=1

destroot.args-append \
                    V=1

if {${subport} eq "${name}"} {
    pre-configure {
        if {![file exists ${workpath}/spl-${spl_commit}/configure]} {
            ui_msg "--->    Preparing SPL"
            system -W ${workpath}/spl-${spl_commit} "./autogen.sh"
        }
        if {![file exists ${workpath}/zfs-${zfs_commit}/configure]} {
            ui_msg "--->    Preparing ZFS"
            system -W ${workpath}/zfs-${zfs_commit} "./autogen.sh"
        }
    #     reinplace "s|`mktemp`|`mktemp ./zfsdevXXXXXX`|g" ${workpath}/zfs-${zfs_commit}/configure
        ui_msg "--->    Configuring SPL"
        system -W ${workpath}/spl-${spl_commit} "${configure.cmd} ${configure.pre_args} ${configure.args}"

        ui_msg "--->    Configuring ZFS"
        configure.args-append \
                        --with-spl=${workpath}/spl-${spl_commit}
    }

    pre-build {
        ui_msg "--->    Building SPL"
        system -W ${workpath}/spl-${spl_commit} "${build.cmd} ${build.pre_args} ${build.args}"

        ui_msg "--->    Building ZFS"
    }

    pre-destroot {
        ui_msg "--->    Staging SPL into destroot"
        system -W ${workpath}/spl-${spl_commit} "${destroot.cmd} ${destroot.pre_args} ${destroot.args} ${destroot.post_args}"

        ui_msg "--->    Staging ZFS into destroot"
    }
} else {
    pre-configure {
        if {![file exists ${worksrcpath}/configure]} {
            ui_msg "--->    Preparing ZFS"
            system -W ${worksrcpath} "./autogen.sh"
        }
    }
}

post-destroot {
    foreach d [glob -nocomplain ${destroot}/Library/*] {
        file rename ${d} ${destroot}${prefix}/Library/
    }
    xinstall -m 755 ${filespath}/${PPREFIX}/zfs_unload_kexts.sh ${destroot}${prefix}/sbin/zfs_unload_kexts.sh
    xinstall -m 755 ${filespath}/${PPREFIX}/zfs_kexts_loaded.sh ${destroot}${prefix}/sbin/zfs_kexts_loaded.sh
    xinstall -m 755 ${filespath}/zfsopt.sh ${destroot}${prefix}/bin/zfsopt
    # librt contains clock_gettime, among others, which can mess up building e.g. python 3.10 on OS versions
    # that don't have a header for clock_gettime.
    file delete -force ${destroot}${prefix}/lib/librt.a
}

# keep this in sync with the list in etc/launchd/daemons/launchd-daemons.am
# but exclude the import-all plist as well as the zed plist which causes the
# kexts to load.
set launchddaemon_DATA {org.openzfsonosx.zconfigd.plist \
                        org.openzfsonosx.InvariantDisks.plist}

pre-deactivate {
    if {[catch {exec ${prefix}/sbin/zfs_kexts_loaded.sh} result]} {
        ui_debug "ZFS and SPL kexts aren't loaded: allowing deactivation"
        foreach p ${launchddaemon_DATA} {
            # unload the plists just as the standard install process does
            # (keep in sync whenever patchfile:patch-dont-invoke-launchctl.diff needs refactoring!)
            set pl "${prefix}/Library/LaunchDaemons/${p}"
            if {[file exists ${pl}]} {
                if {[catch {system "launchctl unload -F ${pl}"} err]} {
                    ui_warn "error unloading ${pl}: ${err}"
                }
            }
        }
    } else {
        ui_error ${result}
        ui_error "Unmount all ZFS pools and then invoke zfs_unload_kexts.sh before deactivating port:${subport}"
        # Being able to use `return -code error` here would be nice
        set thePid [pid]
        # but the only way to interrupt deactivation ATM is to send ourselves a SIGKILL
        system "kill -9 ${thePid}"
    }
}

post-activate {
    if {${subport} eq "${name}"} {
        set kexts "-d ${prefix}/Library/Extensions/spl.kext ${prefix}/Library/Extensions/zfs.kext"
    } else {
        set kexts "${prefix}/Library/Extensions/zfs.kext"
    }
    # do a diag run of the kext we just installed, but catch any errors!
    if {[catch {exec kextutil -n -print-diagnostics -v 6 ${kexts}} result]} {
        ui_msg "Checking zfs.kext:\n${result}"
        foreach p ${launchddaemon_DATA} {
            # load the plists just as the standard install process does
            # (keep in sync whenever patchfile:patch-dont-invoke-launchctl.diff needs refactoring!)
            set pl "${prefix}/Library/LaunchDaemons/${p}"
            if {[file exists ${pl}]} {
                if {[catch {system "launchctl load -wF ${pl}"} err]} {
                    ui_warn "error loading ${pl}: ${err}"
                } else {
                    ui_msg "Loaded ${pl} (${err})"
                }
            }
        }
    } else {
        ui_error "Error checking zfs.kext:\n${result}"
    }
}

notes-append "You can test this port as follows: \n\
# create a file for a 128m disk image: \n\
dd if=/dev/zero of=/path/to/tank.pool.bin bs=1024k count=128 \n\
# create a ZFS pool called 'tank' in that disk image (ashift=12 is for modern disk with 4k blocks) \n\
# and the normalisation option is to avoid issues displaying files with non-ASCII names: \n\
zpool create -o ashift=12 -O normalization=formD tank /path/to/tank.pool.bin \n\
zpool status -v \n\
# there should now be a new volume: \n\
df -H \n\
ls -la /Volumes/tank \n\
# get the pool's properties: \n\
zpool get all tank \n\
# get the pool's main dataset's properties: \n\
zfs get all tank \n\
# set lz4 compression and 2-way, single-device redundancy:\n\
zfs set compression=lz4 tank \n\
zfs set copies=2 tank \n\
# export (unmount) the pool and all its datasets: \n\
zpool export tank \n\
# to import the pool once more:\n\
zpool import -d /path/to tank \n\
# to see all available pools on the connected storage devices:\n\
zpool import -d /dev \n\
\n\
See ${homepage} for more information about ZFS and its OS X implementation.
"

livecheck.url   http://openzfsonosx.org/wiki/Downloads
livecheck.regex OpenZFS_on_OS_X_(\\d+(\\.\\d+)+)
