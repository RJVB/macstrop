# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           muniversal 1.0
PortGroup           github 1.1

# unpack_armv7.S:1:1: error: unexpected token at start of statement
lappend merger_configure_args(arm64)    --disable-asm

# if host is not set in 32-bit mode, wrong assembly code is used
lappend merger_configure_args(i386)     --host=i686-apple-${os.platform}${os.version}

if {!(${universal_possible} && [variant_isset universal])
        && ![info exists merger_configure_args(${configure.build_arch})]
        && ${os.platform} ne "darwin"
} {
    # annoyingly, the CMake file misdetects the available assemblers on Mac
    PortGroup       cmake 1.1
    set use_cmake   yes
}
PortGroup           LTO 1.0
PortGroup           save_configure_cmd 1.0

github.tarball_from archive
github.setup        dbry wavpack 5.9.0

checksums           rmd160  79070dcc8766da2be1c3e947d05910d12a6bc901 \
                    sha256  c431da5ba4953b63d12e06fea005162ef4f0bc1b3513943c1e8a842567cda2cf
#                     size    2136686

categories          audio
license             BSD
maintainers         nomaintainer
description         Hybrid lossless audio compression
long_description    WavPack is a completely open audio compression format \
                    providing lossless, high-quality lossy, and a unique \
                    hybrid compression mode. Although the technology is \
                    loosely based on previous versions of WavPack, the new \
                    version 4 format has been designed from the ground up \
                    to offer unparalleled performance and functionality.
homepage            https://www.wavpack.com
use_zip             yes

depends_lib         port:libiconv

if {![tbool use_cmake]} {
    use_autoreconf  yes
    pre-configure {
        if {[file exists ${worksrcpath}/configure]} {
            use_autoreconf no
        }
    }
    configure.args-append \
                    --enable-libiconv \
                    --enable-legacy \
                    --enable-rpath
} else {
    configure.args-append \
                    -DWAVPACK_ENABLE_LIBICONV=ON \
                    -DWAVPACK_ENABLE_LEGACY=ON \
                    -DWAVPACK_INSTALL_CMAKE_MODULE=ON \
                    -DWAVPACK_INSTALL_PKGCONFIG_MODULE=ON \
                    -DBUILD_SHARED_LIBS=ON
}

platform darwin {
    # handrolled asm has issues on older platforms.  Disable it if we don't have access to newer compilers.
    # Note that "libc++" is really a proxy here for "configuration that supports newer compilers"; the C++
    # runtime actually isn't relevant other than being a requirement of clang-3.5+.
    if {${configure.cxx_stdlib} eq "libc++"} {
        compiler.blacklist {macports-clang-3.[34]} {clang < 500} *gcc*
    } else {
        if {[tbool use_cmake]} {
            configure.args-append \
                    -DWAVPACK_ENABLE_ASM=OFF
        } else {
            configure.args-append \
                    --disable-asm
        }
    }
}

if {!(${universal_possible} && [variant_isset universal]) && [info exists merger_configure_args(${configure.build_arch})]} {
    configure.args-append   {*}$merger_configure_args(${configure.build_arch})
}

livecheck.type      regex
livecheck.url       ${homepage}/downloads.html
livecheck.regex     "${name}-(\\d+(?:\\.\\d+)*)${extract.suffix}"
