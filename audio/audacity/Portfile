# -*- coding: utf-8; mode: tcl; tab-width: 4; truncate-lines: t; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:et:sw=4:ts=4:sts=4

PortSystem          1.0

PortGroup           github 1.1

# placeholder name
name                audacity
categories          audio
maintainers         {gmail.com:rjvbertin @RJVB} openmaintainer
platforms           darwin
license             GPL-2+
universal_variant   no

if {${os.platform} eq "darwin"} {
    set aud_app_path ${applications_dir}/Audacity.app
} else {
    set aud_app_path ${prefix}/bin/audacity
}

patch.pre_args      -p1

# The manual subport is defined first since it doesn't require a lot of setting up
subport "${name}-manual" {}
if {${subport} eq "${name}-manual"} {
    description     The manual for audacity
    long_description {*}${description}. Users of port:audacity2-legacy may prefer to use the \
                    online manual instead of installing this port.
    github.setup    audacity audacity-manual 3.5.1 v
    homepage        https://manual.audacityteam.org/
    checksums       rmd160  a2394c21f7b5d27cc45208b226c743051278a41b \
                    sha256  a87f9bdd774ee7ce5a4a4e5e0783ffa7cb28913dcffe7310efc065471a14cf93
#                     size    22377404
    depends_run     path:${aud_app_path}:audacity
    worksrcdir      help
    use_configure   no
    build           {}
    installs_libs   no
    supported_archs noarch
    platforms       any
    destroot {
        platform darwin {
            xinstall -m 755 -d ${destroot}${aud_app_path}/Contents/help
            file copy ${worksrcpath}/manual ${destroot}${aud_app_path}/Contents/help
        }
        platform linux {
            xinstall -m 755 -d ${destroot}${prefix}/share/audacity/help
            file copy ${worksrcpath}/manual ${destroot}${prefix}/share/audacity/help
        }
    }
    return          0
}

set VST3VERSION 3.7.7

subport vst3sdk-src {}
if {${subport} eq "vst3sdk-src"} {
    description     VST 3 Plug-In SDK (sources)
    long_description \
                    VST 3 is a general rework of the long-serving VST plug-in interface. \
                    It is not compatible with the older VST versions, but it includes some \
                    new features and possibilities. We have redesigned the API to make it \
                    not only far easier and more reliable for developers to work with, but \
                    have also provided completely new possibilities for plug-ins.\n\
                    This port installs the sources, patched for use by Audacity!
    license         Steinberg-VST3
    github.tarball_from \
                    archive
    github.setup steinbergmedia vst3sdk ${VST3VERSION} v _build_19
    version     ${VST3VERSION}
    # the interesting things are all in submodules, so we need to git-fetch...
    fetch.type      git
    distname        vst3sdk

    depends_build   port:bzip2 \
                    port:libarchive \
                    port:dos2unix
    platform linux {
        depends_build-append \
                    port:coreutils
    }
    post-extract {
        system -W ${worksrcpath} "dos2unix cmake/modules/*"
    }

    # patch applied by Audacity:
    patchfiles-append \
                    vst3/disable-validator.diff
    if {[vercmp ${xcodeversion} 9.0] < 0} {
        # v3.7.7 claims to require at least Xcode 9. For lesser versions
        # we apply the patch below, which was tested on 10.9.5 (and may
        # still require 10.9). We'll assume that Xcode 9 is as sufficient
        # requirement (= that upstream actually test with that version).
        patchfiles-append \
                    vst3/patch-support-10.9-and-maybe-earlier.diff
    }
    # don't build Jack support:
    patchfiles-append \
                    vst3/patch-disable-jack.diff

    use_configure   no
    installs_libs   no
    supported_archs noarch
    platforms       any

    set dest        ${prefix}/src/vst3sdk/
    build       {
        system -W ${workpath} "bsdtar -cvj --exclude .git --exclude .gitmodules -f ${version}.tar.bz2 vst3sdk"
    }
    destroot {
        xinstall -m 755 -d ${destroot}${dest}
        xinstall -m 644 ${workpath}/${version}.tar.bz2 ${destroot}${dest}/
    }
    return          0
}

# Now we can load the PortGroups for the main ports
PortGroup           wxWidgets 1.0
PortGroup           active_variants 1.1
PortGroup           cmake 1.1

#>> RJVB
PortGroup           compress_workdir 1.0
PortGroup           LTO 1.0
PortGroup           from_ports_variant 1.0
## only when upgrading the port version:
## PortGroup           patch-updater 1.0
#<< RJVB

patch.pre_args      -Np1

subport audacity-wxWidgets {}

dist_subdir         ${name}

# default to our own "stealth" wxWidgets prefix; these will be reset 
# by wxWidget.use if +wxsystem
wxWidgets.name      wxWidgets
wxWidgets.version   audacity-3.1
wxWidgets.prefix    ${prefix}/libexec/${wxWidgets.name}/${wxWidgets.version}
wxWidgets.wxdir     ${wxWidgets.prefix}/bin
wxWidgets.wxconfig  ${wxWidgets.wxdir}/wx-config
wxWidgets.wxrc      ${wxWidgets.wxdir}/wxrc
# dependencies for wxWidgets:
set awx_deps        {path:include/turbojpeg.h:libjpeg-turbo \
                    port:tiff
                    port:libpng \
                    port:zlib \
                    port:libiconv}

supported_archs     x86_64 arm64
cmake.generator     Ninja
if {${build.cmd} ne "ninja"} {
    build.args-append   V=1 VERBOSE=1
    build.post_args-append -k
} else {
    build.pre_args-prepend -k 0
}


#>> RJVB
cmake.save_configure_cmd "install log"
#<< RJVB

if {${subport} eq "audacity" || ${subport} eq "audacity-wxWidgets"} {
    compiler.blacklist {clang < 602}
    foreach clv {3.3 3.4 3.5 3.7 5.0 6.0 7.0 8.0 9.0 10 11 12 13 14} {
        compiler.blacklist-append macports-clang-${clv}
    }
    foreach clv {15 16 17 18 19 20} {
        if {[file exists ${prefix}/bin/clang-mp-${clv}]} {
            compiler.whitelist-prepend macports-clang-${clv}
        }
        compiler.fallback-prepend macports-clang-${clv}
    }
}

# ensure pre-compiled headers and the possible use of ccache can co-exist
configure.env-append \
                    "CCACHE_SLOPPINESS=pch_defines,time_macros"
build.env-append    "CCACHE_SLOPPINESS=pch_defines,time_macros"

proc if_not_ccache {} {
    global configure.ccache
    if {[tbool configure.ccache]} {
        return "OFF"
    } else {
        return "ON"
    }
}

if {${subport} eq "audacity"} {
    ######### main port #########

description         A Free, Cross-Platform Digital Audio Editor.
long_description    Audacity is a free, easy-to-use, multi-track audio editor and recorder \
                    for Windows, Mac OS X, GNU/Linux and other operating systems. The interface is translated \
                    into many languages. You can use Audacity to: \n\
                    Record live audio. \n\
                    Record computer playback on any Windows Vista or later machine. \n\
                    Convert tapes and records into digital recordings or CDs. \n\
                    Edit WAV, AIFF, FLAC, MP2, MP3 or Ogg Vorbis sound files. \n\
                    AC3, M4A/M4R (AAC), WMA and other formats supported using optional libraries. \n\
                    Cut, copy, splice or mix sounds together. \n\
                    Numerous effects including change the speed or pitch of a recording. \n\
                    And more!

conflicts           audacity2-legacy audacity3-legacy audacity-legacy audacity-gtk
## # 2 commits after the 3.5.1 release:
## github.setup        audacity audacity 0ffa73b9f44acc339e12a350edadd96590dc4b17
## version             3.5.1.2 ; revision 1
## last commit with with 3.3 source code layout: 2677399f41473fd1cd13f607018475741350c0e1
git.shallow_since   2024-01-01
github.setup        audacity audacity 2677399f41473fd1cd13f607018475741350c0e1
version             3.5.1.409 ; revision 1
## From the CMake file:
# Set this value to 0 for alpha, 1 for beta, 2 for release builds
set build_level     2

# this port is intended to facilitate development efforts shared with "upstreams"
# so using git instead of a tarball fetch type is more appropriate for the current maintainer
fetch.type          git
distname            audacity-git
set PPREFIX         3.2.5/

platform darwin {
    #>> RJVB ; set this to 15 for mainstream!
    set min_OS_for_wx32 13
    #<< RJVB
}

if {${os.platform} eq "darwin"} {
    if {${os.major} < 13} {
        replaced_by audacity3-legacy
        PortGroup   obsolete 1.0
        return      0
    } else {
        if {${os.major} >= ${min_OS_for_wx32}} {
            variant wxsystem description {Depend on wxWidgets from MacPorts} {}
            default_variants +wxsystem
        }
    }
} else {
    variant wxsystem description {Depend on wxWidgets from MacPorts} {}
    default_variants +wxsystem
}

compiler.cxx_standard 2017

proc add_patchfile_ifexists {args} {
    global PPREFIX filespath
    foreach fname ${args} {
        if {[file exists ${filespath}/${fname}]} {
            patchfiles-append ${fname}
        }
    }
}

homepage            https://www.audacityteam.org/

## Audacity's build depends on a python3 interpreter but it will find a suitable one from the installed versions
## itself - and at least one must be installed because it's an indirect dependency of port:git .

if {[variant_isset wxsystem]} {
    wxWidgets.use   wxWidgets-3.2

    require_active_variants ${wxWidgets.port} "" stdlib

    depends_lib-append \
                    port:${wxWidgets.port}
} else {
    if {${os.platform} eq "darwin"} {
        # wxWidgets will be embedded in the app bundle
        depends_build-append \
                    port:audacity-wxWidgets
    } else {
        depends_lib-append \
                    port:audacity-wxWidgets
    }
    depends_lib-append \
                    {*}${awx_deps}
}

depends_build-append \
                    port:pkgconfig \
                    port:rapidjson \
                    port:nasm
platform darwin {
    depends_build-append \
                    port:cctools
}

depends_lib-append  port:freetype \
                    path:lib/pkgconfig/harfbuzz.pc:harfbuzz \
                    port:portaudio \
                    port:soundtouch \
                    port:soxr \
                    port:twolame \
                    port:expat \
                    path:bin/ffmpeg:ffmpeg \
                    port:flac \
                    port:lame \
                    port:libogg \
                    port:libsndfile \
                    port:libvorbis \
                    port:sqlite3
if {${os.arch} ne "arm"} {
    depends_lib-append \
                    port:libid3tag \
                    port:libmad
}
depends_lib-append  port:mpg123 \
                    port:wavpack
platform linux {
    depends_lib-append \
                    port:util-linux
}
# audacity could use port:lv2 and port:lilv but would also require a port:suil for that to be possible.

# 4 lines of Mac-specific comments
#
#
#
patchfiles-append   patch-cmakefiles-@prefix@.diff \
                    patch-build-preparations.diff \
                    patch-fix_bundlepy-@prefix@.diff

platform darwin {
    if {[variant_isset wxsystem]} {
        # make certain we're not embedding any wxWidgets libraries
        add_patchfile_ifexists \
                    patch-no-embedded-wxW.diff
    } else {
        # make certain we're embedding our wxWidgets libraries
        add_patchfile_ifexists \
                    patch-embed-wxW.diff
    }
}

patchfiles-append   patch-aboutdlg.diff \
                    add_enGB_translation.diff \
                    patch-libnyquist-symbol-visibility.diff \
                    patch-ffmpeg-functions.diff

platform darwin {
    # use lame from MacPorts (partial revert of ae431bc5)
    patchfiles-append \
                    patch-more-decent-font-sizes.diff \
                    patch-use-MPlame.diff
#     add_patchfile_ifexists \
#                     patch-no-sdk1013.diff
}
platform linux {
    patchfiles-append \
                    patch-rearrange-control-buttons.diff
    # use lame from MacPorts (partial revert of ae431bc5)
    patchfiles-append \
                    patch-use-MPlame.diff
    # I've begun to be hit by a known bug in libstdc++ and/or glibc, causing std::call_once
    # to hang after a call to it was interrupted by an exception (cf. https://gcc.gnu.org/bugzilla/show_bug.cgi?id=66146
    # and yes, it can happen on x86 too). Replace all calls to the function with something that
    # has the same effect.
    add_patchfile_ifexists \
                    ${PPREFIX}patch-linux-call_once.diff \
                    ${PPREFIX}patch-fix-owner_less.diff \
                    patch-call-XInitThreads.diff
}
patchfiles-append   patch-enable-compact-menu.diff
add_patchfile_ifexists \
                    ${PPREFIX}patch-temp-fixes.diff

platform darwin {
    variant suil description {Build the GUI library for LV2 plugins\; requires Qt5} {
        PortGroup   qt5 1.0
    }
}

if {${os.platform} eq "darwin"} {
    if {${os.major} >= 17} {
        # Audacity builds "normally" from 10.7 to 10.13, but will request the 10.13 SDK
        # on 10.14 and newer to "prevent high CPU usage and slow drawing". Linking will
        # fail if MacPort lets source be compiled against the platform SDK, so we handle
        # SDK selection ourselves entirely. MP builds aren't required to support anything
        # but the OS version they were built for. On 11.x we just build as if there are
        # no OS specifics.
        if {${os.major} < 20} {
            macosx_deployment_target 10.13
            patchfiles-delete patch-no-sdk1013.diff
            configure.sdkroot
        }
    } elseif {${os.major} == 13} {
        macosx_deployment_target 10.9
        configure.sdkroot \
                    ${developer_dir}/Platforms/MacOSX.platform/Developer/SDKs/MacOSX10.10.sdk
    }
}

# make it find PortAudio again
patchfiles-append   patch-portaudio-cmake-alias.diff
patchfiles-append   patch-cmake-dont-strip.diff
# avoid a race-like condition when quitting:
patchfiles-append   patch-scrubui-issue-9585.diff

patchfiles-append   patch-fix-@version@.diff \
                    patch-conan-@cxx_stdlib@.diff \
                    patch-viewport-racelike-condition.diff
# restore theme blending to avoid psychedelic colours
patchfiles-append   patch-restore-theme-blending.diff

# backport FFmpeg8 support
patchfiles-append   patch-ffmpeg8-support.diff

# quelch a huge number of warnings
configure.cxxflags-append \
                    -Wno-inconsistent-missing-override \
                    -Wno-overloaded-virtual

# if {${subport} eq "${name}"} {
# #>> RJVB
#     ## uncomment only when intending to regenerate the patchfiles
#     ## Prerequisites:
#     ## - git-unpatch
#     ## - checkout -b <some scratch branch>
#     ## Or use the patch-updater 1.0 PortGroup !!
#     patch {
#         foreach p ${patchfiles} {
#             set pf [file join ${filespath} ${p}]
#             ui_info "--> applying patch ${p} (${pf})"
#             system -W ${worksrcpath} "${patch.cmd} ${patch.pre_args} -i ${pf}"
#             if {${p} eq "add_enGB_translation.diff"} {
#                 system -W ${worksrcpath} "git add locale/en_GB.po"
#             }
#             system -W ${worksrcpath} "git diff --no-ext-diff HEAD -- . > ${pf}"
#             system -W ${worksrcpath} "git commit -v -a -m \"${pf}\""
#         }
#     }
# #<< RJVB
# }

platform darwin {
    # set the install_rpath first
    cmake.install_rpath-append \
                    ${prefix}/lib/audacity \
                    ${aud_app_path}/Contents/Frameworks
    if {[variant_isset wxsystem]} {
        cmake.install_rpath-append \
                    ${wxWidgets.prefix}/lib
    } else {
        configure.ldflags-append \
                    -Wl,-rpath,${prefix}/lib/audacity \
                    -Wl,-rpath,${aud_app_path}/Contents/Frameworks
    }
    if {${subport} eq "${name}"} {
        # now change the prefix
        cmake.install_prefix ${applications_dir}
    }
    configure.pre_args-replace \
                    -DCMAKE_INSTALL_NAME_DIR="${cmake.install_prefix}/lib" \
                    -DCMAKE_INSTALL_NAME_DIR="${aud_app_path}/Contents/Frameworks"
    set AUDIOUNITS  on
}
platform linux {
    cmake.install_rpath-append \
                    ${prefix}/lib/audacity
    configure.ldflags-append \
                    -Wl,-rpath,${prefix}/lib/audacity
    cmake.install_rpath-append \
                    ${wxWidgets.prefix}/lib/
    configure.ldflags-append \
                    -Wl,-rpath,${wxWidgets.prefix}/lib/
    # just add the Qt5 lib path. We attempt not to build the Qt5 suil plugin
    # but it seems the build system doesn't heed that instruction so we can
    # just as well make certain it uses our libraries if they're there.
    cmake.install_rpath-append \
                    ${prefix}/libexec/qt5/lib
    configure.ldflags-append \
                    -Wl,-rpath,${prefix}/libexec/qt5/lib
    set AUDIOUNITS  off
}

set _OPT "audacity_"
configure.args-append \
                    -DAUDACITY_BUILD_LEVEL=${build_level} \
                    -DCMAKE_MACOSX_RPATH=ON \
                    -DCMAKE_STRIP=/bin/echo \
                    -D${_OPT}lib_preference=system \
                    -D${_OPT}has_tests=off \
                    -D${_OPT}has_sentry_reporting=off \
                    -D${_OPT}has_crashreports=off \
                    -D${_OPT}has_updates_check=off \
                    -D${_OPT}conan_allow_prebuilt_binaries=off \
                    -D${_OPT}use_audio_units=${AUDIOUNITS} \
                    -D${_OPT}use_ffmpeg=loaded \
                    -D${_OPT}use_soxr=system \
                    -D${_OPT}use_vamp=local \
                    -D${_OPT}use_lv2=local \
                    -D${_OPT}use_suil_qt5=OFF \
                    -D${_OPT}use_sbsms=local \
                    -D${_OPT}use_soundtouch=system \
                    -D${_OPT}use_twolame=system \
                    -D${_OPT}use_midi=system \
                    -D${_OPT}has_vst3=OFF \
                    -D${_OPT}use_sqlite=system \
                    -D${_OPT}use_pch=[if_not_ccache] \
                    -DCMAKE_DISABLE_FIND_PACKAGE_JACK=On
## Is this automatic nowadays?
# if {${os.arch} eq "arm"} {
#     configure.args-append \
#                     -D${_OPT}use_mad=off \
#                     -D${_OPT}use_id3tag=off
# }

# in this port implementation we always get wxWidgets from the system
# (but we embed the libraries if building -wxsystem)
configure.args-append \
                    -DwxWidgets_CONFIG_EXECUTABLE=${wxWidgets.wxconfig} \
                    -D${_OPT}use_wxwidgets=system
configure.env-append \
                    WX_CONFIG=${wxWidgets.wxconfig}

if {[variant_exists suil] && [variant_isset suil]} {
    configure.args-replace \
                    -D${_OPT}use_suil_qt5=OFF \
                    -D${_OPT}use_suil_qt5=ON
}
platform linux {
    configure.cppflags-append \
                    -I${prefix}/include -DUSE_AQUA_THEME
    variant jack description {enable JACK support} {}
    if {[variant_isset jack]} {
        # disable the find_package disabler!
        configure.args-replace \
                    -DCMAKE_DISABLE_FIND_PACKAGE_JACK=On \
                    -DCMAKE_DISABLE_FIND_PACKAGE_JACK=Off
        require_active_variants portaudio jack
    } else {
        require_active_variants portaudio "" jack
    }
}

patchfiles-append   patch-parallel-vst3-build.diff

variant vst3 description {enable support for VST3 plugins} {}
if {[variant_exists vst3] && [variant_isset vst3]} {
    depends_build-append \
                    port:vst3sdk-src \
                    port:libarchive
    pre-configure {
        if {![file exists ${worksrcpath}/vst3sdk]} {
            system -W ${worksrcpath} "bsdtar -x --exclude doc -f ${prefix}/src/vst3sdk/${VST3VERSION}.tar.bz2"
        }
    }
    # The build system will build the vst3 sdk during the configure step
    configure.env-append \
                    CMAKE_BUILD_PARALLEL_LEVEL=${build.jobs} \
                    BUILD_JOBS=${build.jobs}
    configure.args-replace \
                    -D${_OPT}has_vst3=OFF \
                    -D${_OPT}use_vst3sdk=system
}

# make sure the C++ stdlib argument is added to the LDFLAGS too
if {${configure.cxx_stdlib} ne "" && [string match *clang* ${configure.cxx}]} {
    configure.ldflags-append \
                    -stdlib=${configure.cxx_stdlib}
}

proc select_newer_ffmpeg {fvers} {
    global prefix configure.args cmake.module_path cmake.install_rpath
    PortGroup       conflicts_build 1.0
    depends_lib-replace \
                    path:bin/ffmpeg:ffmpeg \
                    port:${fvers}
    cmake.module_path-append \
                    ${prefix}/libexec/${fvers}
    cmake.install_rpath-append \
                    ${prefix}/libexec/${fvers}/lib
    conflicts_build ffmpeg-dev
}

variant ffmpeg6 conflicts ffmpeg7 ffmpeg8 description {build against ffmpeg6} {}
variant ffmpeg7 conflicts ffmpeg6 ffmpeg8 description {build against ffmpeg7} {}
variant ffmpeg8 conflicts ffmpeg6 ffmpeg7 description {build against ffmpeg8} {}
if {[variant_isset ffmpeg8]} {
    select_newer_ffmpeg ffmpeg8
} elseif {[variant_isset ffmpeg7]} {
    select_newer_ffmpeg ffmpeg7
} elseif {[variant_isset ffmpeg6]} {
    select_newer_ffmpeg ffmpeg6
} else {
    cmake.module_path
    depends_build-append \
                port:ffmpeg-dev
}

pre-configure {
    if {![file exists ${build.dir}/conan]} {
        ui_warn "The Conan build environment needs to be set up. It's as barbarian as the name suggests \
            so this will take some time!"
    }
    if {[variant_isset vst3] && ![file exists ${build.dir}/vst3sdk]} {
        ui_warn "The VST3 SDK will be built during the configure step ... patience required!"
    }
}

# with everything defined/declared as intended we can now
# handle the post-patch step with its reinplace statements.
post-patch {
    reinplace -W ${worksrcpath} "s|/usr/local|${prefix}|g" \
                                src/effects/ladspa/LadspaEffect.cpp \
                                src/effects/VST/VSTEffect.cpp \
                                modules/import-export/mod-mp3/ExportMP3.cpp \
                                lib-src/lv2/lilv/wscript \
                                lib-src/lv2/lilv/test/lilv_test.c
    reinplace -W ${worksrcpath} "s|/Library/Audio/Plug-Ins/Vamp|/Library/Audio/Plug-Ins/Vamp:${prefix}/lib/vamp|g" \
                                lib-src/libvamp/src/vamp-hostsdk/PluginHostAdapter.cpp
#     reinplace -W ${worksrcpath} "s|/Library/Application Support/audacity/libs|${prefix}/lib|g" \
#                                 modules/import-export/mod-ffmpeg/FFmpeg.h
    reinplace -W ${worksrcpath} "s|@PREFIX@|/opt/local|g" \
                                cmake-proxies/lv2/CMakeLists.txt \
                                scripts/build/macOS/fix_bundle.py \
                                modules/import-export/mod-ffmpeg/lib-ffmpeg-support/FFmpegFunctions.cpp
    set varray [split ${version} "."]
    reinplace -W ${worksrcpath} "s|@VERSION_MINOR@|[lindex ${varray} 1]|g" CMakeLists.txt
    reinplace -W ${worksrcpath} "s|@VERSION_PATCH@|[lindex ${varray} 2]|g" CMakeLists.txt
    if {${configure.cxx_stdlib} eq "" || [string match *libstdc* ${configure.cxx_stdlib}]} {
        reinplace -W ${worksrcpath} "s|@CXX_STDLIB@|libstdc++11|g" conan/helpers/profiles.py
    } else {
        reinplace -W ${worksrcpath} "s|@CXX_STDLIB@|libc++|g" conan/helpers/profiles.py
    }
}

pre-build {
    # register the exact source version in the build:
    if {[file exists ${worksrcpath}/mac/scripts/get_gitident.sh]} {
        system -W ${worksrcpath}/mac "scripts/get_gitident.sh"
        system "echo '#define GIT_DESC \"${version}\"' >> ${worksrcpath}/src/RevisionIdent.h"
    } else {
        system "echo '#define GIT_DESC \"${version}\"' > ${worksrcpath}/src/GitDesc.h"
    }
}
if {[info exists PatchUpdater::ignore_files]} {
    PatchUpdater::ignore_files \
            src/RevisionIdent.h \
            src/GitDesc.h
}

platform darwin {
    proc fixup_wx_libraries {} {
        global destroot aud_app_path wxWidgets.prefix
        if {[variant_isset wxsystem]} {
            # replace the embedded libraries with symlinks, so they're found via
            # the @executable_path rpath.
            foreach lwx [glob -nocomplain ${destroot}${aud_app_path}/Contents/Frameworks/libwx*] {
                file delete ${lwx}
                ln -s "${wxWidgets.prefix}/lib/[file tail ${lwx}]" ${lwx}
            }
        }
    }

    post-destroot {
        fixup_wx_libraries
        xinstall -m 755 -d ${destroot}${prefix}/share/audacity
        ln -s ${aud_app_path}/Contents/plug-ins ${destroot}${prefix}/share/audacity/
        xinstall -m 755 -d ${destroot}${prefix}/share/mime/packages
        xinstall -m 644 ${worksrcpath}/src/audacity.xml ${destroot}${prefix}/share/mime/packages/
        xinstall -m 755 -d ${destroot}${prefix}/share/appdata
        xinstall -m 644 ${worksrcpath}/help/audacity.appdata.xml ${destroot}${prefix}/share/appdata/
        xinstall -m 755 -d ${destroot}${prefix}/share/doc/Audacity
        xinstall -m 644 ${worksrcpath}/README.md ${worksrcpath}/LICENSE.txt ${destroot}${prefix}/share/doc/Audacity
        xinstall -m 755 -d ${destroot}${prefix}/share/man/man1
        xinstall -m 644 ${worksrcpath}/help/audacity.1 ${destroot}${prefix}/share/man/man1/
        xinstall -m 755 -d ${destroot}${prefix}/share/icons/hicolor/scalable/apps
        xinstall -m 644 ${worksrcpath}/images/audacity.svg ${destroot}${prefix}/share/icons/hicolor/scalable/apps/
        foreach s {16 22 24 32 48} {
            xinstall -m 755 -d ${destroot}${prefix}/share/icons/hicolor/${s}x${s}
            xinstall -m 644 ${worksrcpath}/images/icons/${s}x${s}/audacity.png ${destroot}${prefix}/share/icons/hicolor/${s}x${s}
        }
        if {[variant_isset suil]} {
            file rename ${destroot}${aud_app_path}/Contents/Audacity.app/Contents/Frameworks/libsuil_qt5_in_cocoa.so \
                ${destroot}${aud_app_path}/Contents/Frameworks/
            ln -s libsuil_qt5_in_cocoa.so ${destroot}${aud_app_path}/Contents/Frameworks/libsuil_qt5_in_cocoa.dylib
        }
        if {[variant_isset wxsystem]} {
            foreach l [glob -nocomplain ${wxWidgets.prefix}/share/locale/*] {
                set lang [file tail ${l}]
                xinstall -m 755 -d ${destroot}${aud_app_path}/Contents/Resources/${lang}.lproj
                ln -s ${l}/LC_MESSAGES/wxstd.mo ${destroot}${aud_app_path}/Contents/Resources/${lang}.lproj/
            }
        }
        # allow the application to find certain things where it expects them on OS X (= inside the app bundle)
        foreach r [glob -nocomplain ${destroot}${prefix}/share/audacity/*] {
            set f [file tail ${r}]
            if {![file exists ${destroot}${aud_app_path}/Contents/Resources/${f}]} {
                ln -s ${prefix}/share/audacity/${f} ${destroot}${aud_app_path}/Contents/Resources/
            }
        }
        # install a wrapper script in ${prefix}/bin
        system "echo \"#!/bin/sh\nexec \\\"${aud_app_path}/Contents/MacOS/Wrapper\\\" \\\"\\\$\@\\\"\" > ${destroot}${prefix}/bin/audacity"
        system "chmod 755 ${destroot}${prefix}/bin/audacity"
    }
}
platform linux {
    post-destroot {
        if {[variant_isset wxsystem]} {
            # delete any of these
            foreach l [glob -nocomplain ${destroot}${prefix}/lib/audacity/libwx*.so] {
                file delete ${l}
            }
        } else {
            foreach l [glob -nocomplain ${build.dir}/lib/libwx*.so] {
                xinstall -m 755 ${l} ${destroot}${prefix}/lib/audacity/
            }
        }
        file delete ${destroot}${prefix}/share/audacity/plug-ins/cmake_install.cmake
        file delete ${destroot}${prefix}/share/audacity/nyquist/cmake_install.cmake
        # install a wrapper script that sets LD_LIBRARY_PATH, which may be required
        # for dynamic FFmpeg loading to work.
	   file rename ${destroot}${prefix}/bin/audacity ${destroot}${prefix}/bin/audacity.bin
	   xinstall -m 755 ${filespath}/audacity.sh ${destroot}${prefix}/bin/audacity
	   reinplace "s|@PREFIX@|${prefix}|g" ${destroot}${prefix}/bin/audacity
    }
}

post-destroot {
    if {${os.platform} ne "darwin"} {
        xinstall -m 755 ${filespath}/vocalremover.ny ${destroot}${prefix}/share/audacity/plug-ins/
    } else {
        xinstall -m 755 ${filespath}/vocalremover.ny ${destroot}${aud_app_path}/Contents/plug-ins/
    }
}

if {${os.platform} eq "darwin"} {
    set seedoc "installing_and_updating_audacity_on_mac_os_x"
} else {
    set seedoc "installing_and_updating_audacity_on_linux"
}

notes "
    In case Audacity complains about finding FFmpeg or Lame, please locate these libraries\
    under ${prefix}/lib in the Libraries section of the Preferences dialog (see\
    https://manual.audacityteam.org/man/${seedoc}.html#locate).

    In case you observe colours that seem off in Audacity's user interface after updating, find\
    the text of this note in `port file ${subport}` and read the suggestions below on how you\
    might be able to fix that.
"

## about weird colours appearing in the Audacity user interface:
# Audacity uses bitmaps to define how buttons look (theming), and had a colour blend function (that
# I bring back in this port). For this, it invokes an "image-compiler" utility during the build
# process, which uses the lib-theme.so *shared* library Audacity will itself use too. The problem is
# that this step is not well shielded from lib-theme.so libraries elsewhere on the system where the
# loader can find them, in particular the one in the destination prefix. This can lead to weird
# "glitches" and the aforementioned weird colours appearing in the user interface.
# See https://forum.audacityteam.org/t/which-wxwidgets-version-and-gtk-engine-on-linux/149541/4?u=rjvb
# If this happens, do `sudo port deactivate audacity` and rebuild the port. If you still have the
# build directory you can do
# > rm -rf `port work audacity`/build/libraries/lib-theme*
# > touch `port work audacity`/build/CMakeCache.txt
# followed by your usual build command (after deactivating the port, of course)..

platform darwin {
    if {![variant_isset wxsystem]} {
        # audacity-wxWidgets was embedded into the app bundle
        notes-append ">> You can now `sudo port deactivate audacity-wxWidgets`! <<"
    }
}

# kate: backspace-indents true; indent-pasted-text true; indent-width 4; keep-extra-spaces true; remove-trailing-spaces modified; replace-tabs true; replace-tabs-save true; syntax Tcl/Tk; tab-indents true; tab-width 4;
#>> RJVB
PortGroup           locale_select 1.0
platform darwin {
    langselect_lproj_dir-append \
                    ${destroot}${aud_app_path}/Contents/Resources
}
#<< RJVB

    ######### end main port #########
} elseif {${subport} eq "audacity-wxWidgets"} {
    ######### wxWidgets #########
    github.setup        audacity wxWidgets 837669d

    name                audacity-wxWidgets
    version             3.1.3.2 ; revision 1
    distname            awxWidgets-git
    checksums           rmd160  594e539c0cb8e4ebd21303ac5c7354efcd70f54a \
                        sha256  0547bd279056ce33db8da88ecfe4d85de28513e18983fffd558ac6051feea368
#                         size    20462802

    description         C++ framework for cross-platform GUI development
    long_description    Audacity's version of wxWidgets 3.1, an open-source cross-platform C++ \
                        GUI framework for Mac OS, Unix, Linux, Windows. This port is not intended \
                        for use with 3rd party software, and only needs to be active when building \
                        Audacity WITHOUT the `wxsystem` variant set (i.e. -wxsystem). \n\
                        >> Use of this wxWidgets version is compulsory if the port:Audacity does \
                        not have a `wxsystem` variant! ! <<
    license             wxwidgets-3.1

    depends_lib-append  {*}${awx_deps}

    patch.pre_args      -Np1
    patchfiles-append   wxW/patch-relax-buildoption-check.diff \
                        wxW/patch-windowmm.diff
    # patchfiles borrowed from audacity's conan build for their wxWidgets:
    patchfiles-append   wxW/patch-configcmake.diff \
                        wxW/patch-fix-cocoa-process.diff \
                        wxW/patch-cocoa-build.diff

    if {${os.platform} eq "darwin"} {
        set wxtype      osx_cocoa
    } else {
        set wxtype      gtk2
        # we take GTk2 deps from the system, preferably
        fromPorts::depends lib-append \
                        port:gtk2
    }
    compiler.c_standard 2011
    compiler.cxx_standard 2014

    cmake.install_prefix ${wxWidgets.prefix}
    configure.args-append \
                        -DwxBUILD_TOOLKIT=${wxtype} \
                        -DwxUSE_ACCESSIBILITY=[expr {${os.platform} eq "darwin"}] \
                        -DwxUSE_MEDIACTRL=OFF \
                        -DwxUSE_LIBICONV=sys \
                        -DwxUSE_LIBJPEG=sys \
                        -DwxUSE_LIBTIFF=sys \
                        -DwxUSE_LIBPNG=sys \
                        -DwxUSE_ZLIB=sys \
                        -DwxUSE_LIBLZMA=sys \
                        -DwxUSE_OPENGL=ON \
                        -DwxUSE_LIBSDL=OFF \
                        -DwxUSE_AUI=ON \
                        -DwxUSE_DISPLAY=ON \
                        -DwxUSE_XRC=ON \
                        -DwxUSE_GRAPHICS_CONTEXT=ON \
                        -DwxBUILD_PRECOMP=[if_not_ccache] \
                        -DwxBUILD_STRIPPED_RELEASE_DEFAULT=OFF \
                        -DwxBUILD_CXX_STANDARD=14 \
                        -DCMAKE_CXX_STANDARD_REQUIRED=ON \
                        -DwxBUILD_COMPATIBILITY=3.0

    if { ${os.platform} eq "darwin"} {
        if {${os.major} == 13 } {
            configure.sdkroot ${developer_dir}/Platforms/MacOSX.platform/Developer/SDKs/MacOSX10.10.sdk
        }

        post-configure {
            if {![catch {registry_active ${subport}}]} {
                ui_warn "${subport} will not be packaged (destrooted) if it is still active by that time"
            }
        }
        pre-destroot {
            if {![catch {registry_active ${subport}}]} {
                ui_error "${subport} cannot be packaged (destrooted): it is already active."
                ui_error "Please forcibly deactivate the existing copy, e.g. by running:"
                ui_error ""
                ui_error "    sudo port -f deactivate ${subport}"
                ui_error ""
                ui_error "Then try again."
                return -code error "port is active"
            }
        }
    }

    post-destroot {
        set confscript ${wxWidgets.prefix}/lib/wx/config/${wxtype}-unicode-3.1
        ln -s ${confscript} ${destroot}${wxWidgets.prefix}/bin/wx-config
        ln -s wxrc ${destroot}${wxWidgets.prefix}/bin/wxrc-3.1
    }
}

livecheck.type      none
