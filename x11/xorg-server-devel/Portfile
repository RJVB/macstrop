# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem      1.0
PortGroup       compiler_blacklist_versions 1.0
PortGroup       github 1.0
PortGroup       code_sign 1.0
PortGroup       meson 1.0
set LTO.LTO.maybe_ForceLD yes
PortGroup       LTO 1.0
PortGroup       save_configure_cmd 1.0

if {[file exists ${filespath}/../../../_resources/port1.0/group/locale_select-1.0.tcl]} {
    PortGroup       locale_select 1.0
}

platform darwin {
    # Enable HFS compression of the workdir if the compress_workdir PortGroup is installed
    if {[file exists ${filespath}/../../../_resources/port1.0/group/compress_workdir-1.0.tcl]} {
        PortGroup   compress_workdir 1.0
    }
}

github.setup    XQuartz xorg-server bc1e869799b620888deee051e9bec602555e0ceb
# it's a mess but I counted 14 commits after the one bumping the server version to
# 21.1.8 , which is reported by the server as 1.21.1.8 (so we use that version!)
version         1.21.1.8.14
set xq_version  2.8.6-beta2
fetch.type      git
epoch           1

name            xorg-server-devel
conflicts       xorg-server xorg-server-1.18
set my_name     xorg-server
categories      x11 devel
license         X11
maintainers     {jeremyhu @jeremyhu} openmaintainer
description     The X.org / Xquartz X server.
homepage        https://www.x.org
platforms       darwin macosx
long_description The X.org X server allows you to run X11 applications on your computer. \
                This is XQuartz version ${xq_version}

worksrcdir      ${my_name}-git
distname        ${my_name}-git

use_parallel_build yes

variant xquartz description {(try to) use all as much as possible from an XQuartz install} {}

# Yes, mesa is a *BUILD* dependency
depends_build-append \
                port:pkgconfig \
                port:mesa \
                port:xorg-xorgproto \
                port:xorg-xtrans

# This xinit dependency needs to be port: not bin: because we specifically run ${prefix}/bin/startx from bundle-main.c
depends_run-append \
                port:xinit \
                port:xorg-fonts \
                port:xkeyboard-config

depends_lib-append \
                path:lib/pkgconfig/pixman-1.pc:libpixman \
                port:xorg-libxkbfile \
                port:xorg-libXfont2 \
                port:xorg-libXt \
                port:xorg-libAppleWM \
                port:xorg-libXfixes \
                port:libepoxy

if {![variant_isset xquartz]} {
    depends_run-append \
                port:quartz-wm
}

platform darwin {
    # https://trac.macports.org/ticket/36055
    # https://llvm.org/bugs/show_bug.cgi?id=30346
    # https://trac.macports.org/ticket/53910
    compiler.blacklist gcc-4.0 macports-clang-3.9 macports-clang-devel {clang >= 802 < 900}
    foreach clv {4.0 5.0 6.0} {
        if {[file exists ${prefix}/bin/clang-mp-${clv}]} {
            compiler.whitelist-prepend macports-clang-${clv}
        }
        compiler.fallback-prepend macports-clang-${clv}
    }
}

if {[string match *meson ${configure.cmd}]} {
    set use_meson yes
}

# keep the autoconf code for a bit
if {[tbool use_meson]} {
    configure.args-append \
        -Dapple-applications-dir=${applications_dir} \
        -Dbundle-id-prefix=org.macports \
        -Ddtrace=false \
        -Ddocs=false \
        -Dxephyr=false \
        -Dsha1=CommonCrypto \
        -Dxpbproxy=true \
        -Dglamor=true \
        -Dlisten_tcp=true \
        -Dlisten_unix=true \
        -Dlisten_local=true \
        -Dipv6=true \
        -Dudev=false \
        -Dsecure-rpc=false \
        -Dinput_thread=true \
        -Dxnest=true
} else {
    configure.args \
        --with-apple-applications-dir=${applications_dir} \
        --with-bundle-id-prefix=org.macports \
        --without-dtrace \
        --without-doxygen \
        --without-fop \
        --without-xmlto \
        --disable-devel-docs \
        --with-sha1=CommonCrypto \
        --disable-dri2 \
        --disable-dri3 \
        --disable-composite \
        --disable-kdrive \
        --disable-xnest \
        --disable-xephyr \
        --enable-standalone-xpbproxy \
        --enable-glamor \
        --enable-listen-tcp \
        --enable-unix-transport \
        --enable-tcp-transport \
        --enable-ipv6 \
        --enable-local-transport \
        --enable-input-thread

    variant dri2 description {enable DRI2 support (experimental)} {
        configure.args-replace \
            --disable-dri2 \
            --enable-dri2
    }

    variant composite description {build the COMPOSITE extension (experimental)} {
        configure.args-replace \
            --disable-composite \
            --enable-composite
    }

    platform darwin 8 {
        configure.args-append --disable-dependency-tracking
    }
    pre-configure {
        use_autoreconf [expr ![file exists ${worksrcpath}/config.log]]
    }
    autoreconf.args -fvi
}

variant docs description "Install extra documentation" {
    depends_build-append \
            port:fop \
            port:xmlto \
            port:xorg-sgml-doctools

    depends_skip_archcheck-append \
            xmlto

    if {[tbool use_meson]} {
        depends_build-append \
            port:libxslt
        depends_skip_archcheck-append \
            libxslt
        configure.args-replace \
            -Ddocs=false \
            -Ddocs=true
    } else {
        depends_build-append \
            port:doxygen
        depends_skip_archcheck-append \
            doxygen \
        configure.args-delete \
            --without-doxygen \
            --without-fop \
            --without-xmlto \
            --disable-devel-docs 

        configure.args-append \
            --with-doxygen \
            --with-fop \
            --with-xmlto \
            --enable-devel-docs

        configure.env-append \
            DOXYGEN="${prefix}/bin/doxygen" \
            FOP="${prefix}/bin/fop" \
            XMLTO="${prefix}/bin/xmlto"
    }
}

variant xephyr description {build the Xephyr nested server instead of XNest} {
    depends_lib-append \
        port:xorg-libxcb
    patchfiles-append \
        patch-enable-xephyr.diff
    if {[tbool use_meson]} {
        configure.args-replace \
            -Dxnest=true \
            -Dxnest=false
        configure.args-replace \
            -Dxephyr=false \
            -Dxephyr=true
    } else {
        configure.args-replace \
            --disable-kdrive \
            --enable-kdrive=yes
        configure.args-replace \
            --disable-xephyr \
            --enable-xephyr=yes
    }
}

# GL/internal/dri_interface.h is missing in prefix (provided by libdrm for the xorg DDX... not helpful for us)
# configure.cppflags-append -I/usr/include -I${filespath}/dri -F${configure.sdkroot}/System/Library/Frameworks/ApplicationServices.framework/Frameworks

# Debugging ...
#configure.cppflags-append  -g -fsanitize=address -fno-omit-frame-pointer
#configure.cflags-append    -g -fsanitize=address -fno-omit-frame-pointer
#configure.cppflags-replace -Os -O1
#configure.cflags-replace   -Os -O1

configure.env-append \
        RAWCPP=${configure.cpp}

patchfiles-append patch-fix-xpbproxy.diff

# RJVB: enable some features that are experimental on Mac: DRI(2) and the DPMS and Composite extensions
patchfiles-append patch-enable-exp-features.diff \
                  patch-undef-bool.diff

# RJVB
patchfiles-append   patch-aquamenubar-debug.diff \
                    patch-log-screendimensions.diff
patchfiles-append   patch-bundleprefix-fallback.diff

patchfiles-append   patch-no-qos-pre-1010.diff
if {${os.major} < 14} {
    patchfiles-append \
                    patch-objc-pre-1010.diff
}

patch.pre_args -Np1

depends_build-append \
	port:xorg-util-macros \
    port:xorg-font-util

post-destroot {
    ln -s Xquartz ${destroot}${prefix}/bin/X
    if {[variant_isset xquartz] && [file exists /opt/X11/bin/mkfontdir]} {
        ln -s /opt/X11/bin/mkfontdir ${destroot}${prefix}/bin
        ln -s /opt/X11/bin/mkfontscale ${destroot}${prefix}/bin
    }
}

platform macosx {
    if { ${os.major} < 10 } {
        # Xplugin.h is missing on Tiger and incorrect on Leopard
        configure.cppflags-append -I${filespath}/include
    }
    if {[variant_exists langselect]} {
        langselect_lproj_dir-append \
            ${destroot}${applications_dir}/X11.app/Contents/Resources
    }

    if { ${os.major} < 10 } {
        # Xplugin.h is missing on Tiger and incorrect on Leopard
        configure.cppflags-append -I${filespath}/include
    }

    proc sign {{ddir ""}} {
        global applications_dir prefix
        ui_info "--->  signing ${ddir}${applications_dir}/X11.app"
        codesign ${ddir}${applications_dir}/X11.app -
        if {[variant_isset xephyr]} {
            ui_info "--->  signing ${ddir}${prefix}/bin/Xephyr"
            codesign ${ddir}${prefix}/bin/Xephyr -
        }
    }
    ## sign in post-destroot and post-activate, so the signature files are recorded as ours
    post-destroot {
        sign ${destroot}
    }
    post-activate {
        sign
    }
}

variant xcsecurity description "Enable Security Extensions" {
    if {[tbool use_meson]} {
        configure.args-append \
            -Dxcsecurity=true
    } else {
        configure.args-append \
            --enable-xcsecurity
    }
}


if {[tbool use_meson]} {
    build.pre_args-prepend -k 0
} else {
    build.post_args-append -wk
    build.args V=1
    configure.save_configure_cmd "install log"
}

livecheck.type none
