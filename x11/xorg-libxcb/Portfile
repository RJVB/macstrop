# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem      1.0
PortGroup       active_variants 1.1
PortGroup       LTO 1.0
PortGroup       save_configure_cmd 1.0

name            xorg-libxcb
version         1.17.0

checksums       rmd160  a5e36fb6d60154354c60c6fe94082f4c2d23e5fd \
                sha256  599ebf9996710fea71622e6e184f3a8ad5b43d0e5fa8c4e407123c88a59a6d55
#                size    455208

categories      x11 devel
license         X11
maintainers     {jeremyhu @jeremyhu} openmaintainer
description     X.org libxcb
homepage        http://xcb.freedesktop.org/
platforms       darwin macosx
long_description The X C Binding (XCB) library
master_sites     xorg:individual/lib

distname        libxcb-${version}

use_xz          yes
use_parallel_build yes

depends_build   port:pkgconfig

platform darwin {
    depends_lib port:xorg-libXau \
                port:xorg-libXdmcp \
                port:xorg-libpthread-stubs
}
depends_lib-append \
                port:xorg-xcb-proto

patch.pre_args  -Np1
# prevent a sporadic hang situation when querying the mouse position too frequently on a remote display
patchfiles-append \
                patch-xcbwaitreply-hang.diff

configure.save_configure_cmd "install log"
configure.args  --disable-devel-docs \
                --disable-silent-rules \
                --enable-ge --enable-xevie --enable-xprint

variant docs description "Install extra documentation" {
    depends_build-append \
        path:bin/doxygen:doxygen \
        path:bin/dot:graphviz

    depends_skip_archcheck-append \
        doxygen

    configure.args-delete \
        --disable-devel-docs

    configure.args-append \
        --enable-devel-docs

    configure.env-append \
        DOXYGEN=${prefix}/bin/doxygen
}

# No need to use require_active_variants here. Technically, `xorg-libxcb +python37` builds fine
# with `xorg-xcb-proto +python36` as the xcbgen module in xorg-xcb-proto is not located using
# usual Python import mechanism. Instead, the path for xcbgen is recorded in
# `$prefix/lib/pkgconfig/xcb-proto.pc` as the pythondir variable when building xorg-xcb-proto
# and the build system of libxcb loads xcbgen using that pkg-config variable [1].
#
# [1] https://gitlab.freedesktop.org/xorg/lib/libxcb/blob/1.13.1/configure.ac#L85

set pythonversions {3.5 3.6 3.7 3.8 3.9 3.10 3.11 3.12}
foreach pdv ${pythonversions} {
    set pv [join [lrange [split ${pdv} .] 0 1] ""]
    set conflist ""
    foreach v ${pythonversions} {
        if {${v} ne ${pdv}} {
            set vv [join [lrange [split ${v} .] 0 1] ""]
            set conflist "${conflist} python${vv}"
        }
    }
    variant python${pv} conflicts {*}${conflist} description "Use Python ${pdv}" {}
    if {[variant_isset python${pv}]} {
        set pyvar_set 1
    }
}

if {![tbool pyvar_set]} {
    default_variants +python37
}

# now check which variant is actually set and take the appropriate action
foreach pdv ${pythonversions} {
    set pv [join [lrange [split ${pdv} .] 0 1] ""]
    if {[variant_isset python${pv}]} {
        set python_branch   ${pv}
        set python_version  ${pdv}
    }
}
if {[info exists python_version]} {
    depends_build-append    port:python${python_branch}
    depends_skip_archcheck-append \
                            port:python${python_branch}
    license_noconflict      python${python_branch}
    configure.python        ${prefix}/bin/python${python_version}

    require_active_variants xorg-xcb-proto python${python_branch}

    post-destroot {
        if {![file exists ${destroot}${prefix}/lib/python${python_version}] \
                && [file exists ${destroot}/usr/lib/python${python_version}]} {
            ui_msg "Moving Python modules into ${prefix}"
            file rename ${destroot}/usr/lib/python${python_version} \
                ${destroot}${prefix}/lib/python${python_version}
        }
        if {![file exists ${destroot}${prefix}/lib/python${python_version}/site-packages] \
                && [file exists ${destroot}${prefix}/lib/python${python_version}/dist-packages]} {
            ui_msg "Renaming dist-packages to site-packages"
            file rename ${destroot}${prefix}/lib/python${python_version}/dist-packages \
                ${destroot}${prefix}/lib/python${python_version}/site-packages
        }
    }
} else {
    ui_error "${subport} needs a Python variant to be set"
    return -code error "Missing variant"
}


livecheck.type  regex
livecheck.url   https://xorg.freedesktop.org/archive/individual/lib/?C=M&O=D
livecheck.regex libxcb-(\\d+(?:\\.\\d+)*)
