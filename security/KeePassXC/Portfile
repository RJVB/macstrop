# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem              1.0
platform darwin {
    PortGroup           legacysupport 1.1
    # Enable use of 'macports-libcxx' for macOS 10.11 and earlier, as port uses
    # libcxx features normally only available on 10.12 and later.
    # See: https://trac.macports.org/ticket/64055
    legacysupport.newest_darwin_requires_legacy 15
    legacysupport.use_mp_libcxx yes
}
PortGroup               qt5 1.0
PortGroup               github 1.0
PortGroup               cmake 1.1
PortGroup               compiler_blacklist_versions 1.0
# PortGroup               gpg_verify 1.0
if {![info exists langselect_qm_dir]} {
    # portindex may not have included the locale_select PG (through the Qt5 PG)!
    PortGroup           locale_select 1.0
}
if {![variant_exists LTO]} {
    PortGroup           LTO 1.0
}

name                    KeePassXC
subport                 KeePassXC-devel  {}
categories              security aqua
maintainers             nomaintainer
description             KeePassXC is a cross-platform community-driven port \
                        of the Windows application \"Keepass Password Safe\".
long_description        KeePassXC is a community fork of KeePassX with the \
                        goal to extend and improve it with new features and \
                        bugfixes to provide a feature-rich, fully \
                        cross-platform and modern open-source password manager.
platforms               darwin
license                 GPL-2+
license_noconflict      openssl openssl10 openssl11 openssl3

homepage                https://keepassxc.org

if {${subport} eq ${name}} {
    # stable
    github.setup        keepassxreboot keepassxc 2.7.6
    github.tarball_from releases
    distname            keepassxc-${version}-src
    use_xz              yes
    distfiles-append    ${distname}${extract.suffix}.sig
    worksrcdir          keepassxc-${version}

    conflicts           KeePassXC-devel

    set PPREFIX         ""

    # See keepassxc-${version}-src.tar.xz.DIGEST on upstream GitHub releases page for SHA256 sums
    checksums           ${distname}${extract.suffix} \
                        rmd160  a939976cff7273f9243f9c3aae39269e11047e6d \
                        sha256  a58074509fa8e90f152c6247f73e75e126303081f55eedb4ea0cbb6fa980d670 \
                        ${distname}${extract.suffix}.sig \
                        rmd160  693cbc4a5648735cd4a53afdb9617bca9702bf45 \
                        sha256  af439d1954101c1b140f759cf0f42efdfe19d066f90412e325b806f8d3774eb4 \

    patchfiles-append   patch-no-deployqt.diff
} else {
    conflicts-append    ${name}
    fetch.type          git
    # check out the "develop" branch:
    github.setup        keepassxreboot keepassxc 9e119230d4604c177766e93304578d954e3e8c1f
    # after `git tag w2.7.5 -m "just after release 2.7.5 tagging" 263ce20473419792f6740afe03ff4f3fdf88b5c7`
    # after `git tag w2.7.6 -m "release 2.7.6 backport" 1919c23c097fd5f3e64ae6488aaaabd502db6afb`
    version             2.7.6.28 ; revision 1
    distname            ${github.project}-git
    worksrcdir          ${github.project}-git
    variant standalone description {Build as a standalone, relocatable app bundle} {}
    if {![variant_isset standalone]} {
        patchfiles-append \
                        patch-no-deployqt.diff
    }

    set PPREFIX         "devel/"

    pre-configure {
        system -W ${worksrcpath} "git describe > .version"
    }
    pre-build {
        # refresh .version just in case cmake decides it wants to re-run
        system -W ${worksrcpath} "git describe > .version"
    }

    post-destroot {
        file delete -force ${worksrcpath}/.version
    }
}

qt5.min_version         5.5.0
qt5.depends_component   qtsvg
qt5.depends_build_component \
                        qttools

depends_lib-append      port:argon2 \
                        port:botan \
                        port:libgcrypt \
                        port:libsodium \
                        port:minizip \
                        port:readline \
                        port:qrencode \
                        port:zlib
platform linux {
    depends_lib-append  port:keyutils
}

patchfiles-append       patch-no-findpackage-path.diff \
                        ${PPREFIX}add_support_for_old_macos.diff \
                        add_support_for_old_macos-2.diff \
                        patch-darwin-allow-dbus+secretservice.diff \
                        patch-no-double-AppKitImpl.diff \
                        patch-darwin-allow-x11mode.diff \
                        patch-darwin-allow-starting-in-debugger.diff

# KeePassXC uses -fstack-protector-strong on Clang [1]. That flag is not
# available until clang 602 [2] or upstream clang 3.5 [3]
# [1] https://github.com/keepassxreboot/keepassxc/blob/develop/CMakeLists.txt
# [2] https://opensource.apple.com/source/clang/clang-600.0.57/src/tools/clang/lib/CodeGen/CodeGenModule.cpp.auto.html
#     https://opensource.apple.com/source/clang/clang-602.0.53/src/tools/clang/lib/CodeGen/CodeGenModule.cpp.auto.html
# [3] https://github.com/llvm-project/clang/blob/release_34/lib/CodeGen/CodeGenModule.cpp
#     https://github.com/llvm-project/clang/blob/release_35/lib/CodeGen/CodeGenModule.cpp
compiler.blacklist-append {clang < 602}

compiler.cxx_standard   2017

if {${os.platform} eq "darwin"} {
    qt5.depends_component \
                        qtmacextras
    # we need a clang version that supports -fstack-protector-strong or -fstack-protector
    compiler.blacklist {clang < 602} {macports-clang-3.*} macports-clang-4.0
    foreach clv {5.0 6.0 7.0 8.0} {
        # clang 5 fails to build KPXC with port:libcxx v13, clang 9 succeeds...
        compiler.blacklist-append macports-clang-${clv}
    }
    foreach clv {9.0 10 11 12 13 14 15 16} {
        if {[file exists ${prefix}/bin/clang-mp-${clv}]} {
            compiler.whitelist-prepend macports-clang-${clv}
        }
        compiler.fallback-prepend macports-clang-${clv}
    }
} else {
    qt5.depends_component \
                        qtx11extras
}

cmake.save_configure_cmd "install log"

configure.args-append \
    -DCMAKE_INSTALL_MANDIR=${prefix}/share/man \
    -DCMAKE_PREFIX_PATH=${qt_cmake_module_dir} \
    -DKEEPASSXC_BUILD_TYPE=Release \
    -DWITH_XC_BROWSER=ON \
    -DWITH_XC_KEESHARE=ON \
    -DWITH_XC_NETWORKING=ON \
    -DWITH_XC_SSHAGENT=ON \
    -DWITH_XC_UPDATECHECK=OFF \
    -DWITH_XC_DOCS=OFF


# QTest::addRow was introduced in Qt 5.9
# Don't build tests in that case
if {[vercmp ${qt5.version} 5.9] < 0} {
    configure.args-append -DWITH_TESTS=OFF
}

configure.args-append   -DWITH_XC_FDOSECRETS=ON \
                        -DWITH_STANDALONE_APP_BUNDLE=OFF \
                        -DCMAKE_DISABLE_FIND_PACKAGE_OpenMP=On
# new option, depends on Qt >= 5.12 but is ignored for earlier Qt versions
configure.args-append   -DWITH_XC_BROWSER_PASSKEYS=ON

if {${os.platform} eq "darwin"} {
    if {[variant_exists standalone] && [variant_isset standalone]} {
        cmake.install_prefix ${applications_dir}
        configure.pre_args-append \
                        -DCMAKE_INSTALL_PREFIX=${applications_dir}
        configure.args-replace \
                        -DWITH_STANDALONE_APP_BUNDLE=OFF \
                        -DWITH_STANDALONE_APP_BUNDLE=ON
    }
    # translation files are pruned before the appbundle is moved into place, so
    # register the initial location of the bundle.
    langselect_qm_dir   ${destroot}${prefix}/KeePassXC.app/Contents/Resources/translations
} else {
    langselect_qm_dir   ${destroot}${prefix}/share/keepassxc/translations
}
langselect_qm_basename  keepassxc_

post-destroot {
    set kp_doc_path ${destroot}${prefix}/share/doc/${name}

    xinstall -d ${kp_doc_path}
    xinstall -W ${worksrcpath} COPYING LICENSE.BSD LICENSE.CC0 \
             LICENSE.GPL-2 LICENSE.GPL-3 LICENSE.LGPL-2.1 LICENSE.LGPL-3 \
             LICENSE.MIT LICENSE.NOKIA-LGPL-EXCEPTION LICENSE.OFL \
             ${kp_doc_path}

    platform darwin {
        if {![variant_isset standalone] && ${os.platform} eq "darwin"} {
            file rename ${destroot}${prefix}/KeePassXC.app ${destroot}${applications_dir}/
            xinstall -m 755 -d ${destroot}${prefix}/share/keepassxc
            ln -s ${applications_dir}/KeePassXC.app/Contents/Resources/translations ${destroot}${prefix}/share/keepassxc/
        }
        set kp_app_path ${applications_dir}/KeePassXC.app/Contents/MacOS
        set kp_bin_path ${destroot}${prefix}/bin
        ln -s ${kp_app_path}/keepassxc-cli \
            ${kp_bin_path}/keepassxc-cli
        ln -s ${kp_app_path}/keepassxc-proxy \
            ${kp_bin_path}/keepassxc-proxy
        if {[info procs qt5.add_app_wrapper] ne ""} {
            qt5.add_app_wrapper keepassxc KeePassXC KeePassXC ${applications_dir}
        }
    }
}

variant yubikey description {Enable YubiKey challenge-response support} {
    configure.args-append \
                        -DWITH_XC_YUBIKEY=ON
    depends_lib-append port:ykpers
}

test.run        yes
test.target     test ARGS="-V"
test.env        TMPDIR=/tmp

# Ignore betas, RCs, etc. - See: https://trac.macports.org/ticket/64956
github.livecheck.regex  {([0-9.]+)}
