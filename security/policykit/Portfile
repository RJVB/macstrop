# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:filetype=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           gitlab 1.0
PortGroup           muniversal 1.0
platform darwin {
    PortGroup       legacysupport 1.0
}
PortGroup           meson 1.0
PortGroup           LTO 1.0
PortGroup           locale_select 1.0

gitlab.instance     https://gitlab.freedesktop.org
gitlab.setup        polkit polkit 123
name                policykit
version             0.123
description         PolicyKit is an application-level toolkit for defining and \
                    handling the policy that allows unprivileged processes to \
                    speak to privileged processes
long_description    ${description}
maintainers         {devans @dbevans} openmaintainer
categories          security
license             LGPL-2+

checksums           rmd160  011d5e21ef16499d9fb43c74305ff645290312c5 \
                    sha256  72d9119b0aa35da871fd0660601d812c7a3d6af7e4e53e237840b71bb43d0c63
#                     size    707480

depends_build-append \
                    path:lib/pkgconfig/gobject-introspection-1.0.pc:gobject-introspection \
                    port:docbook-xml-4.1.2 \
                    port:docbook-xml-4.5 \
                    port:docbook-xsl-nons \
                    port:gettext \
                    port:libxslt \
                    port:pkgconfig
platform linux {
    depends_build-append \
                    port:gettext-dev \
                    port:python39 \
                    port:py39-dbusmock
}

depends_lib         path:lib/pkgconfig/glib-2.0.pc:glib2 \
                    port:dbus \
                    port:duktape

post-extract {
    reinplace "s|python3|python3.7|g" ${worksrcpath}/meson_post_install.py
    reinplace "s|/usr/bin/python3|${prefix}/bin/python3.7|g" \
                    ${worksrcpath}/test/polkitbackend/polkitbackendjsauthoritytest-wrapper.py
}

pre-patch {
    if {${os.platform} eq "darwin" && ${os.major} < 10} {
        reinplace "s|security/pam|pam/pam|" \
            ${worksrcpath}/meson.build \
            ${worksrcpath}/src/polkitagent/polkitagenthelper-pam.c \
            ${worksrcpath}/src/programs/pkexec.c
    }
}

if {${os.platform} eq "darwin"} {
    variant autoconf_libversions description {set the same library versions as the autoconf build system \
        did in older versions of this port.\
        Select this variant if you don't want to have to rebuild all variants.} {}

    patchfiles      patch-src__polkitbackend__meson.build-define-darwin-c-source.diff
    # thanks to https://stackoverflow.com/a/71807139/1460868 :
    patchfiles-append \
                    patch-mach-process-starttime.diff
    patchfiles-append \
                    patch-fix-noconnect-crash.diff

    if {[variant_isset autoconf_libversions]} {
        depends_build-append \
                    port:cctools
        patchfiles-append \
                    patch-autoconf-libversions.diff
        post-destroot {
            # convert the libraries to how the autoconf buildsystem made them:
            file rename ${destroot}${prefix}/lib/libpolkit-gobject-1.1.dylib \
                ${destroot}${prefix}/lib/libpolkit-gobject-1.0.dylib
            file rename ${destroot}${prefix}/lib/libpolkit-agent-1.1.dylib \
                ${destroot}${prefix}/lib/libpolkit-agent-1.0.dylib
            ln -s -f libpolkit-gobject-1.0.dylib ${destroot}${prefix}/lib/libpolkit-gobject-1.dylib
            ln -s -f libpolkit-agent-1.0.dylib ${destroot}${prefix}/lib/libpolkit-agent-1.dylib
            # also make the library available the way the typelibs would probably expect it:
            ln -s -f libpolkit-gobject-1.0.dylib ${destroot}${prefix}/lib/libpolkit-gobject-1.1.dylib
            ln -s -f libpolkit-agent-1.0.dylib ${destroot}${prefix}/lib/libpolkit-agent-1.1.dylib
            system "install_name_tool -id ${prefix}/lib/libpolkit-gobject-1.0.dylib \
                ${destroot}${prefix}/lib/libpolkit-gobject-1.0.dylib"
            system "install_name_tool -id ${prefix}/lib/libpolkit-agent-1.0.dylib \
                ${destroot}${prefix}/lib/libpolkit-agent-1.0.dylib"
            system "install_name_tool -change ${prefix}/lib/libpolkit-gobject-1.1.dylib \
                ${prefix}/lib/libpolkit-gobject-1.0.dylib \
                ${destroot}${prefix}/lib/libpolkit-agent-1.0.dylib"
            # convert the executables too
            foreach b {bin/pkcheck bin/pkaction bin/pkexec bin/pkttyagent lib/polkit-1/polkit-agent-helper-1 lib/polkit-1/polkitd} {
                system "install_name_tool -change ${prefix}/lib/libpolkit-gobject-1.1.dylib \
                    ${prefix}/lib/libpolkit-gobject-1.0.dylib \
                    ${destroot}${prefix}/${b}"
                system "install_name_tool -change ${prefix}/lib/libpolkit-agent-1.1.dylib \
                    ${prefix}/lib/libpolkit-agent-1.0.dylib \
                    ${destroot}${prefix}/${b}"
            }
            if {[geteuid] == 0} {
                # ensure the ownership and (setuid!) permissions on these:
                file attributes ${destroot}${prefix}/bin/pkexec -owner root
                file attributes ${destroot}${prefix}/lib/polkit-1/polkit-agent-helper-1 -owner root
                file attributes ${destroot}${prefix}/bin/pkexec -permissions 04755
                file attributes ${destroot}${prefix}/lib/polkit-1/polkit-agent-helper-1 -permissions 04755
            }
        }
    }

    configure.args-append \
                    -Dpolkitd_user=polkitd \
                    -Dauthfw=pam \
                    -Dpam_module_dir=/usr/lib/pam \
                    -Dpam_prefix=/etc/pam.d \
                    -Dman=true \
                    -Dgtk_doc=false \
                    -Djs_engine=duktape \
                    -Dintrospection=true

    merger_arch_compiler yes

    # gobject-introspection uses g-ir-scanner, which uses $CC and $CFLAGS from env
    # setting these is the only reliable way to make sure we get a universal version
    # of this part of the build.
    if {${universal_possible} && [variant_isset universal]} {
        foreach arch ${configure.universal_archs} {
            lappend merger_build_env(${arch})  "CC=${configure.cc}"
            lappend merger_build_env(${arch})  "CFLAGS=${configure.cflags} -arch ${arch}"
        }
    }

    destroot.violate_mtree yes
} else {
    # align with current Ubuntu 14.04 norms:
    configure.args-append \
                    -Dpolkitd_user=root \
                    -Dauthfw=pam \
                    -Dman=true \
                    -Dgtk_doc=false \
                    -Djs_engine=duktape \
                    -Dintrospection=true \
                    -Dtests=true
    test.run        yes
}
configure.args-append \
                    -Dexamples=false

meson.save_configure_cmd "install log"

destroot.keepdirs   ${destroot}${prefix}/etc/polkit-1/localauthority \
                    ${destroot}${prefix}/var/lib/polkit-1

variant docs description {Build API docs using gtk-doc} {
        configure.args-replace \
            -Dgtk_doc=false \
            -Dgtk_doc=true
        depends_build-append  port:gtk-doc
}

variant examples description {Build examples} {
    configure.args-replace \
            -Dexamples=false \
            -Dexamples=true
}

if {${os.platform} eq "darwin"} {
    add_users       polkitd group=polkitd realname=Policy\ Kit
} else {
    post-destroot {
        # /opt/local/lib/polkit-1/polkit-agent-helper-1
        # find the host's authentication agent, which will probably work (much better)
        foreach alt {/usr/lib/policykit-1/polkit-agent-helper-1} {
            if {[file exists ${alt}]} {
                set alt_agent ${alt}
            }
        }
        if {[info exists alt_agent]} {
            file rename ${destroot}${prefix}/lib/polkit-1/polkit-agent-helper-1 ${destroot}${prefix}/lib/polkit-1/polkit-agent-helper-1-mp
#             ln -s ${alt_agent} ${destroot}${prefix}/lib/polkit-1/polkit-agent-helper-1
            xinstall -m 755 ${filespath}/alt_agent.sh ${destroot}${prefix}/lib/polkit-1/polkit-agent-helper-1
            reinplace "s|@ALT_AGENT@|${alt_agent}|g" ${destroot}${prefix}/lib/polkit-1/polkit-agent-helper-1
            # copy the permissions:
            file attributes ${destroot}${prefix}/lib/polkit-1/polkit-agent-helper-1 -permissions \
                [file attributes ${alt_agent} -permissions]
        }
        if {[file exists ${destroot}/usr/lib/girepository-1.0]} {
            file rename ${destroot}/usr/lib/girepository-1.0 ${destroot}${prefix}/lib/girepository-1.0
        }
        if {[file exists ${destroot}/usr/share/gir-1.0]} {
            file rename ${destroot}/usr/share/gir-1.0 ${destroot}${prefix}/share/gir-1.0
        }
    }
}

livecheck.type      regex
livecheck.url       https://gitlab.freedesktop.org/polkit/polkit/-/tags
livecheck.regex     "polkit-(\\d+(?:\\.\\d+)*)"
