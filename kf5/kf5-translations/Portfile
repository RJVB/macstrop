# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id$
# Copyright (c) 2015, 2016 R.J.V. Bertin

PortSystem          1.0

PortGroup           kf5 1.1

name                kf5-translations
# this port isn't likely to evolve as fast as the actual software, so we hardcode the version
version             16.12.3
homepage            http://download.kde.org/Attic/applications/${version}/src/kde-l10n/
master_sites        http://download.kde.org/Attic/applications/${version}/src/kde-l10n/
maintainers         gmail.com:rjvbertin mk openmaintainer

installs_libs       no
supported_archs     noarch

description         Metaport providing KF5 translations
long_description    Metaport providing translations for KF5 applications that do not include their own translation files.

set languages { de en_GB fr nl }
foreach lang ${languages} {
    lappend mp.names "kf5-translations-${lang}"
}

set rmd160s     {
                    {b1e80bd20e41a38366c7d40d1081fd3b2580e7b6 de}
                    {110fd96c2c478d02bf898a6d733d0e3c1a2546f4 en_GB}
                    {224e7324279a32660335a8c29ffc272db71aa2a0 fr}
                    {edffb8aaefd6b5f79b2e89d106bb4b7f4923e3c7 nl}
                }
set sha256s     {
                    {660a98fe83ad38299a401fe588079fe050b29d54a2d30e66e36b67836c70a67a de}
                    {36246656f1f39b8815a27b3a0a6368498ff0bff89cf717f6138480a9ca748679 en_GB}
                    {9571a472e3e0dc9742f3ec224f02a54a0051ab7d95999ebb3da9844b6322a863 fr}
                    {f1ef286e2b270dd0d2c9140c366b1813d1a3ff810b3bcd185d820470b0bcb42b nl}
                }

set kf5.allow_apidocs_generation no

if {${subport} eq ${name}} {
    foreach mp.name ${mp.names} {
        depends_build-append    port:${mp.name}
    }
    # use just any of the language tarballs as ours:
    distname        kde-l10n-en_GB-${version}
    fetch {}
    checksum {}
    extract {}
    configure {}
    build {}
    destroot {
        xinstall -d -m 755 ${destroot}${prefix}/share/doc/kf5-installed-metaports
        system "touch ${destroot}${prefix}/share/doc/kf5-installed-metaports/${name}"
    }
}

depends_build-append \
                    ${kf5::pythondep} \
                    port:gettext
kf5.depends_frameworks \
                    ki18n

foreach mp.name ${mp.names} {

    subport ${mp.name} {
        set idx [lsearch ${mp.names} ${mp.name}]
        set lang [lindex ${languages} $idx]
        if {${lang} eq "ca_valencia"} {
            set mp.name "kf5-translations-ca@valencia"
        }
        description         The \'${lang}\' translation files for KF5 applications
        long_description    ${description}
        kf5.require_kf5compat -port kde-l10n-${lang}
        distname            kde-l10n-${lang}-${version}
        worksrcdir          kde-l10n-${lang}-${version}
        checksums           rmd160  [lindex [lindex ${rmd160s} $idx] 0] \
                            sha256  [lindex [lindex ${sha256s} $idx] 0]
        # make sure the langselect variant is unset (if it exists)
        if {[variant_exists langselect]} {
            global variations
            set variations(langselect) -
        }
        if {![variant_isset docs]} {
            # the build system is still going to require KDoctools even if we don't build the documentation
            kf5.depends_frameworks \
                    kdoctools
            post-patch {
                reinplace -W ${worksrcpath} "s|add_subdirectory(docs)|# no documentation|g" 5/${lang}/CMakeLists.txt
            }
        }
        platform darwin {
            post-patch {
                reinplace -W ${worksrcpath} "s|add_subdirectory(data)|# no data files|g" 5/${lang}/CMakeLists.txt
                catch {system -W ${worksrcpath}/5/${lang}/messages "patch -Np0 -i ${filespath}/patch-only-applications.diff"} result context
            }
        }
        if {${lang} eq "fr"} {
            post-patch {
                reinplace "s|add_subdirectory(scripts)|# add_subdirectory(scripts)|g" ${worksrcpath}/5/fr/CMakeLists.txt
            }
        }
        configure.args-append \
                    -DPYTHON_EXECUTABLE=${prefix}/bin/python${kf5::pyversion}
        post-destroot {
            # prune files that (may) conflict with newer versions of the corresponding ports. The list is
            # bound to grow...
            foreach f {kdenlive kcmkwallet kwalletmanager dolphin baloo_naturalqueryparser ark \
                    kio5_activities kfileaudiopreview5 komparediff2 kompare kdebugsettings \
                    kasten oktetacore oktetagui oktetakasten okteta oktetapart gwenview okular \
                    org.kde.active.documentviewer tabswitcherplugin plasma_wallpaper_org.kde.plasma.worldmap \
                    plasma_runner_marble plasma_applet_org.kde.plasma.worldclock plasma_applet_org.kde.plasma.katesessions \
                    kate minuet marble marble_qt webengineviewer templateparser mimetreeparser messageviewer \
                    messagelist messagecore messagecomposer kmime5 kwrite kterustcompletion lokalize \
                    kruler kcontacts5 kcalc dragonplayer dragon kcmkamera kamera \
                    webenginepart webarchiver validatorsplugin uachangerplugin searchbarplugin \
                    rellinks minitoolsplugin mf_konqplugin konq kshellcmdplugin konqueror \
                    khtmltts khtmlsettingsplugin kfmclient kfind keditbookmarks kdialog kcmperformance \
                    kcmkonqhtml kcmkonq kcmbookmarks imgalleryplugin fsview domtreeviewer dirfilterplugin \
                    babelfish autorefresh akregator_konqplugin adblock kbookmarkmerger \
                    fileviewsvnplugin fileviewhgplugin fileviewgitplugin fileviewbazaarplugin} {
                file delete -force ${destroot}${prefix}/share/doc/HTML/${lang}/${f}
                file delete -force ${destroot}${prefix}/share/doc/HTML/${lang}/kcontrol/${f}
                file delete -force ${destroot}${prefix}/share/locale/${lang}/LC_MESSAGES/${f}.mo
                file delete -force ${destroot}${prefix}/share/locale/${lang}/LC_MESSAGES/${f}.qm
                file delete -force ${destroot}${prefix}/share/locale/${lang}/LC_MESSAGES/lib${f}.mo
                foreach m [glob -nocomplain ${destroot}${prefix}/share/man/${lang}/man*/${f}*] {
                    file delete ${m}
                }
            }
            foreach f [glob -nocomplain ${destroot}${prefix}/share/locale/${lang}/LC_MESSAGES/*okular*.mo] {
                file delete ${f}
            }
            foreach f [glob -nocomplain ${destroot}${prefix}/share/locale/${lang}/LC_MESSAGES/kate*.mo] {
                file delete ${f}
            }
        }
    }
}

extract.post_args-append \
                    ${worksrcdir}/CMakeLists.txt ${worksrcdir}/5
post-extract {
    if {[file exists ${worksrcpath}/CMakeLists.txt]} {
        reinplace "s|add_subdirectory(4)|# skipping KDE4 translations|g" ${worksrcpath}/CMakeLists.txt
    }
}

