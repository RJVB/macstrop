# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# Copyright (c) 2015-20 R.J.V. Bertin

PortSystem          1.0

# store the cpp flags as the pre-configure phase will move them into
# configure.cflags and configure.cxxflags, and we want them in our post-destroot
# We register this pre-configure block before including any portgroups so we're
# to be called first.
pre-configure {
    if {${configure.cppflags} ne ""} {
        ui_debug "Saving CPPFLAGS=${configure.cppflags}"
        set fp [open "${workpath}/.cppflags" "w"]
        set cppflags [join ${configure.cppflags}]
        puts ${fp} "export CFLAGS=\"\$\{CFLAGS\} ${cppflags}\""
        puts ${fp} "export CXXFLAGS=\"\$\{CXXFLAGS\} ${cppflags}\"\n"
        close ${fp}
    } else {
        file delete -force ${workpath}/.cppflags
    }
}

set kf5.project     kde-cli-tools
set kf5.virtualPath plasma
set kf5.no_configure_log yes
PortGroup           kf5 1.1
set kf5.latest_plasma 5.13.5 ; revision 1 ;
kf5.use_latest      plasma
name                kf5-cli-tools
# save configure.args
set configure.args.orig ${configure.args}

maintainers         gmail.com:rjvbertin openmaintainer

installs_libs       no

description         KF5 CLI utilities
long_description    KF5 command line interface utilities.
# 5.13.5
checksums           rmd160  1af06cee50d297b22521ecf1e0b75c04d6e985e6 \
                    sha256  170aee9169a7e5e8b46f950f4732a8ad095292a1bc4ddbab8d8d920b04f92a5c
#                     size    585436

patchfiles-append   patch-preserve-window-icon.diff

kf5.depends_qt5_components \
                    qtsvg
kf5.depends_frameworks \
                    kcmutils kconfig kdoctools ki18n \
                    kiconthemes kinit kio kservice kwindowsystem
# these are optional:
kf5.depends_frameworks \
                    kdesu

# v5.13.5 has a hard dependency on libKWorkspace from plasma-workspace 5.13.x which doesn't appear to be required
# turn it into weak dependency, but add a dependency already on the port that will provide it.
patchfiles-append   patch-kworkspace-optional.diff
depends_lib-append  port:kf5-plasma-runtime

# Qt documentation for this package makes no sense
kf5.allow_apidocs_generation no

ifplatform darwin {
    patchfiles-append \
                    patch-no-x11.diff \
                    patch-nongui-execs.diff
    kf5.kde4compat -port kde4-runtime {
        post-destroot {
            file delete -force ${destroot}${prefix}/share/doc/HTML
            file delete -force ${destroot}${prefix}/share/locale
            file delete -force ${destroot}${prefix}/share/man
        }
    }
    post-destroot {
        kf5.add_app_wrapper kcmshell5
    }
} else {
    # this is only to get qt_libs_dir into the rpath in the cmake-kf5-mp wrapper
    cmake.install_rpath-prepend \
                    ${qt_libs_dir}
}

post-destroot {

    set cmakewrappername "cmake-kf5-mp"
    set cmakewrapperfile "${destroot}${prefix}/bin/${cmakewrappername}"
    set fp [open ${cmakewrapperfile} "w"]
    puts ${fp} "#!/bin/sh\n"
    puts ${fp} "# This script wraps cmake with the arguments for building KF5 software in MacPorts style"
    puts ${fp} "# Generated automatically by port:${subport} v${version}"
    puts ${fp} ""
    puts ${fp} "if \[ \"\$\{PREFIX\}\" = \"\" \] \;then"
    puts ${fp} "    PREFIX=@MPPREFIX@"
    puts ${fp} "fi"
    puts ${fp} ""
    puts ${fp} "if \[ \"\$\{CC\}\" = \"\" \] \;then"
    puts ${fp} "    export CC=${configure.cc}"
    puts ${fp} "fi"
    puts ${fp} "if \[ \"\$\{CXX\}\" = \"\" \] \;then"
    puts ${fp} "    export CXX=${configure.cxx}"
    puts ${fp} "fi\n"
    if {[file exists ${workpath}/.cppflags]} {
        close ${fp}
        system "cat ${workpath}/.cppflags >> ${cmakewrapperfile}"
        set fp [open ${cmakewrapperfile} "a"]
    }
    puts ${fp} "LDFLAGS=\"${configure.ldflags} \$\{LDFLAGS\}\""
    puts ${fp} ""

    puts ${fp} "if \[ \"\$\{1\}\" = \"-E\" \] \;then"
        puts ${fp} "    case \$2 in"
        puts ${fp} "        server|capabilities)"
        puts ${fp} "            if \[ \"\$2\" = \"server\" -a \"`basename \$0`\" = \"${cmakewrappername}_noserver\" \] \;then"
        puts ${fp} "                # emulate the error message from an older CMake version"
        puts ${fp} "                (   echo \"CMake Error: cmake version 3.0.1 (faked to avoid server mode)\""
        puts ${fp} "                    echo \"Usage: /opt/local/bin/cmake -E \[command\] \[arguments ...\]\""
        puts ${fp} "                ) 1>&2"
        puts ${fp} "                exit 1"
        puts ${fp} "            fi"
        puts ${fp} "            exec ${configure.cmd} \"\$@\""
        puts ${fp} "            \;\;"
        puts ${fp} "        *)"
        puts -nonewline ${fp} "            exec ${configure.cmd} \"\$@\""
        if {[info exists configure.pre_args]} {
            puts -nonewline ${fp} " [join ${configure.pre_args}]"
        }
        if {[info exists configure.args.orig]} {
            puts -nonewline ${fp} " [join ${configure.args.orig}]"
        }
        if {[info exists configure.post_args]} {
            puts -nonewline ${fp} " [string map [list ${worksrcpath} ""] [join ${configure.post_args}]]"
        }
        puts ${fp} ""
        puts ${fp} "    esac"
    puts ${fp} "elif \[ \"\$\{1\}\" = \"--version\" -a \"`basename \$0`\" = \"${cmakewrappername}_noserver\" \] ;then"
        puts ${fp} "    # emulate the version output from an older CMake version to avoid"
        puts ${fp} "    # both server mode and cmake-file-api mode."
        puts ${fp} "    echo \"CMake Error: cmake version 3.0.1 (faked to avoid server and file-api mode)\""
        puts ${fp} "    echo"
        puts ${fp} "    echo \"CMake suite maintained and supported by Kitware (kitware.com/cmake).\""
        puts ${fp} "    exit 0"
    puts ${fp} "else"
        puts -nonewline ${fp} "    exec ${configure.cmd}"
        if {[info exists configure.pre_args]} {
            puts -nonewline ${fp} " [join ${configure.pre_args}]"
        }
        if {[info exists configure.args]} {
            puts -nonewline ${fp} " [join ${configure.args}]"
        }
        if {[info exists configure.post_args]} {
            puts -nonewline ${fp} " [string map [list ${worksrcpath} ""] [join ${configure.post_args}]]"
        }
        puts ${fp} "\"\$@\""
    puts ${fp} "fi"
    close ${fp}
    reinplace "s|${prefix}|\$\{PREFIX\}|g" ${cmakewrapperfile}
    reinplace "s|@MPPREFIX@|${prefix}|g" ${cmakewrapperfile}
    system "chmod 755 ${cmakewrapperfile}"
    ln -s [file tail ${cmakewrapperfile}] ${cmakewrapperfile}_noserver
}

livecheck.type      regex
livecheck.url       http://download.kde.org/stable/plasma/
livecheck.regex     (5+(\\.\\d+)+)
