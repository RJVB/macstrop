# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem       1.0
PortGroup        github 1.0
PortGroup        legacysupport 1.1
PortGroup        makefile 1.0
PortGroup        conflicts_build 1.0

set LTO_needs_pre_build yes
variant LTO description {build with Link Time Optimisation} {}
PortGroup        LTO 1.0
PortGroup        save_configure_cmd 1.0

# _clock_gettime
legacysupport.newest_darwin_requires_legacy 15

compwrap.add_compiler_flags yes

configure.save_build_cmd install

name             quickjs

categories       devel
license          MIT
maintainers      nomaintainer
description      A small and embeddable Javascript engine
long_description ${name} is a small and embeddable Javascript engine. It \
    supports the ES2023 specification including modules, asynchronous \
    generators, proxies and BigInt.
homepage         https://bellard.org/quickjs/

subport          ${name}-devel {}

if {${subport} eq ${name}} {
    conflicts       ${name}-devel
    github.setup    bellard ${name} 19abf1888db5884a5758036ff6e7fa2b340acedc
    github.tarball_from archive
    version         20250405 ; revision 2
    checksums       rmd160  f7e78f2f04101f01a34fe2e8dc0d58e3d6d06064 \
                    sha256  20e931a015376de4c38609b889016e926ddbf17a741ced24ef741c1c721f9e53 \
                    size    570178
} else {
    # quickjs-devel
    conflicts       ${name}
    github.setup    bellard ${name} f10ef299a6ab4c36c4162cc5840f128f74ec197c
    github.tarball_from archive
    version         20250520 ; revision 2
    checksums       rmd160 b1ac071d6601277d89c1900f8cb7a63e76bd302d \
                    sha256 6e1db4fc459f1eb4fc9f5c2716d02e5d6f6494937f7e24d7c3dd7a1777a62408 \
                    size   590714
    post-extract {
        # `VERSION` is only updated for "real" releases.
        reinplace "s|2025-04-26|${version}|" ${worksrcpath}/VERSION
    }
}

compiler.c_standard  2011
# fatal error: 'stdatomic.h' file not found
compiler.blacklist-append {clang < 700}

# we wrap the compiler, but therefore we need to avoid that this wrapper gets registered in qjsc!
build.args-append   QJSC_CC=${prefix}/libexec/quickjs/qjscc
if {[variant_isset LTO]} {
    build.args-append \
                    CFLAGS_LTO="${LTO.ltoflags}" \
                    CONFIG_LTO=yes
    destroot.args-append \
                    CFLAGS_LTO="${LTO.ltoflags}" \
                    CONFIG_LTO=yes
    test.target-append \
                    CFLAGS_LTO="${LTO.ltoflags}" \
                    CONFIG_LTO=yes
}
build.args-append   AR=${configure.ar}
destroot.args-append \
                    AR=${configure.ar}
test.target-append  AR=${configure.ar}

pre-build {
    if {[variant_isset PGO]} {
        if {[string match *gcc* ${configure.cc}]} {
            # make certain build.env has been completed *before* we start
            makefile_pg::setup_phase build
            ui_msg " ----> PGO training build"
            # build.env gets applied *after* the pre-build, so we have to do it ourselves.
            # Borrowed from portutil:parse_environment().
            foreach assignment ${build.env} {
                set equals_pos [string first = $assignment]
                if {${equals_pos} != -1} {
                    set key [string range $assignment 0 $equals_pos-1]
                    set ::env(${key}) [string range $assignment $equals_pos+1 end]
                    ui_debug "${key}=$::env(${key})"
                }
            }
            system -W ${worksrcpath} \
                "${build.cmd} ${build.pre_args} [join ${build.args}] PGO_GEN=-fprofile-generate ${build.post_args} -j${build.jobs}"
            ui_msg " ----> PGO training runs"
            for {set i 0} {$i < 5} {incr i} {
                ui_debug "Training loop ${i} of 5"
                system -W ${worksrcpath} \
                    "${build.cmd} test microbench [join ${build.args}] PGO_GEN=-fprofile-generate ${build.post_args}"
            }
            ui_msg " ----> PGO optimised build"
            system -W ${worksrcpath} "${build.cmd} -kj${build.jobs} rewind_build"
            build.args-append \
                    PGO_USE="-fprofile-use -fprofile-correction -fprofile-partial-training"
        } else {
            ui_error "${subport} supports +PGO only with a GCC compiler for now!"
            return -code error "Please try again with GCC"
        }
    }
}

pre-destroot {
    destroot.env-append {*}${build.env}
    if {[variant_isset PGO]} {
        if {[string match *gcc* ${configure.cc}]} {
            destroot.args-append \
                    PGO_USE="-fprofile-use -fprofile-correction -fprofile-partial-training"
        }
    }
}

variant PGO description {build with Profile Guided Optimisation} {}
if {[variant_isset PGO]} {
# for GCC: -fprofile-generate / -fprofile-use -fprofile-correction -fprofile-partial-training
    if {![string match *gcc* ${configure.cc}]} {
        ui_warn "Currently, +PGO requires a GCC compiler!"
    } else {
        # don't allow qjsc to use an installed copy of libquickjs!
        conflicts_build-append \
                    ${subport}
    }
}

build.post_args     -wk
build.env-append    QJSCC=[compwrap::wrap_compiler cc]
destroot.env-append QJSCC=[compwrap::wrap_compiler cc]

# without this linking to legacysupport is broken
if {${os.platform} eq "darwin" && ${os.major} < 16} {
    configure.cflags-append \
                    -D_DARWIN_C_SOURCE -isystem${prefix}/include/LegacySupport

    # See https://github.com/bsekisser/quickjs/commit/c35e6bf
    if {[string match *gcc* ${configure.compiler}]} {
            patchfiles-append \
                    patch-darwin-gcc.diff
            configure.ldflags-append \
                    -latomic
    } elseif {[string match *clang* ${configure.compiler}]} {
            patchfiles-append \
                    patch-darwin-clang.diff
    }
    post-patch {
        reinplace "s|@PREFIX@|${prefix}|g" ${worksrcpath}/qjsc.c
    }
}

patchfiles-append   patch-makefile-use-our-compopts.diff \
                    patch-add-QJSCC-env_var-and-PGO_GEN.diff

destroot.destdir PREFIX=${destroot}${prefix}

test.target-append  microbench stats
test.run            yes
test.env-append     QJSCC=[compwrap::wrap_compiler cc]

post-destroot {
    file mkdir ${destroot}${prefix}/lib/pkgconfig
    copy ${filespath}/${name}.pc ${destroot}${prefix}/lib/pkgconfig
    reinplace "s|@@PREFIX@@|${prefix}|" ${destroot}${prefix}/lib/pkgconfig/${name}.pc
    reinplace "s|@@VERSION@@|${version}|" ${destroot}${prefix}/lib/pkgconfig/${name}.pc

    file mkdir ${destroot}${prefix}/libexec/quickjs
    xinstall [compwrap::wrap_compiler cc] ${destroot}${prefix}/libexec/quickjs/qjscc
    if {[variant_isset LTO] && [string match *fat-lto* ${LTO.ltoflags}]} {
        if {[file exists ${destroot}${prefix}/lib/quickjs/libquickjs.lto.a]} {
            # we've built a static archive with fat objects, so we don't need the
            # version WITHOUT LTO information.
            ln -sf libquickjs.lto.a ${destroot}${prefix}/lib/quickjs/libquickjs.a
        }
    }
}
