# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           muniversal 1.0
PortGroup           LTO 1.0

name                openssl
epoch               1
version             1.0.2r ; revision 4
# distname            openssl-${version}

# Please revbump these ports when updating OpenSSL.
#  - freeradius (#43461)
#  - openssh (#54990)

categories          devel security
platforms           darwin
license             OpenSSL SSLeay
maintainers         {larryv @larryv} {cal @neverpanic} openmaintainer

description         OpenSSL SSL/TLS cryptography library
long_description    The OpenSSL Project is a collaborative effort to \
                    develop a robust, commercial-grade, full-featured, \
                    and Open Source toolkit implementing the Secure \
                    Sockets Layer (SSL v2/v3) and Transport Layer \
                    Security (TLS v1) protocols as well as \
                    a full-strength general purpose cryptography \
                    library.
homepage            https://www.openssl.org

if {${name} eq "openssl"} {
    conflicts       libressl libressl-devel
}

####################
if {${os.platform} ne "darwin"} {
    PortGroup devport 1.0

    create_devport port:${name}

    if {${subport} eq "${name}-dev"} {
        # openssl-dev doesn't care about anything that follows,
        # return here to avoid having to add more subport checks.
        return
    }
}
####################

depends_lib         port:zlib

# See https://www.openssl.org/source/mirror.html
master_sites        ${homepage}/source \
                    ftp://gd.tuwien.ac.at/infosys/security/openssl/source/ \
                    http://mirror.switch.ch/ftp/mirror/openssl/source/ \
                    ftp://ftp.fi.muni.cz/pub/openssl/source/ \
                    ftp://ftp.pca.dfn.de/pub/tools/net/openssl/source/ \
                    http://artfiles.org/openssl.org/source/ \
                    ftp://ftp.linux.hr/pub/openssl/source/ \
                    ftp://guest.kuria.katowice.pl/pub/openssl/source/

checksums           sha1    b9aec1fa5cedcfa433aed37c8fe06b0ab0ce748d \
                    rmd160  f268c8f87ee6b8ca1523761b064de575f6851ae0 \
                    sha256  ae51d08bba8a83958e894946f15303ff894d75c2b8bbd44a852b64e3fe11d0d6
#                     size    5348369

if {${os.platform} eq "darwin"} {
    patchfiles      install-headers-HFS+.patch \
                    parallel-building.patch \
                    remove-duplicate-bn_print-doc.patch 
    configure.perl  /usr/bin/perl
    configure.cmd   ./Configure
} else {
    patchfiles      parallel-building.patch
    configure.cmd   ./config
}

# configure.ccache    no
configure.args     -L${prefix}/lib \
                    no-krb5 \
                  --openssldir=${prefix}/etc/openssl \
                  --libdir=lib/openssl-1.0 \
                    shared \
                    zlib
# Use SDK if necessary.
if {${configure.sdkroot} ne ""} {
    configure.args-append   '-isysroot ${configure.sdkroot}' \
                            -Wl,-syslibroot,${configure.sdkroot}
}

platform darwin {
    set merger_arch_compiler no
    array set merger_configure_args {
        ppc     darwin-ppc-cc
        i386    darwin-i386-cc
        ppc64   darwin64-ppc-cc
        x86_64  darwin64-x86_64-cc
    }
    # Don't use i386 assembly on Tiger (#38015, #43303).
    if {${os.major} <= 8} {
        append merger_configure_args(i386) { no-asm}
    }
    # Don't use x86-64 assembly on Tiger or Leopard.
    if {${os.major} <= 9} {
        append merger_configure_args(x86_64) { no-asm}
    }
}
# Don't pass --host to configure.
array set merger_host {ppc {} i386 {} ppc64 {} x86_64 {}}

if {![variant_isset universal]
        && [info exists merger_configure_args(${configure.build_arch})]} {
    configure.args-append $merger_configure_args(${configure.build_arch})
}
configure.universal_args-delete --disable-dependency-tracking

# Parallel builds don't quite work (#46719).
use_parallel_build  no

post-configure {
    # splice in our build options
    reinplace "s|^CFLAG=.*|& ${configure.cflags}|g" ${worksrcpath}/Makefile
    reinplace "s|^SHARED_LDFLAGS=.*|& ${configure.ldflags}|g" ${worksrcpath}/Makefile
}

test.run            yes

if {[variant_isset universal]} {
    pre-destroot {
        global merger_dont_diff
        if {[llength ${universal_archs_to_use}] > 2} {
            lappend merger_dont_diff ${prefix}/include/openssl/opensslconf.h
        }
    }
}

post-destroot {
    # Move some files around
    xinstall -d ${destroot}${prefix}/include/openssl-1.0
    move ${destroot}${prefix}/include/openssl ${destroot}${prefix}/include/openssl-1.0/
    # add a symlink that will make it possible to replace `#include <openssl/foo.h>` with
    # `#include <openssl10/foo.h>` in ports that require openssl 1.0 but pick up headers
    # from openssl 1.1 despite a `-I $prefix/include/openssl-1.0` directive.
    ln -s openssl-1.0/openssl ${destroot}${prefix}/include/openssl10
    move ${destroot}${prefix}/bin/openssl ${destroot}${prefix}/bin/openssl-1.0

    # Update includedir in .pc files
    reinplace "s|/include$|/include/openssl-1.0|" {*}[glob ${destroot}${prefix}/lib/openssl-1.0/pkgconfig/*.pc]

    if {${name} eq "openssl10"} {
        file delete -force ${destroot}${prefix}/etc
        file delete ${destroot}${prefix}/bin/c_rehash
    } else {
        # temporary config; make the "hidden" files available where they're expected
        # by current dependents (until they're rebuilt)
        ln -s openssl-1.0/openssl ${destroot}${prefix}/include/openssl
        ln -s openssl-1.0 ${destroot}${prefix}/bin/openssl
        foreach pc [glob ${destroot}${prefix}/lib/openssl-1.0/pkgconfig/*.pc] {
            ln -s ../openssl-1.0/pkgconfig/[file tail ${pc}] ${destroot}${prefix}/lib/pkgconfig/
        }
        if {${os.platform} eq "darwin"} {
            ln -s openssl-1.0/libcrypto.1.0.0.dylib ${destroot}${prefix}/lib/
            ln -s openssl-1.0/libssl.1.0.0.dylib ${destroot}${prefix}/lib/
            # these should be temporary:
            ln -s openssl-1.0/libcrypto.dylib ${destroot}${prefix}/lib/
            ln -s openssl-1.0/libssl.dylib ${destroot}${prefix}/lib/
        } else {
            ln -s openssl-1.0/libcrypto.so.1.0.0 ${destroot}${prefix}/lib/
            ln -s openssl-1.0/libssl.so.1.0.0 ${destroot}${prefix}/lib/
            # these should be temporary:
            ln -s openssl-1.0/libcrypto.so ${destroot}${prefix}/lib/
            ln -s openssl-1.0/libssl.so ${destroot}${prefix}/lib/
        }
    }

    if {[info procs create_devport_content_archive] ne ""} {
        register_devport_standard_content
        devport_content-append ${prefix}/include/openssl10
        devport_content-append ${prefix}/lib/openssl-1.0/libssl.so
        devport_content-append ${prefix}/lib/openssl-1.0/libcrypto.so
        devport_content-append ${prefix}/lib/openssl-1.0/libssl.a
        devport_content-append ${prefix}/lib/openssl-1.0/libcrypto.a
        foreach h [glob -nocomplain ${destroot}${prefix}/lib/openssl-1.0/pkgconfig/*] {
            devport_content-append [string map [list ${destroot} ""] ${h}]
        }
        create_devport_content_archive
    }
}

destroot.destdir    INSTALL_PREFIX=${destroot}
if {${name} eq "openssl10"} {
    destroot.target install_sw
} else {
    destroot.args   MANDIR=${prefix}/share/man MANSUFFIX=ssl
}

variant rfc3779 description {enable RFC 3779: X.509 Extensions for IP Addresses and AS Identifiers} {
    configure.args-append   enable-rfc3779
}

livecheck.type      regex
livecheck.url       [lindex ${master_sites} 0]
livecheck.regex     ${name}-(\[0-9.\]+\[a-z\]?)\\.tar\\.gz
