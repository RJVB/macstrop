# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem                  1.0
PortGroup                   clang_dependency 1.0
PortGroup                   conflicts_build 1.0
PortGroup                   legacysupport 1.1
PortGroup                   meson 1.0
PortGroup                   muniversal 1.0
PortGroup                   locale_select 1.0
set LTO.gcc_lto_jobs        auto
PortGroup                   LTO 1.0
PortGroup                   compress_workdir 1.0

# Please keep the glib2 and glib2-devel ports as similar as possible.

name                        glib2
conflicts                   glib2-devel glib2-upstream
set my_name                 glib
version                     2.78.4 ; revision 1
epoch                       1

checksums                   rmd160  7941be85af18c428d86f4a00e52f41542b061aa9 \
                            sha256  24b8e0672dca120cc32d394bccb85844e732e04fe75d18bb0573b2dbc7548f63
#                             size    5331892

set branch                  [join [lrange [split ${version} .] 0 1] .]
categories                  devel
maintainers                 {mascguy @mascguy} openmaintainer
license                     LGPL-2+
homepage                    https://wiki.gnome.org/Projects/GLib
platforms                   darwin
dist_subdir                 glib2
distname                    ${my_name}-${version}
use_xz                      yes
use_parallel_build          yes

meson.save_configure_cmd    "install log"

description                 Library with data structure functions and other constructs
long_description            Glib is a library which includes support routines \
                            for C, such as lists, trees, hashes, memory \
                            allocation, and many other things.

master_sites                gnome:sources/${my_name}/${branch}/

# Disable unexpected download of subprojects
meson.wrap_mode             nodownload

# Needed for clock_gettime
legacysupport.newest_darwin_requires_legacy 15

patchfiles-append           libintl.patch
patchfiles-append           patch-gio-tests-meson.build.diff
patchfiles-append           patch-glib_gmem.c.diff
patchfiles-append           patch-glib_gspawn.c.diff
patchfiles-append           patch-glib_gunicollate.c.diff
patchfiles-append           patch-gio_xdgmime_xdgmime.c.diff
patchfiles-append           patch-get-launchd-dbus-session-address.diff
patchfiles-append           patch-gmodule-gmodule-dl.c.diff
patchfiles-append           patch-meson_build-meson_options-appinfo.diff
patchfiles-append           patch-meson-build-python-path.diff
patchfiles-append           patch-meson_build-atomic-test-older-clang-versions.diff
patchfiles-append           universal.patch
patchfiles-append           patch-glib2-findfolders-before-SL.diff
patchfiles-append           patch-declarations.diff
patchfiles-append           no-distutils.patch

# RJVB: try to avoid aborting when a gsettings key isn't found
patchfiles-append           patch-gsettings-tweak.diff
platform linux {
	# machine-id is installed at the system-level!
	patchfiles-append      patch-gio_gdbusprivate.c.diff
}

platform darwin {
    if {${os.major} < 11} {
        patchfiles-append   patch-gio_gcredentialsprivate.h.diff
        patchfiles-append   patch-gio_gcredentials.c.diff
        patchfiles-append   patch-gio_gsocket.h.diff
    }

    if {${os.major} == 10 && ${build_arch} eq "ppc"} {
        patchfiles-replace \
                            patch-glib2-findfolders-before-SL.diff \
                            patch-glib2-findfolders-before-Lion.diff
    }
}

depends_build-append        port:gettext \
                            bin:xmllint:libxml2 \
                            path:bin/pkg-config:pkgconfig

set python_versions         {3.7 3.8 3.9 3.10 3.11 3.12}

proc set_python {dv} {
    global py_ver py_ver_nodot
    set py_ver              ${dv}
    set py_ver_nodot        [string map {. {}} ${py_ver}]
    depends_build-append    port:python${py_ver_nodot}
    depends_run-append      port:python${py_ver_nodot}
    depends_skip_archcheck-append \
                            python${py_ver_nodot}
}

set variant_set             no
foreach v ${python_versions} {
    set py_ver_nodot        [string map {. {}} ${v}]
    variant python${py_ver_nodot} description "build using Python${v}" {}
    if {[variant_isset python${py_ver_nodot}]} {
	   set_python ${v}
        set variant_set yes
    }
}
if {![tbool variant_set]} {
    default_variants        +python39
    # cannot be avoided?!
    set_python              3.9
}

depends_lib-append          port:gettext-runtime \
                            port:libelf \
                            port:libffi \
                            port:libiconv \
                            port:pcre2 \
                            port:zlib

# Python only needed for scripts
depends_skip_archcheck-append \
                            python${py_ver_nodot}
platform linux {
    depends_build-append    port:gettext-dev \
                            port:libiconv-dev
    #configure.env-append    "LIBS=-L${prefix}/lib -lintl"
    #build.env-append        "LIBS=-L${prefix}/lib -lintl"
}

compiler.cxx_standard       2011

# Issues for Clang 5.x:
# - fatal error: error in backend: Cannot select: 0x103357f10: i8,ch = AtomicLoad 0x10334b410,
#   0x103354b10<Volatile LD1[@is_running.b]> [ID=18]
# - ERROR: Problem encountered: GLib requires a C++ compiler with support for C99 __VA_ARG__ in macros.
# Issues for Clang 6.x:
# - gcocoanotificationbackend.c:115:52: error: array subscript is not an integer
# - Checking if "GCC size_t typedef is long" compiles: NO
compiler.blacklist-append   {clang < 700}

# https://bugzilla.gnome.org/show_bug.cgi?id=697017
compiler.blacklist-append   gcc-3.3 *gcc-4.0 *gcc-4.2

if {${os.platform} eq "darwin"} {
    # -lresolv is needed at least on Tiger PPC.
    configure.ldflags-append \
                            -lresolv \
                            -bind_at_load
} else {
    # avoid having to declare a build conflict with ourselves:
    # (glib-compile-resources: symbol lookup error: /opt/local/var/lnxports/build/_opt_local_site-ports_devel_glib2/glib2/work/build/gio/glib-compile-resources: undefined symbol: g_string_free_and_steal)
    configure.ldflags-delete \
                            -L${prefix}/lib
}

configure.cflags-append     -fstrict-aliasing

# stop excessive warnings
configure.cflags-append     -Wno-deprecated-declarations
configure.objcflags-append  -Wno-deprecated-declarations

configure.args-append       -Ddefault_library=shared \
                            -Dlibelf=enabled \
                            -Dwarning_level=0

variant static description {build static libraries too} {}
if {[variant_isset static]} {
    configure.args-replace  -Ddefault_library=shared \
                            -Ddefault_library=both
}

if {${os.platform} ne "darwin"} {
#     configure.args-append   -Diconv=external
} else {
    configure.args-append   -Dlibmount=disabled
}
configure.args-append       -Dgtk_doc=false

configure.perl              /usr/bin/perl
configure.python            ${prefix}/bin/python${py_ver}
configure.env-append        PERL_PATH=${configure.perl}

if {${universal_possible} && [variant_isset universal]} {
    foreach my_arch {ppc ppc64 i386 x86_64 arm64} {
        # strip the automatic setting of host, meson does not accept
        set merger_host(${my_arch}) ""
    }
}

post-patch {
    reinplace -W ${worksrcpath} "s|@PYTHON@|${configure.python}|" \
        meson.build

    if {${os.platform} eq "darwin"} {
        reinplace -W ${worksrcpath} "s|@PREFIX@|${prefix}|g" \
            gio/xdgmime/xdgmime.c \
            glib/gi18n-lib.h \
            glib/gi18n.h \
            gio/gdbusaddress.c
    } else {
        reinplace -W ${worksrcpath} "s|@PREFIX@|${prefix}/share:/usr/local|g" \
            gio/xdgmime/xdgmime.c
        reinplace -W ${worksrcpath} "s|@PREFIX@|${prefix}|g" \
            glib/gi18n-lib.h \
            glib/gi18n.h 
        reinplace -W ${worksrcpath} "s|@PREFIX@||g" \
            gio/gdbusprivate.c \
            gio/gdbusaddress.c
    }

    reinplace -W ${worksrcpath} "s|data_dirs = \"/usr|data_dirs = \"${prefix}/share:/usr|g" \
        glib/gutils.c

    reinplace -W ${worksrcpath} "s|path = \"/bin|path = \"${prefix}/bin:/bin|g" \
        glib/gutils.c \
        glib/gspawn.c

    # Upstream mixes three variations of Python shebangs, so account for all
    fs-traverse f ${worksrcpath} {
        if {[string match *.py ${f}]} {
            ui_info "patching testpath: ${f}"
            reinplace -q "s|/usr/bin/python3$|${configure.python}|" ${f}
            reinplace -q "s|/usr/bin/env python$|${configure.python}|" ${f}
            reinplace -q "s|/usr/bin/env python3$|${configure.python}|" ${f}
        }
    }
}

if {${os.platform} ne "darwin"} {
    build.env-append LD_LIBRARY_PATH=${build.dir}/glib
}

# this edit does not, for some reason, apply against the build-arm64 config.h
# and should not be needed if we're using the muniversal PortGroup
# needs testing on 32 bit systems to be certain of this
#post-configure {
#    system "ed - ${build.dir}/config.h < ${filespath}/config.h.ed"
#}

build.args-append           --verbose
build.pre_args-append       -k 0

post-build {
    if {[variant_exists universal] && [variant_isset universal]} {
        set dirs {}
        foreach arch ${universal_archs_to_use} {
            lappend dirs ${workpath}/build-${arch}
        }
    } else {
        set dirs ${workpath}/build
    }
    foreach dir ${dirs} {
        # -lm is spuriously added by meson NYD to some builds (arm64)
        reinplace -q {s| -lm||g}              ${dir}/meson-private/glib-2.0.pc
    }
}

# test.args-append            --verbose
test.run                    yes
test.target                 --verbose test

post-destroot {
    delete ${destroot}${prefix}/lib/charset.alias

    set docdir ${prefix}/share/doc/${my_name}
    xinstall -d ${destroot}${docdir}
    xinstall -m 0644 -W ${worksrcpath} COPYING NEWS README.md \
        ${destroot}${docdir}
    if {![file exists ${destroot}${prefix}/include/gio-unix-2.0/gio/gdesktopappinfo.h]} {
	   ui_msg "installing missing gdesktopappinfo.h file"
        xinstall -m 644 ${worksrcpath}/gio/gdesktopappinfo.h ${destroot}${prefix}/include/gio-unix-2.0/gio/gdesktopappinfo.h
    }
}

post-activate {
    if {[file exists /usr/share/glib-2.0/schemas]} {
        if {[catch {system "${prefix}/bin/glib-compile-schemas /usr/share/glib-2.0/schemas"} result]} {
            ui_warning "glib-compile-schemas failed on /usr/share/glib-2.0/schemas: ${result}"
        }
    }
    if {[catch {system "${prefix}/bin/glib-compile-schemas ${prefix}/share/glib-2.0/schemas"} result]} {
        ui_warning "glib-compile-schemas failed on ${prefix}/share/glib-2.0/schemas: ${result}"
    }
}

platform darwin {
    configure.args-append   -Ddtrace=false
    
    # some tests fail to build on 10.6.8 Rosetta: https://trac.macports.org/ticket/65517
    if {${os.major} == 10 && ${build_arch} eq "ppc"} {
        configure.args-append   -Dtests=false
    }
}

platform darwin 8 {
    # https://trac.macports.org/ticket/67307
    configure.cflags-append -D__DARWIN_NON_CANCELABLE=1

    # the @loader_path linkages in the test binaries can't find the dylibs
    configure.args-append   -Dtests=false

    # meson on Tiger cannot use rpaths, so we workaround with this to find dylibs
    foreach my_phase {build test destroot} {
        ${my_phase}.env-append  "DYLD_LIBRARY_PATH=${build_dir}/glib:${build_dir}/gobject:${build_dir}/gio:${build_dir}/gthread:${build_dir}/gmodule"
    }
}

variant quartz conflicts x11 {
    configure.args-append   -Dappinfo_backend=native
}

variant x11 conflicts quartz {
    if {${os.platform} eq "darwin"} {
        configure.args-append   -Dappinfo_backend=generic
    } else {
        configure.args-append   -Dappinfo_backend=native
    }
}

if {${os.platform} ne "darwin" || ![variant_isset quartz]} {
    default_variants +x11
}
if {![variant_isset x11]} {
    default_variants +quartz
}
if {![variant_isset quartz] && ![variant_isset x11]} {
    pre-configure {
        return -code error "Either +x11 or +quartz is required"
    }
}

variant docs description {Install the gtk-doc documentation for glib2} {}
if {![variant_isset docs]} {
    post-destroot {
        file delete -force ${destroot}${prefix}/share/gtk-doc
    }
} else {
    configure.args-replace  -Dgtk_doc=false \
                            -Dgtk_doc=true
}

variant install_tests description {Install the built test binaries} {}
if {[variant_isset install_tests]} {
    configure.args-append  -Dinstalled_tests=true
} else {
    configure.args-append  -Dinstalled_tests=false
}

proc port_conflict_check {p_port_name p_conflict_ver_min p_conflict_ver_max} {
    ui_debug "port_conflict_check: Checking for conflict against port: ${p_port_name}"

    if { ![catch {set port_conflict_ver_info [lindex [registry_active ${p_port_name}] 0]}] } {
        set port_conflict_ver [lindex ${port_conflict_ver_info} 1]_[lindex ${port_conflict_ver_info} 2]
        ui_debug "port_conflict_check: ${p_port_name} active version: ${port_conflict_ver}"

        if { [vercmp ${port_conflict_ver} ${p_conflict_ver_min}] >= 0
            && [vercmp ${port_conflict_ver} ${p_conflict_ver_max}] <= 0 } {

            ui_debug "port_conflict_check: ${p_port_name} conflicts; declare build conflict"
            conflicts_build-append \
                ${p_port_name}
        } else {
            ui_debug "port_conflict_check: ${p_port_name} does not conflict"
        }
    }
}

# If the user happens to have `gobject-introspection` 1.80.x installed, force them to
# deactivate it first. Otherwise, the newer version can cause other unexpected failures
# if still active on downgrade.
# Logic added 2024-04-17; keep in place for at least 12 months.
proc gobj_conflict_check {} {
    set gobj_port_name "gobject-introspection"
    set gobj_port_names [list ${gobj_port_name} "${gobj_port_name}-devel"]
    set gobj_ver_min "1.80.1_0"
    set gobj_ver_max "1.80.1_0"

    foreach p ${gobj_port_names} {
        port_conflict_check ${p} ${gobj_ver_min} ${gobj_ver_max}
    }
}

gobj_conflict_check

livecheck.type              gnome
livecheck.name              ${my_name}
