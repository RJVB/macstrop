# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0

name                qt5-creator

if { ${subport} eq "${name}-docs" || ${subport} eq "${name}-examples" } {
    universal_variant no
    supported_archs   noarch
}

set qt5.prefer_kde  1
PortGroup           qmake5 1.0
# this is just to keep portindex happy:
PortGroup           locale_select 1.0
# universal build hasn't been tested (20150410)
# universal_variant   no
# optional support for HFS-compression of the source and build dirs
if {[file exists ${qt5::currentportgroupdir}/compress_workdir-1.0.tcl]} {
    PortGroup       compress_workdir 1.0
    compress.in_applications_dir-append \
                    ${qt_apps_dir}/Qt-Creator.app
}

if {${os.platform} ne "darwin"} {
    langselect_qm_dir \
                    ${prefix}/share/qtcreator/translations
    langselect_qm_basename \
                    qtcreator_
}

categories          devel aqua
platforms           darwin linux
license             GPL-3
maintainers         gmail.com:rjvbertin openmaintainer
description         Cross-platform integrated development environment (IDE) tailored to the needs of Qt developers.
long_description    Qt Creator is a cross-platform integrated development environment (IDE) tailored to the needs of Qt developers.
installs_libs       no

homepage            http://wiki.qt.io/Category:Tools::QtCreator

subport ${name}-mac-devel {
    replaced_by "${name}-devel"
    PortGroup   obsolete 1.0
}
subport ${name}-mac {
    replaced_by "${name}"
    PortGroup   obsolete 1.0
}

variant legacy description {Provides a version that still builds against Qt 5.3.2, and thus on OS X 10.6} {}

qt5.depends_component \
                        qtscript \
                        qtdeclarative \
                        qttools \
                        qtmacextras \
                        qtquickcontrols \
                        qtsvg

subport ${name}-devel {
    conflicts           ${name} qt5-qtcreator
    set use_git         yes
    if {${use_git}} {
        fetch.type      git
        git.url         git://code.qt.io/qt-creator/qt-creator.git
        if {[variant_isset legacy]} {
            git.branch  3a8b52d68cb16aac27be531f4dd55ff816f71081
            # no idea what to set this to ... this commit was between 3.4.1 and 3.4.2 :
            set qbs.branch  760dc4ef7571e780489adcd51ddb80beb000bd21
            version         3.4.2-1.g3a8b52d
        } else {
            # The last QC and qbs versions that build against Qt 5.9:
            git.branch      66fd5abe583629cc2609e0724f0912e449e9d569
            # corresponding qbs branch obtained from `git pull --recurse-submodules ; git submodule update`:
            # (v1.13.0-30-gfe9e48d1d):
            set qbs.branch  fe9e48d1d939a2bf98a110be5efccc55ebdcbd4f
            # rjvb-4.9.82-166-g66fd5abe58 (after `git tag rjvb-4.9.82 -a -m "tag 4.9.82" 523ddee6`)
            version         4.9.82.166 ; revision 2
            # don't let QtCreator set up a global Mac-style menubar when using the xcb plugin
            # also disables icons-in-menus.
            patchfiles-append \
                        devel/patch-show-menubar-with-xcb.diff \
                        devel/patch-clang-fixes-devel.diff \
                        devel/patch-add-missing-theme-colour.diff
            if {[string match *gcc* ${configure.compiler}]} {
                # turns out GCC (v13 at least) needs c++17 instead of c++14
                # also disables the QML profiler.
                patchfiles-append \
                        devel/patch-force-c++17.diff
            }
            patchfiles-append \
                        devel/patch-setwindowmodified-everywhere.diff \
                        devel/patch-fix-un,indent-features.diff
        }
        distname        qt-creator-opensource-src-git
        fetch {
            # I don't see any provisions for git submodules in MacPorts, so we have to clone by hand:
            if {![file exists ${workpath}/${worksrcdir}]} {
                ui_debug "git clone --recursive ${git.url} ${workpath}/${worksrcdir}"
                system "${prefix}/bin/git clone --recursive -q ${git.url} ${workpath}/${worksrcdir}"
            }
            ui_debug "checking out main branch ${git.branch}"
            system "cd ${workpath}/${worksrcdir} ; ${prefix}/bin/git checkout -q ${git.branch}"
        }
    }
    if {![variant_isset legacy]} {
        # do not opportunistically use QtWebengine
        patchfiles-append \
                        devel/patch-no_qtwebengine.diff \
                        devel/patch-no-nativehelp-xcb.diff \
                        devel/patch-compat-109.diff
        platform darwin {
            if {${os.major} < 15} {
                patchfiles-append \
                        devel/patch-notouchbar.diff
            }
        }
        # prevent on-exit-crashing due to a race-condition
        patchfiles-append \
                        devel/patch-txtedit--no-exitcrashing.diff
        patchfiles-append \
                        devel/patch-ksyntaxhighlighting-5.60.0.diff
        compiler.cxx_standard 2017
        if {[string match *g++* ${configure.cxx}]} {
            configure.cxxflags-append -std=gnu++17
        }
    }

    variant qtwebengine conflicts qtwebkit legacy description {build with QtWebengine support} {
        patchfiles-delete  devel/patch-no_qtwebengine.diff
        qt5.depends_component \
                        qtwebengine
    }

    if {[variant_isset legacy]} {
        set lldversions {5.0 6.0}
    } else {
        set lldversions {5.0 6.0 8.0 9.0 12}
    }
    # define the variants for the supported clang/llvm versions
    foreach ldv ${lldversions} {
        set llv [join [lrange [split ${ldv} .] 0 1] ""]
        set conflist ""
        # construct the list of variants the current variant conflicts with
        foreach v ${lldversions} {
            if {${v} ne ${ldv}} {
                set v [join [lrange [split ${v} .] 0 1] ""]
                set conflist "${conflist} llvm${v}"
            }
        }
        variant llvm${llv} conflicts ${conflist} description "Use LLVM/Clang ${ldv} for the Clang Code Model plugin" {}
    }
    # find the selected (active) variant
    foreach ldv ${lldversions} {
        set llv [join [lrange [split ${ldv} .] 0 1] ""]
        if {[variant_isset llvm${llv}]} {
            # gotcha, do what we have to do
            # (add depspec, set env. variables for configure and build steps)
            depends_lib-append \
                port:llvm-${ldv} port:clang-${ldv}
            if {${ldv} < 6.0} {
                ui_warn "Build against LLVM 6.0 or better to get full functionality"
                patchfiles-append \
                    devel/patch-clang5-kludge.diff
            } elseif {${ldv} >= 8} {
                patchfiles-append \
                    devel/patch-build-with-llvm8.diff
            }
            if {${ldv} >= 9} {
                patchfiles-append \
                    devel/patch-build-with-llvm9.diff
            }
            if {${ldv} >= 12} {
                patchfiles-append \
                    devel/patch-build-with-llvm12.diff
                post-patch {
                    if {${os.platform} eq "darwin"} {
                        # libLLVM.dylib is required for `___isPlatformVersionAtLeast`
                        reinplace "s|@LLVM_LIB@|${prefix}/libexec/llvm-${ldv}/lib/libLLVM.dylib|g" \
                            ${worksrcpath}/src/tools/clangpchmanagerbackend/clangpchmanagerbackend.pro \
                            ${worksrcpath}/src/tools/clangrefactoringbackend/clangrefactoringbackend.pro \
                            ${worksrcpath}/src/plugins/clangformat/clangformat.pro
                    } else {
                        # a priori not needed elsewhere
                        reinplace "s|@LLVM_LIB@||g" \
                            ${worksrcpath}/src/tools/clangpchmanagerbackend/clangpchmanagerbackend.pro \
                            ${worksrcpath}/src/tools/clangrefactoringbackend/clangrefactoringbackend.pro \
                            ${worksrcpath}/src/plugins/clangformat/clangformat.pro
                    }
                }
            }
            configure.env-append \
                LLVM_INSTALL_DIR=${prefix}/libexec/llvm-${ldv}
            build.env-append \
                LLVM_INSTALL_DIR=${prefix}/libexec/llvm-${ldv}
        }
    }

}
#else
if { ${subport} ne "${name}-devel" } {
    # for now.
    replaced_by qt5-qtcreator
    PortGroup   obsolete 1.0

    conflicts       ${name}-devel qt5-qtcreator
    # double-check: 3.4.2 should still build against Qt 5.3.2, i.e. on OS X 10.6.8
    if {[variant_isset legacy]} {
        version     3.4.2
        checksums   rmd160  944ee0982776e580330ad75e9fea0470a33e2b73 \
                    sha256  f57df9954768badb252154a51cca9b21a4903f8e003ef944d25801cde1754bab
    } else {
        version     4.1.0
        checksums   rmd160  aad5f1096cf038b23969c6677cb4094515eaaa48 \
                    sha256  412c0223dbfd86983b39e10e2c7cb42b530995cfe2f3aa3529ea3f1b00ffb403
        patchfiles-append \
                    patch-show-menubar-with-xcb-4.1.0.diff
    }
    master_sites    https://download.qt.io/official_releases/qtcreator/[join [lrange [split ${version} .] 0 1] .]/${version}/

    distname        qt-creator-opensource-src-${version}
    livecheck.type  regex
    livecheck.url   http://www.qt.io/download-open-source/
    livecheck.regex qt-creator-opensource-src-(\[a-z0-9.\]+)${extract.suffix}

    # do not opportunistically use QtWebkit
    patchfiles-append      patch-no_qtwebkit.diff

    if {![variant_isset legacy]} {
        # do not opportunistically use QtWebengine
        patchfiles-append  patch-no_qtwebengine.diff
    }

    variant qtwebkit conflicts qtwebengine description {build with QtWebkit support} {
        patchfiles-delete  patch-no_qtwebkit.diff
        qt5.depends_component \
                            qtwebkit
    }
    default_variants        +qtwebkit

    variant qtwebengine conflicts qtwebkit legacy description {build with QtWebengine support} {
        patchfiles-delete  patch-no_qtwebengine.diff
        qt5.depends_component \
                            qtwebengine
    }

}

if {[variant_isset legacy]} {
    patchfiles-append \
                    legacy/patch-no-dockmenu-xcb.diff \
                    legacy/patch-tweak-menus.diff
} else {
    # don't set up a Dock menu when using the xcb plugin
    patchfiles-append \
                    patch-no-dockmenu-xcb.diff
    if {${subport} eq "${name}"} {
        # icons-in-menus already disabled for the devel subport
        patchfiles-append \
                    patch-no-icons-in-menus.diff
    }
}

if {${os.platform} eq "darwin"} {
    set appname "Qt Creator.app"
} else {
    set appname qtcreator
}

# shouldn't be needed anymore
#destroot.destdir    INSTALL_ROOT=${destroot}${prefix}

variant docs description {Include the documentation} {
    build.target-append \
                    docs
    destroot.target-append \
                    install_docs
}

if { ${subport} eq ${name} || ${subport} eq "${name}-devel" } {
    if {${os.platform} eq "darwin"} {
        depends_lib-append     port:botan
    }
    depends_lib-append      port:qbs

    configure.args-append  "USE_SYSTEM_BOTAN=1"
    if {[variant_isset LTO]} {
        build.cmd               ${prefix}/bin/gmake
        depends_build-append    port:gmake
    }

    post-fetch            {
        file mkdir ${workpath}/build
        if {(${subport} eq "${name}-devel") && ${use_git}} {
            catch {eval exec sh -c "\"cd ${workpath}/${worksrcdir} ; git submodule update --init\""} result
            ui_debug "git submodule -init: $result"
            if {[file exists ${filespath}/QC-git/.git]} {
                reinplace -W ${workpath}/${worksrcdir} "s|qt-labs/qbs.git|files/QC-git/src/shared/qbs|g" .git/config
            }
            catch {eval exec sh -c "\"cd ${workpath}/${worksrcdir} ; git submodule update\""} result
            ui_debug "git submodule: $result"
            ui_debug "checking out QBS branch ${qbs.branch}"
            system "cd ${workpath}/${worksrcdir}/src/shared/qbs ; ${prefix}/bin/git checkout -q master"
            system "cd ${workpath}/${worksrcdir}/src/shared/qbs ; ${prefix}/bin/git pull -q"
            system "cd ${workpath}/${worksrcdir}/src/shared/qbs ; ${prefix}/bin/git checkout -q ${qbs.branch}"
        }
    }

    configure.args-append   QBS_INSTALL_DIR=${prefix} \
                            -r ../${worksrcdir}/qtcreator.pro
    configure.env-append    QTC_ENABLE_CLANG_LIBTOOLING=1
    build.env-append        QTC_ENABLE_CLANG_LIBTOOLING=1

    default configure.dir   {${workpath}/build}
    default build.dir       {${workpath}/build}
    # Qt Creator has a different way of interpreting INSTALL_ROOT:
    destroot.destdir        "INSTALL_ROOT=${destroot}${prefix}"

    if { ![variant_isset universal] } {
        if {${os.platform} eq "darwin"} {
            destroot {
                # make install attempts to install command line tools into /bin, etc.
                xinstall -m 755 -d ${destroot}${qt_apps_dir}
                copy "${build.dir}/bin/${appname}" "${destroot}${qt_apps_dir}"
          }
        }
    } else {
        # We should really just override destroot, but the muniversal Portgroup would override our override.
        destroot.cmd "true"
        merger-post-destroot {
            foreach arch ${universal_archs_to_use} {
                set adest ${destroot}-${arch}${qt_apps_dir}
                xinstall -m 755 -d ${adest}
                copy "${worksrcpath}-${arch}/bin/${appname}" "${adest}"
            }
        }
    }
    post-destroot {
        set QCAppBundle "${qt_apps_dir}/Qt Creator.app"
        if {[variant_isset docs]} {
            system -W ${build.dir} "${build.cmd} install_docs ${destroot.destdir}"
            platform darwin {
                file delete -force "${destroot}${prefix}/Qt Creator.app"
            }
            xinstall -m 755 -d ${destroot}${qt_docs_dir}/html
#             foreach d [glob -nocomplain ${build.dir}/doc/*] {
#                 copy ${d} ${destroot}${qt_docs_dir}/html/
#             }
            foreach d [glob -nocomplain ${destroot}${QCAppBundle}/Contents/Resources/doc/*.qch] {
                file rename ${d} ${destroot}${qt_docs_dir}
            }

        }
        if {${os.platform} eq "darwin"} {
            if {![info exists keep_languages]} {
                if {[file exists ${prefix}/etc/macports/locales.tcl]
                    && [file exists ${destroot}${QCAppBundle}/Contents/Resources/translations]} {
                    if {[catch {source "${prefix}/etc/macports/locales.tcl"} err]} {
                        ui_error "Error reading ${prefix}/etc/macports/locales.tcl: $err"
                        return -code error "Error reading ${prefix}/etc/macports/locales.tcl"
                    }
                }
            }
            if {[info exists keep_languages]} {
                foreach l [glob -nocomplain ${destroot}${QCAppBundle}/Contents/Resources/translations/*.qm] {
                    set langcomps [split [file rootname [file tail ${l}]] _]
                    set simplelang [lindex ${langcomps} end]
                    set complang [join [lrange ${langcomps} end-1 end] _]
                    if {[lsearch -exact ${keep_languages} ${simplelang}] ne "-1"} {
                        ui_debug "won't delete ${l} (${simplelang})"
                    } elseif {[lsearch -exact ${keep_languages} ${complang}] ne "-1"} {
                        ui_debug "won't delete ${l} (${complang})"
                    } else {
                        ui_debug "rm ${l}"
                        file delete -force ${l}
                    }
                }
            }
            xinstall -m 755 -d ${destroot}${QCAppBundle}/Contents/libexec
            ln -s ${prefix}/libexec/qbs ${destroot}${QCAppBundle}/Contents/libexec/qbs
        }

        if {${os.platform} eq "darwin"} {
            # get rid of the space in the bundle name
            file rename ${destroot}${QCAppBundle} ${destroot}${qt_apps_dir}/Qt-Creator.app

            qt5.add_app_wrapper qtcreator5 Qt-Creator "Qt Creator"
            qt5.wrapper_env_additions "QT_QPA_PLATFORM=xcb QT_XCB_GL_INTEGRATION=xcb_egl ## This requires port:qt5-kde-x11 or qt5-kde-devel-x11"
            qt5.add_app_wrapper qtcreator5-remote Qt-Creator "Qt Creator"
            reinplace "s|\"\$@\"|-noload Welcome \"\$@\"|g" ${destroot}${prefix}/bin/qtcreator5-remote

            if {[file exists "${destroot}/Qt Creator.app"]} {
                file delete -force "${destroot}/Qt Creator.app"
            }
        } else {
            qt5.add_app_wrapper qtcreator5 qtcreator "" ${prefix}/bin
            qt5.wrapper_env_additions "QT_QPA_PLATFORM=xcb"
            qt5.add_app_wrapper qtcreator5-remote qtcreator "" ${prefix}/bin
            reinplace "s|\"\$@\"|-noload Welcome \"\$@\"|g" ${destroot}${prefix}/bin/qtcreator5-remote
        }
    }

    build.post_args-append -wk
}

subport ${name}-docs {
    replaced_by ${name} +docs
    PortGroup   obsolete 1.0
}

subport ${name}-examples {
    depends_lib-append port:${name}
    build           {}
    destroot.dir    ${worksrcpath}/src/shared/qbs
    destroot.target -f Makefile.static INSTALL_ROOT=${destroot}${prefix} install_examples
}

if {[info procs qmake5.save_configure_cmd] ne ""} {
    qmake5.save_configure_cmd "install log"
}

# kate: backspace-indents true; indent-pasted-text true; indent-width 4; keep-extra-spaces true; remove-trailing-spaces modified; replace-tabs true; replace-tabs-save true; syntax Tcl/Tk; tab-indents true; tab-width 4;
