# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id: Portfile 122038 2014-07-13 11:29:03Z nicos@macports.org $

PortSystem          1.0
PortGroup           kde4   1.1

# optional support for HFS-compression of the source and build dirs
if {[file exists ${cmake::currentportgroupdir}/compress_workdir-1.0.tcl]} {
    PortGroup       compress_workdir 1.0
}

fetch.type          git
git.url             git://anongit.kde.org/kdelibs

# v4.14.38-3-g2c3762fedd
git.branch          2c3762feddf7e66cf6b64d9058f625a715694a00

name                kdelibs4
version             4.14.38.3
distname            ${name}-4.14.git
categories          kde kde4
maintainers         nicos
license             LGPL-2+ GPL-2+ BSD
description         KDE4 core libraries, git/master
long_description    Core libraries for KDE4 applications to function. \
                    This port follows git/master
platforms           darwin
homepage            http://www.kde.org
master_sites        kde:stable/${version}/src/
use_xz              yes

test.run            yes

## no longer true with the OpenSSL 1.1 patch:
# #No binary links to openssl libraries, apart from libkio.dylib at
# #runtime if required. As libkio.dylib code is purely LGPL, there is no
# #conflict with openssl license
# license_noconflict  openssl

depends_build-append port:flex port:gmake port:docbook-xsl-ns

# dropped grantlee dependency because it's optional and used only for 1 test
depends_lib-append  port:bzip2 port:zlib \
                    port:soprano port:cyrus-sasl2 \
                    port:strigi port:gettext \
                    port:pcre port:shared-mime-info \
                    lib:libgif:giflib port:tiff \
                    path:include/turbojpeg.h:libjpeg-turbo port:libpng \
                    port:jasper port:openexr \
                    port:expat port:libart_lgpl \
                    port:libidn port:libiconv \
                    path:lib/pkgconfig/glib-2.0.pc:glib2 \
                    path:lib/libssl.dylib:openssl port:enchant \
                    port:aspell port:aspell-dict-en \
                    port:attica port:xz \
                    port:avahi port:qca \
                    port:libxml2 port:libxslt \
                    port:dbusmenu-qt port:docbook-xml \
                    port:docbook-xsl-nons \
                    port:kerberos5 port:bison \
                    port:shared-desktop-ontologies \
                    port:perl5

# the aspell dictionaries are just files
depends_skip_archcheck aspell-dict-en

conflicts           kdelibs3

#workaround-kdeinit4-crash: Mac-specific patch developed by Macports to circumvent some crashes.
#avoid-kwindowinfo-destructor: Prevent sending null pointer to CFRelease (see ticket #44258)
#default-kde4-xdg-menu-prefix: solves a conflict with a file from gnome-menus (see ticket #18914)
#add-bundles-to-path: improve support for KIO slaves by adding a search path
#kapplications-raster: ensures that kde applications start in raster (faster) mode, but also provides a switch for non-working applications
#removeFindFlex: remove FindFlex.cmake which may hide the working one of cmake (ticket #44119)
#patch-cmake-modules-FindKDE4-Internal.cmake: Fixes zlib detection (see ticket #24128)
#patch-cmake-modules-KDE4Macros.cmake: Ensures that compiled objects are properly detected during build (see ticket #43720)

patchfiles          workaround-kdeinit4-crash.patch \
                    default-kde4-xdg-menu-prefix.patch \
                    add-bundles-to-path.patch \
                    patch-kapplications-raster.diff \
                    patch-removeFindFlex.diff \
                    patch-cmake-modules-FindKDE4-Internal.cmake.diff \
                    patch-cmake-modules-FindMySQL.cmake.diff

# RJVB
# fixCrashInKCrash.patch
patchfiles-append    fixInvokeBrowser.patch \
                     fixReportBug.patch \
                     streamline-systray-menu.patch \
                     correct-Preferences-Menu.patch \
                     nogui-patches.patch \
                     patch-improved-osx-keychain.diff \
                     patch-kdecore-tests.diff \
                     patch-backtrace-demangle-4osx.diff \
                     patch-kwindowsystem-4osx.diff \
                     patch-Display-typedef.diff \
                     patch-restore-prevactive-after-notification.diff \
                     patch-kmenu-addtitle.diff \
                     patch-kioslave-as-agent.diff \
                     patch-launch-lldb-not-gdb.diff \
                     patch-kidletime-poller.diff \
                     patch-solid-backend-iokit.diff \
                     patch-simulate-full_session.diff \
                     patch-mimetyperepo.diff \
                     patch-revert-a02df05e.diff \
                     patch-catch-fileopen.diff \
                     patch-aqua-widget-alignment.diff \
                     kdelibs-openssl-1.1.patch


variant nativefiledialogs description {File dialogs are native by default.\
                    Set Native=false under [KFileDialog Settings] in kdeglobalrc to use KDE dialogs} {}

if { [variant_isset nativefiledialogs] } {
    patchfiles-append  native-filedialogs.patch
}

variant unifiedtoolbar description {Add a unified title and tool bar} {}

if { [variant_isset unifiedtoolbar] } {
    patchfiles-append  unified-toolbar.patch
}

patchfiles-append   patch-no-concurrent-meinproc4.diff

# make sure findSoprano.cmake doesn't get confused because of moved soprano headers
patchfiles-append   patch-findsoprano-cmake.diff

patch.pre_args      -p1

# explicitly define certain headers and libraries, to avoid
# conflicts with those installed into system paths by the user.
configure.args-append   -DWITH_ENCHANT=ON \
                        -DWITH_Soprano=ON \
                        -DWITH_Avahi=ON \
                        -DKDE_DEFAULT_HOME=Library/Preferences/KDE \
                        -DWITH_HSPELL=OFF \
                        -DWITH_FAM=OFF \
                        -DKDE4_AUTH_BACKEND_NAME="OSX"

# Install the kdelibs headerfiles in their own directory to prevent clashes with KF5 headers
configure.args-append   -DINCLUDE_INSTALL_DIR=${kde4.include_dirs}

variant osxkeychain description {kwallet uses the OSX KeyChain} {}

if { [variant_isset osxkeychain] } {
    configure.args-append -DMAC_USE_OSXKEYCHAIN:BOOL=ON
}

variant nostrip description {Prevent stripping of binaries} {}

if { [variant_isset nostrip] } {
#     configure.args-delete -DCMAKE_BUILD_TYPE=Release
#     configure.args-append -DCMAKE_BUILD_TYPE:STRING=RelWithDebInfo -DCMAKE_STRIP:FILEPATH=/bin/echo
    ui_warn "you have requested an obsolete variant"
}

if { [file exists ${destroot}${prefix}/lib/kde4/kspell_aspell.so] } {
    notes "
Only the English dictionary has been installed.
You will need to install extra dictionaries to
get support for spell checking other languages.
The command 'port search aspell-dict' will show
you the currently available dictionary options.
"
}

set startup_root ""

post-patch {
    reinplace "s|%PREFIX%|${prefix}|g" \
        ${worksrcpath}/cmake/modules/FindMySQL.cmake
    # patch a number of other hardcoded paths to refer to ${prefix}, using a direct approach
    reinplace "s|/usr/local|${prefix}|g" \
        ${worksrcpath}/kdecore/kernel/kstandarddirs.cpp \
        ${worksrcpath}/kdecore/network/k3socks.cpp \
        ${worksrcpath}/kio/kio/ksambashare.cpp \
        ${worksrcpath}/kio/kssl/kopenssl.cpp \
        ${worksrcpath}/solid/solid/xdgbasedirs.cpp
    reinplace "s|@PREFIX@|${prefix}|g" \
        ${worksrcpath}/kdecore/services/kmimetyperepository.cpp
    reinplace "s|/usr/share|${prefix}/share|g" \
        ${worksrcpath}/kdeui/icons/kiconcache.cpp \
        ${worksrcpath}/kdeui/icons/kiconloader.cpp \
        ${worksrcpath}/kdeui/icons/kicontheme.cpp \
        ${worksrcpath}/sonnet/plugins/hunspell/kspell_hunspelldict.cpp \
        ${worksrcpath}/sonnet/plugins/hunspell/kspell_hunspellclient.cpp
}

pre-configure {
    #The FindFlex.cmake file is not working and may conflict with the one provided by cmake (ticket #44119)
    if {[file exists ${worksrcpath}/cmake/modules/FindFlex.cmake]} {
        delete file ${worksrcpath}/cmake/modules/FindFlex.cmake
    }
}
post-configure {
    catch {eval exec sh -c "\"printenv > ${workpath}/macports.env\""} result
    ui_debug "printenv: $result"
}

pre-activate {
    if {![catch {set vers [lindex [registry_active kde4-runtime] 0]}]
        && [vercmp [lindex $vers 1] 4.8.1] < 0} {
        registry_deactivate_composite kde4-runtime "" [list ports_nodepcheck 1]
    }
}

post-destroot {
    xinstall -d ${destroot}${prefix}/include/nepomuk
    eval xinstall -m 644 [glob ${worksrcpath}/nepomuk/core/*.h] \
        ${destroot}${prefix}/include/nepomuk
    eval xinstall -m 644 [glob ${worksrcpath}/nepomuk/types/*.h] \
        ${destroot}${prefix}/include/nepomuk
    eval xinstall -m 644 [glob ${worksrcpath}/nepomuk/nepomuk_export.h] \
        ${destroot}${prefix}/include/nepomuk

    xinstall -d -m 755 ${destroot}${prefix}/Library/LaunchAgents
    xinstall -m 644 ${filespath}/org.macports.kdecache.plist \
        ${destroot}${prefix}/Library/LaunchAgents
    reinplace "s|%PREFIX%|${prefix}|g" \
        ${destroot}${prefix}/Library/LaunchAgents/org.macports.kdecache.plist
    reinplace "s|%APPLICATIONS_DIR%|${applications_dir}|g" \
        ${destroot}${prefix}/Library/LaunchAgents/org.macports.kdecache.plist

    xinstall -m 755 ${filespath}/kdeinit4.sh ${destroot}${prefix}/bin/kdeinit4
    reinplace "s|@KDEAPPDIR@|${applications_dir}/KDE4|g" ${destroot}${prefix}/bin/kdeinit4

    # The startupitem.install keyword does not exist in MacPorts < 2.1.
    if {![info exists startupitem.install]} {
        set startupitem.install yes
    }
    if { ${startupitem.install} != "no" } {
        xinstall -d -m 755 ${destroot}${startup_root}/Library/LaunchAgents
        ln -s ${prefix}/Library/LaunchAgents/org.macports.kdecache.plist ${destroot}${startup_root}/Library/LaunchAgents
    }

    # The KDE4 PortGroup provides support for a (kf5compat) variant that installs to a legacy_prefix.
    # We provide a symlink that will allow ports to build using this "shadow prefix" (the legacy_prefix)
    # and then move the share directory back into the "real" ${prefix} ... but still find certain
    # resources at runtime. The reason for not leaving them in the legacy_prefix is that other XDG-compliant
    # software (including KDE ports not built with the legacy_prefix) will expect them in ${prefix}/share,
    # not in ${kde4.legacy_prefix}/share.
    xinstall -m 755 -d ${destroot}${kde4.legacy_prefix}
    ln -s ${prefix}/share ${destroot}${kde4.legacy_prefix}/share
}

post-activate {
    if { ![variant_isset no_root] } {
        file attributes ${prefix}/Library/LaunchAgents/org.macports.kdecache.plist -owner root -group wheel 
    }
}

notes-append "An agent to provide communication between KDE programs must be started.
To start it run the following command:
 launchctl load -w ${startup_root}/Library/LaunchAgents/org.macports.kdecache.plist"

variant no_root description {Run the kde cache agent as MacPorts install user.} {
    pre-fetch {
        if { ${install.user}=="root" || ${install.group}=="wheel" } {
            ui_error "The kde cache agent should not be run as root with no_root variant."
            error "Please do not use this variant with your MacPorts configuration."
        }
    }

    set startup_root  [join [lrange [exec /usr/bin/dscl . -read Users/${install.user} NFSHomeDirectory] 1 end]]
    # Files are installed into user's startup directory.
    destroot.violate_mtree  yes
}

post-activate {
    if {[info exists kde4::currentportgroupdir]} {
        set pgroup_files {"kde4-1.1.tcl"}
        ui_msg "Don't forget to copy the following file(s) into your main port tree's _resources directory:"
        foreach pg ${pgroup_files} {
            ui_msg "\t${kde4::currentportgroupdir}/${pg}"
        }
        ui_msg "(That's probably ${prefix}/var/macports/sources/rsync.macports.org/release/ports/_resources/port1.0/group"
        ui_msg "or ${prefix}/var/macports/sources/svn.macports.org/trunk/dports/_resources/port1.0/group)"
    }
}

#livecheck.url       http://kde.mirrors.hoobly.com/stable/
#livecheck.regex     (\\d+(\\.\\d+)+)
